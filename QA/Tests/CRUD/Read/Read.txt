
*** Settings ***
| Documentation 	| Verify that the Read pages for Integration Point objects have the fields and sections for given situations.
| Metadata			| Authors	| Daniel Oliver	| William Sehy
| Force Tags		| in-progress	| team.method
| Suite Setup		| Suite Setup Keyword
| Test Setup		| View Integration Point	| ${IntegrationPoint1}
| Test Teardown 	| Capture Screenshot if Test Fails
| Suite Teardown	| Delete the Objects created in the Suite then Close All Browsers


*** Variables ***

| ${WorkspaceName}		| LDAP Sync
| ${IntegrationPoint1}	| Integration Point 1	| # Enable Scheduler is set to No
| ${IntegrationPoint2}	| Integration Point 2	| # Daily Schedule
| ${ConnectionPath}		| testing.corp/CN=Users,DC=testing,DC=corp
| ${ObjectFilterString}	| (objectClass=*)
| ${Authentication}		| FastBind
| ${ConnectionUsername}	| testing\\administrator
| ${ConnectionPassword}	| P@ssw0rd@1
| ${ImportNestedItems}	| Yes


*** Test Cases ***

| The Integration Point Read page defaults to the correct Layout
| | [Documentation]	| Verify that the default Layout on an Integration Point Read page is "Integration Point Details". 
| | ...				| 
| | [Tags]			| automated
| | Verify that the layout dropdown is set to "Integration Point Details"


| The Integration Point Read page has the correct Layouts
| | [Documentation]	| Verify that the Integration Point Read page has only the "Integration Point Details" Layout. 
| | ...				| 
| | [Tags]			| automated
| | Verify that the Layout Dropdown only has the following Layouts	| Integration Point Details


| The correct Buttons appear in the Header
| | [Documentation]	| Verify that the correct buttons appear at the top of the page.
| | ...				| [id] 117f284f-a89f-11e4-ab56-b8ca3a83048f
| | [Tags]			| automated
| | Verify the buttons are available on the layout	| 
| | ... | Edit				| 
| | ... | Delete			| 
| | ... | Back				| 
| | ... | Edit Permissions	| 
| | ... | View Audit


| The Category Headings are in the correct order
| | [Documentation]	| Verify that the Category headings are sorted correctly on the Integration Point Details" Layout.
| | ...				| [id] 1180fd11-a89f-11e4-b0e4-b8ca3a83048f
| | [Tags]			| automated
| | Verify that the current Layout has the following Categories in Order	| 
| | ... | General	| 
| | ... | Status


| The correct Fields are present when Enable Scheduler is set to No
| | [Documentation]	| Verify that the correct Fields are present on the "Integration Point Details" Layout when Enable Scheduler is set to No.
| | ...				| [id] 1180fd12-a89f-11e4-a95c-b8ca3a83048f
| | [Tags]			| manual	| field_placement
| | Run the following manual steps:
| | ... | Expected Results:
| | ... | 1. Verify that the General Category has the following fields in order:	| 
| | ... | - Name			| 
| | ... | - Overwrite		| 
| | ... | - Source Provider	| 
| | ... | - Destination RDO
| | ... | 2. Verify that the Status Category has the following fields in order:	| 
| | ... | - Enable Scheduler				|
| | ... | - Next Scheduled Runtime (UTC)	| 
| | ... | - Last Runtime (UTC)


| The correct Fields are present when Enable Scheduler is set to Yes
| | [Documentation]	| Verify that the correct Fields are present on the "Integration Point Details" Layout when Enable Scheduler is set to Yes.
| | ...				| [id] 11817242-a89f-11e4-82d7-b8ca3a83048f
| | [Tags]			| manual	| field_placement
| | [Setup]			| View Integration Point	| ${IntegrationPoint2}
| | Run the following manual steps:
| | ... | Expected Results:
| | ... | 1. Verify that the General Category has the following fields in order:	| 
| | ... | - Name			| 
| | ... | - Overwrite		| 
| | ... | - Source Provider	| 
| | ... | - Destination RDO
| | ... | 2. Verify that the Status Category has the following fields in order:	| 
| | ... | - Enable Scheduler				|
| | ... | - Next Scheduled Runtime (UTC)	| 
| | ... | - Last Runtime (UTC)


*** Keywords ***

| Verify that the current Layout has the following Categories in Order
| | [Documentation]	| This keyword verifies that the given Categories are visible on the current Layout in the given order.
| | [ARGUMENTS]		| @{ExpectedCategories}
| | Element Should Be Visible	| id=_dynamicTemplate__kCuraScrollingDiv__dynamicViewFieldRenderer_span	| No Categories were found on the current Layout.
| | @{ActualCategories}=	| Get Element Text	| id=_dynamicTemplate__kCuraScrollingDiv__dynamicViewFieldRenderer_span	| False
| | Log	| Expected Categories: ${ExpectedCategories}
| | Log	| Actual Categories: ${ActualCategories}
| | Lists Should Be Equal	| ${ExpectedCategories}	| ${ActualCategories}

| Verify that the Layout Dropdown only has the following Layouts
| | [Documentation]	| This keyword verifies that the Layout dropdown has only the given Layouts in any order.
| | ...				| This keyword assumes that the Layout dropdown's name is "_dynamicTemplate$ctl03".
| | [ARGUMENTS]		| @{ExpectedLayouts}
| | Element Should Be Visible	| css=select[name='_dynamicTemplate$ctl03']	| The Layout dropdown was not found.
| | @{ActualLayouts}=	| Get Element Text	| css=select[name='_dynamicTemplate$ctl03'] > option	| False
| | Sort List	| ${ExpectedLayouts}
| | Sort List	| ${ActualLayouts}
| | Lists Should Be Equal	| ${ExpectedLayouts}	| ${ActualLayouts}

| Suite Setup Keyword
| | [Documentation]	| Create the Suite Preconditions
| | Setup connection to the relativity database
| | Open and Log in to Method
| | Setup connection to the Relativity Services API for Method	| ${WorkspaceName}
| | Enable Strict Mode for Layouts	| # Makes 'Verify the current layout has the following fields' ensure that only the given fields are present

# --------------------------------------------------------------------------------------------------------

| | @{Step1Fields}=		| Create List	| 
| | ... | ${IntegrationPoint1}	| 
| | ... | Append/Overlay		| 
| | ... | LDAP					| 
| | ... | Custodian				| 
| | ... | No

| | @{Step2Fields}=		| Create List	| 
| | ... | ${ConnectionPath}		| 
| | ... | ${ObjectFilterString}	| 
| | ... | ${Authentication}		| 
| | ... | ${ConnectionUsername}	| 
| | ... | ${ConnectionPassword}	| 
| | ... | ${ImportNestedItems}

| | @{Step3Mapping}=	| Create List	| 
| | ... | Email			| mail			| 
| | ... | Department	| countrycode	| 
| | ... | First Name	| givenname		| 
| | ... | Last Name		| sn			| 
| | ... | UniqueID		| objectguid

| | ${IntegrationPointExists}=	| Determine if the Following Integration Point Exists in the current Workspace	| ${IntegrationPoint1}
| | Run Keyword If	| ${${IntegrationPointExists}}	| Delete the following Integration Point using API	| ${IntegrationPoint1}
| | Create Integration Point | ${Step1Fields} | ${Step2Fields} | ${Step3Mapping} | UniqueID

# --------------------------------------------------------------------------------------------------------

| | @{Step1Fields}=	| Create List	| 
| | ... | ${IntegrationPoint2}	| 
| | ... | Append/Overlay		| 
| | ... | LDAP					| 
| | ... | Custodian				| 
| | ... | Yes					| 
| | ... | Daily					| 
| | ... | NULL					| 
| | ... | NULL					| 
| | ... | 2/15/2025				| 
| | ... | 2/16/2025				| 
| | ... | 12:30 PM

| | ${IntegrationPointExists}=	| Determine if the Following Integration Point Exists in the current Workspace	| ${IntegrationPoint2}
| | Run Keyword If	| ${${IntegrationPointExists}}	| Delete the following Integration Point using API	| ${IntegrationPoint2}
| | Create Integration Point | ${Step1Fields} | ${Step2Fields} | ${Step3Mapping} | UniqueID
| | [Teardown]		| Drop connection to the relativity services api

| Delete the Objects created in the Suite then Close All Browsers
| | [Documentation]	| Delete the Integration Points created in this suite then closes all open browsers.
| | ...				| This is run as the Suite Teardown.
| | Setup connection to the Relativity Services API for Method	| ${WorkspaceName}

| | Delete the following Integration Point using API	| ${IntegrationPoint1}
| | Delete the following Integration Point using API	| ${IntegrationPoint2}

| | Drop connection to the relativity services api
| | Close All Browsers


*** Settings ***
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${KEYWORD_DIR}/Relativity/Processing/Processing/Relativity_Keywords_General.txt
| Resource	| ${KEYWORD_DIR}/Relativity/Shared/Layouts/LayoutKeywords.txt
| Resource	| ${KEYWORD_DIR}/Relativity/Shared/RelativityKeywords.txt
| Resource	| ${METHOD_KEYWORD_DIR}/MiscMethodKeywords.txt
| Resource	| ${METHOD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| C:/SourceCode/LDAPSync/QA/Keywords/DatabaseKeywords.txt
| Resource	| C:/SourceCode/LDAPSync/QA/Keywords/IntegrationPointKeywords.txt