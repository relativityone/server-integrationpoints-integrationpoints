
*** Settings ***
| Documentation 	| Verify that the Connect to Source step of Integration Points field validation works correctly.
| Metadata		    | Authors	| Daniel Oliver
| Force Tags		| in-progress	| team.game_of_holds	| feature.integration_points
| Suite Setup		| Open and Log in to Method
| Test Setup        | Go to Step 2 of the Create Integration Point Process
| Test Teardown 	| Capture screenshot if test fails
| Suite Teardown	| Close all browsers


*** Variables ***

| ${WorkspaceName}			| LDAP Sync
| ${ValidConnectionPath}	| testing.corp
| ${InvalidConnectionPath}	| testing.cor
| ${Connection Path Field}	| connectionPath
| ${Authentication Field}	| authentication


*** Test Cases ***

| A Blank Connection Path Field shows a message after clicking Next
| | [Documentation]	| Verify that when the Connection Path Field is blank the correct warning message is shown when clicking Next.
| | ...				| [id] 102e1740-a89f-11e4-9647-b8ca3a83048f
| | [Tags]			| automated
| | Fill out the Following Fields of the LDAP Integration Point Step 2 form	| Authentication=FastBind
| | Click	| Next
| | Verify the following Integration Point Field has the following Error	| ${Connection Path Field}	| This field is required.


| Proceeding Post-blank Connection Path required field message
| | [Documentation]	| Verify that the next screen can be reached after the Connection Path required field message is shown.
| | ...				| [id] 10332051-a89f-11e4-9be6-b8ca3a83048f
| | [Tags]			| automated
| | [Setup]			| Require Test Case	| A Blank Connection Path Field shows a message after clicking Next
| | Fill out the Following Fields of the LDAP Integration Point Step 2 form	| ConnectionPath=${ValidConnectionPath}
| | Proceed to the Next Step of the Integration Point wizard
| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that you are on the following Step of the Create Integration Point Process	| Map Fields


| A Blank Authentication Field shows a message after clicking Next
| | [Documentation]	| Verify that when the Authentication Field is blank the correct warning message is shown when clicking Next.
| | [Tags]			| automated
| | Fill out the Following Fields of the LDAP Integration Point Step 2 form	| ConnectionPath=${ValidConnectionPath}
| | Click	| Next
| | Verify the following Integration Point Field has the following Error	| ${Authentication Field}	| This field is required.


| Proceeding Post-blank Authentication required field message
| | [Documentation]	| Verify that the next screen can be reached after the Authentication required field message is shown.
| | [Tags]			| automated
| | [Setup]			| Require Test Case	| A Blank Authentication Field shows a message after clicking Next
| | Fill out the Following Fields of the LDAP Integration Point Step 2 form	| Authentication=FastBind
| | Proceed to the Next Step of the Integration Point wizard
| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that you are on the following Step of the Create Integration Point Process	| Map Fields


| Invalid Connection message is displayed when unable to connect using the given values
| | [Documentation]	| Verify that the "Unable to connect to source using the specified settings" warning message is displayed after clicking Next while a Connection Path is specified.
| | ...				| [id] 10332053-a89f-11e4-9135-b8ca3a83048f
| | [Tags]			| automated
| | Fill out the Following Fields of the LDAP Integration Point Step 2 form	| ConnectionPath=${InvalidConnectionPath}	| Authentication=FastBind
| | Click	| Next
| | Wait Until Keyword Succeeds	| 30s	| 2s	| Verify the current Method Page Error has the following Text  | Unable to connect to source using the specified settings.


| Proceeding Post-blank Invalid Connection message
| | [Documentation]	| Verify that the next screen can be reached after the Invalid Connection message is shown.
| | ...				| [id] 10332054-a89f-11e4-a009-b8ca3a83048f
| | [Tags]			| required	| automated
| | [Setup]			| Require Test Case	| Invalid Connection message is displayed when unable to connect using the given values
| | Fill out the Following Fields of the LDAP Integration Point Step 2 form	| ConnectionPath=${ValidConnectionPath}
| | Proceed to the Next Step of the Integration Point wizard
| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that you are on the following Step of the Create Integration Point Process	| Map Fields


| After Proceeding Post-blank Invalid Connection message the Error Message is cleared
| | [Documentation]	| Verify that getting to the next screen after the Invalid Connection message is shown clears the Invalid Connection message.
| | [Tags]			| automated
| | [Setup]			| Require Test Case	| Proceeding Post-blank Invalid Connection message
| | Verify that no Method Page Error is present on the page


*** Keywords *** 

| Go to Step 2 of the Create Integration Point Process
| | Open Create Integration Point
| | Fill out the Following Fields of the Integration Point Step 1 form	| Step 2 Testing	| Append/Overlay	| LDAP	| Custodian	| No
| | Proceed to the Next Step of the Integration Point wizard

| Verify the following Integration Point Field does not have the following Error
| | [Documentation]	| Verifies that the given field does not have the given error.
| | ... 			| Must be called from the Integration Point Create/Edit pages.
| | [ARGUMENTS] 	| ${FieldID}	| ${ErrorText}
| | ${FieldID}=	| fetch from right	| ${FieldID}	| s2id_
| | Element Should Not Be Visible	| xpath=//*[@id='${FieldID}']/following-sibling::div/child::*[contains(@class, 'icon-error') and contains(@class, 'field-validation-error')]	| An error message was found on the field with the ID "${FieldID}".
| | ${ActualText}=	| Get Text	| xpath=//*[@id='${FieldID}']/following-sibling::div/child::*[contains(@class, 'icon-error') and contains(@class, 'field-validation-error')]
| | Run Keyword Unless	| '${ActualText}' == '${ErrorText}'	| Fail	| The error was incorrect. It should have been "${ErrorText}" but it was actually "${ActualText}".


*** Settings ***
| Library	| Collections
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${KEYWORD_DIR}/LegalHold/Legal_Hold_Keywords_API.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/FormKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/MiscMethodKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/IntegrationPointKeywords.txt
