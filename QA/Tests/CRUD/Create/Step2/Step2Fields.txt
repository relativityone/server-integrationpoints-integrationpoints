
*** Settings ***
| Documentation 	| Verify that the Connect to Source step (Step 2) of the Integration Point creation process shows the correct fields and data.
| Metadata			| Authors	| Daniel Oliver	| William Sehy
| Force Tags		| in-progress	| team.game_of_holds	| feature.integration_points
| Suite Setup		| Open and Log in to Method
| Test Setup		| Go to Step 2 of the Create Integration Point Process
| Test Teardown 	| Capture Screenshot If Test Fails
| Suite Teardown	| Close All Browsers


*** Variables ***

| ${WorkspaceName}	| LDAP Sync
| ${Connection Path}			| connectionPath
| ${Object Filter String}		| connectionFilter
| ${Authentication Dropdown}	| s2id_authentication
| ${Username Field}				| connectionUsername
| ${Password Field}				| connectionPassword
| ${Import Nested Items}		| enable	| # the Radio Button Group's Name


*** Test Cases ***

| The Source Connection section's fields are in the correct order
| | [Documentation]	| Verify that the fields in the Source Connection section are sorted correctly.
| | ...				| [id] 1057bf4f-a89f-11e4-96af-b8ca3a83048f
| | [Tags]			| automated
| | Verify that the following Card contains the following fields	| Source Connection	| 
| | ... | Connection Path:		| 
| | ... | Object Filter String:	| 
| | ... | Authentication:		| 
| | ... | Username:				| 
| | ... | Password:				| 
| | ... | Import Nested Items:


| The Authentication list has the correct values
| | [Documentation]	| Verify that the Overwrite List has the correct options
| | ...				| [id] 105d1680-a89f-11e4-878a-b8ca3a83048f
| | [Tags]			| automated	
| | Click	| ${Authentication Dropdown}
| | Verify that the currently opened select2 Dropdown has the following choices in order	| 
| | ... | Select...				| 
| | ... | Anonymous				| 
| | ... | FastBind				| 
| | ... | Secure Socket Layer


| The Authentication list's default value is Select...
| | [Documentation]	| Verify that the Source Provider Field's default value is "Select...".
| | ...				| [id] 105d1681-a89f-11e4-8b43-b8ca3a83048f
| | [Tags]			| automated
| | Verify that the following select2 Dropdown currently has the following value selected	| ${Authentication Dropdown}	| Select...


| The Import Nested Items options are Yes and No
| | [Documentation]	| Verify that the Import Nested Items radio button options are Yes and No.
| | ...				| [id] 105d1682-a89f-11e4-9d3e-b8ca3a83048f
| | [Tags]			| automated
| | Wait Until Keyword Succeeds	| 10s	| 2s	| 
| | ... | Verify that the following radio buttons are grouped together | ${Import Nested Items}  | Yes | No


| Import Nested Items default value is No
| | [Documentation]	| Verify that the Import Nested Items field's default value is No (false).
| | ...				| [id] 105d1683-a89f-11e4-9f71-b8ca3a83048f
| | [Tags]			| automated
| | Radio Button Should Be Set To	| ${Import Nested Items}	| false


| The correct fields have the Required Field Indicator
| | [Documentation]	| Verify that the Required Field indicator is present for the correct fields.
| | ...				| [id] 105d1684-a89f-11e4-80a4-b8ca3a83048f
| | [Tags]			| automated
| | Run Keyword And Continue On Failure	| Verify that the following field is required		| Connection Path:
| | Run Keyword And Continue On Failure	| Verify that the following field is not required	| Object Filter String:
| | Run Keyword And Continue On Failure	| Verify that the following field is required		| Authentication:
| | Run Keyword And Continue On Failure	| Verify that the following field is not required	| Username:
| | Run Keyword And Continue On Failure	| Verify that the following field is not required	| Password:
| | Run Keyword And Continue On Failure	| Verify that the following field is not required	| Import Nested Items:


| The Source Connection Fields are of the correct type
| | [Documentation]	| Verify that the Set Integration Details Fields are the correct type
| | ...				| [id] 105d64a1-a89f-11e4-b2f7-b8ca3a83048f
| | [Tags]			| automated
| | Run Keyword And Continue On Failure	| Verify that the following field ID has a text input		| ${Connection Path}
| | Run Keyword And Continue On Failure	| Verify that the following field ID has a text input		| ${Object Filter String}
| | Run Keyword And Continue On Failure	| Verify that the following field ID has a droplist input	| ${Authentication Dropdown}
| | Run Keyword And Continue On Failure	| Verify that the following field ID has a text input		| ${Username Field} 
| | Run Keyword And Continue On Failure	| Verify that the following field ID has a password input	| ${Password Field}
| | # Import Nested Items is checked in the next test case.


*** Keywords *** 

| Go to Step 2 of the Create Integration Point Process
| | Open Create Integration Point
| | Fill out the Following Fields of the Integration Point Step 1 form | Step 2 Testing | Append/Overlay | LDAP | Custodian | no
| | Proceed to the Next Step of the Integration Point wizard
| | Wait for then Select iFrame	| id=configurationFrame	| 10s	| The page was not on Step 2 of the Integration Point form.


*** Settings ***
| Library   | Collections
#| Library	| ${KEYWORD_DIR}/ManualKeywords.py
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${KEYWORD_DIR}/LegalHold/Legal_Hold_Keywords_API.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/FormKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/MiscLegalHoldKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource  | ${LDAP_KEYWORD_DIR}/IntegrationPointKeywords.txt
