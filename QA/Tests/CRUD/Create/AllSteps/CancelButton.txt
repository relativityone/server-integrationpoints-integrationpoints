
*** Settings ***
| Documentation 	| Verify that the Cancel button on the Integration Point Wizard has the correct functionality.
| Metadata		    | Authors	| William Sehy
| Force Tags		| in-progress	| team.game_of_holds	| feature.integration_points
| Suite Setup		| Suite Setup Keyword
#| Test Setup		| NONE
| Test Teardown 	| Capture Screenshot If Test Fails
| Suite Teardown	| Delete any Objects created in the Suite then Close All Browsers


*** Variables ***

| ${WorkspaceName}		| LDAP Sync
| ${ConnectionPath}		| testing.corp/CN=Users,DC=testing,DC=corp
| ${Authentication}		| FastBind


*** Test Cases ***

| The Cancel button is on the Header bar
| | [Documentation] | Verify that the Cancel button is on the header bar on the Integration Points Wizard.
| | [Tags]			| manual	| visual
| | Run the following manual steps:
| | ... | Steps:
| | ... | 1. Go to Step 1 of the Create Integration Point Wizard
| | ... | 
| | ... | Expected Results:
| | ... | 1. Verify that the Cancel button is on the right side of the header bar
| | ... | 2. Verify that the Cancel button is grey with black text


| The Cancel button on Step 1 Redirects to the List View
| | [Documentation] | Verify that clicking the Cancel button on Step 1 of the Integration Points Wizard redirects to the Integration Points List View.
| | [Tags]			| required	| automated
| | Open Create Integration Point
| | Fill out the Following Fields of the Integration Point Step 1 form | Step 1 Cancellation Test	| Append/Overlay | LDAP | Custodian | No
| | Click	| Cancel
| | Select iFrame	| id=ListTemplateFrame

| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that the view dropdown is set to the following value	| All Integration Points


| Cancelling from Step 1 of the Integration Point Wizard does not create an Integration Point
| | [Documentation] | Verify that clicking the Cancel button on Step 1 of the Integration Points Wizard does not create an Integration Point.
| | [Tags]			| automated
| | [Setup]			| Require Test Case	| The Cancel button on Step 1 Redirects to the List View
| | Verify that the Following Integration Point does not Exist in the current Workspace	| Step 1 Cancellation Test


| The Cancel button on Step 2 Redirects to the List View
| | [Documentation] | Verify that clicking the Cancel button on Step 2 of the Integration Points Wizard redirects to the Integration Points List View.
| | [Tags]			| required	| automated
| | Open Create Integration Point
| | Fill out the Following Fields of the Integration Point Step 1 form | Step 2 Cancellation Test	| Append/Overlay | LDAP | Custodian | No
| | Proceed to the Next Step of the Integration Point wizard
| | Fill out the Following Fields of the Integration Point Step 2 form | ConnectionPath=${ConnectionPath}	| Authentication=${Authentication}
| | Click	| Cancel
| | Select iFrame	| id=ListTemplateFrame

| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that the view dropdown is set to the following value	| All Integration Points


| Cancelling from Step 2 of the Integration Point Wizard does not create an Integration Point
| | [Documentation] | Verify that clicking the Cancel button on Step 2 of the Integration Points Wizard does not create an Integration Point.
| | [Tags]			| automated
| | [Setup]			| Require Test Case	| The Cancel button on Step 2 Redirects to the List View
| | Verify that the Following Integration Point does not Exist in the current Workspace	| Step 1 Cancellation Test


| The Cancel button on Step 3 Redirects to the List View
| | [Documentation] | Verify that clicking the Cancel button on Step 3 of the Integration Points Wizard redirects to the Integration Points List View.
| | [Tags]			| required	| automated
| | Open Create Integration Point
| | Fill out the Following Fields of the Integration Point Step 1 form	| Step 3 Cancellation Test	| Append/Overlay | LDAP | Custodian | No
| | Proceed to the Next Step of the Integration Point wizard
| | Fill out the Following Fields of the Integration Point Step 2 form	| ConnectionPath=${ConnectionPath}	| Authentication=${Authentication}
| | Proceed to the Next Step of the Integration Point wizard
| | Map the Following Fields on the Integration Point Step 3 form	| 
| | ... | Email			| mail			| 
| | ... | Department	| countrycode	| 
| | ... | First Name	| givenname		| 
| | ... | Last Name		| sn			| 
| | ... | UniqueID		| objectguid
| | Click	| Cancel
| | Select iFrame	| id=ListTemplateFrame

| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that the view dropdown is set to the following value	| All Integration Points


| Cancelling from Step 3 of the Integration Point Wizard does not create an Integration Point
| | [Documentation] | Verify that clicking the Cancel button on Step 1 of the Integration Points Wizard does not create an Integration Point.
| | [Tags]			| automated
| | [Setup]			| Require Test Case	| The Cancel button on Step 3 Redirects to the List View
| | Verify that the Following Integration Point does not Exist in the current Workspace	| Step 1 Cancellation Test


*** Keywords ***

| Suite Setup Keyword
| | [Documentation]	| Create the Suite Preconditions
| | Setup connection to the relativity database
| | Open and Log in to Method
| | Setup connection to the Relativity Services API for Method	| ${WorkspaceName}

| | ${IntegrationPointExists}=	| Determine if the Following Integration Point Exists in the current Workspace	| Step 1 Cancellation Test
| | Run Keyword If	| ${${IntegrationPointExists}}	| Delete the following Integration Point using API	| Step 1 Cancellation Test

| | ${IntegrationPointExists}=	| Determine if the Following Integration Point Exists in the current Workspace	| Step 2 Cancellation Test
| | Run Keyword If	| ${${IntegrationPointExists}}	| Delete the following Integration Point using API	| Step 2 Cancellation Test

| | ${IntegrationPointExists}=	| Determine if the Following Integration Point Exists in the current Workspace	| Step 3 Cancellation Test
| | Run Keyword If	| ${${IntegrationPointExists}}	| Delete the following Integration Point using API	| Step 3 Cancellation Test
| | [Teardown]		| Drop connection to the relativity services api

| Delete any Objects created in the Suite then Close All Browsers
| | [Documentation]	| Delete any Integration Point(s) created in this suite then closes all open browsers.
| | ...				| This is run as the Suite Teardown.
| | Setup connection to the Relativity Services API for Method	| ${WorkspaceName}

| | ${IntegrationPointExists}=	| Determine if the Following Integration Point Exists in the current Workspace	| Step 1 Cancellation Test
| | Run Keyword If	| ${${IntegrationPointExists}}	| Delete the following Integration Point using API	| Step 1 Cancellation Test

| | ${IntegrationPointExists}=	| Determine if the Following Integration Point Exists in the current Workspace	| Step 2 Cancellation Test
| | Run Keyword If	| ${${IntegrationPointExists}}	| Delete the following Integration Point using API	| Step 2 Cancellation Test

| | ${IntegrationPointExists}=	| Determine if the Following Integration Point Exists in the current Workspace	| Step 3 Cancellation Test
| | Run Keyword If	| ${${IntegrationPointExists}}	| Delete the following Integration Point using API	| Step 3 Cancellation Test

| | Drop connection to the relativity services api
| | Close All Browsers


*** Settings ***
| Library	| ${KEYWORD_DIR}/ManualKeywords.py
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/NavigationKeywords.txt
# ViewKeywords
| Resource	| ${LDAP_KEYWORD_DIR}/DatabaseKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/IntegrationPointKeywords.txt
