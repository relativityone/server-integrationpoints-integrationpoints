
*** Settings ***
| Documentation 	| Verify that Next and Back buttons on the Integration Point Wizard affect the data correctly.
| Metadata			| Authors	| William Sehy	| Daniel Oliver
| Force Tags		| in-progress	| team.game_of_holds	| feature.integration_points
| Suite Setup		| Run Keywords	| Setup connection to the relativity database	| Open and Log in to Legal Hold
| Test Teardown 	| Run Keyword if Test Failed	| Capture Page Screenshot
| Suite Teardown	| Close All Browsers


*** Variables ***

| ${WorkspaceName}		| LDAP Sync
| ${ConnectionPath1}	| testing.corp/CN=Users,DC=testing,DC=corp
| ${ConnectionPath2}	| testing.corp
| ${ObjectFilterString}	| (objectClass=*)
| ${Authentication}		| FastBind
| ${IPUsername}			| testing\\administrator
| ${IPPassword}			| P@ssw0rd@1
| ${ImportNestedItems}	| Yes
| ${IntegrationName1}	| Next and Back Testing
| ${ManagerRadioButton}		| enable
| ${OverwriteList}			| s2id_overwrite
| ${UniqueIdentifierList}	| s2id_overlay-identifier


*** Test Cases ***

| Returning to Step 3 from Step 1 without changing anything Saves all Step 3 data
| | [Documentation]	| Verify that if you fill out steps 1 through 3, go back to step 1, then return to step 3, that all of the previously entered data on step 3 will still be present.
| | ...				| [id] 0ed034ee-a89f-11e4-93a5-b8ca3a83048f
| | [Tags]			| automated
| | @{Step1Fields}=		| Create List	| ${IntegrationName1}	| LDAP	| Custodian	| No
| | @{Step2Fields}=		| Create List	| ${ConnectionPath1}	| ${ObjectFilterString}	| ${Authentication}	| ${IPUsername}	| ${IPPassword}
| | @{Step3Mapping}=	| Create List	|
| | ... | Email			| mail			
| | Create LDAP Integration Point without Saving	| ${Step1Fields}	| ${Step2Fields}	| ${Step3Mapping}	| Append Only	| Email 
| | Select Radio Button	| ${ManagerRadioButton}	| false
| | Proceed to the Previous Step of the Integration Point wizard
| | Proceed to the Previous Step of the Integration Point wizard
| | Proceed to the Next Step of the Integration Point wizard
| | Proceed to the Next Step of the Integration Point wizard

| | Verify that the Selected Workspace fields are in the following order	| Email
| | Verify that the Selected Source Attribute fields are in the following order	| mail
| | Verify that the following select2 Dropdown currently has the following value selected	| ${UniqueIdentifierList}	| Email
| | Radio button should be set to	| ${ManagerRadioButton}	| false


| Going back from Step 3 and changing the Connection Path in Step 2 to remove some of the Source Attributes selections on Step 3 maintains the Mappings
| | [Documentation]	| Verify that if you fill out steps 1 through 3, go back to step 2 and change the Connection Path in step 2 to 
| | ...				| something that does not have some of the selected Source Attributes, then when you return to step 3 the remaining Source Attribute selections 
| | ...				| will still be correctly mapped with the Workspace Fields selections. Also the Unique Identifier dropdown value will be saved.
| | ...				| [id] 0ed0830f-a89f-11e4-9c79-b8ca3a83048f
| | [Tags]			| automated	| dan682
| | @{Step1Fields}=		| Create List	| ${IntegrationName1}	| LDAP	| Custodian	| No
| | @{Step2Fields}=		| Create List	| ${ConnectionPath1}	| ${ObjectFilterString}	| ${Authentication}	| ${IPUsername}	| ${IPPassword}
| | @{Step3Mapping}=	| Create List	|
| | ... | Department	| objectguid
| | ... | Email			| mail 
| | Create LDAP Integration Point without Saving	| ${Step1Fields}	| ${Step2Fields}	| ${Step3Mapping}	| Append Only	| Email 
| | Select Radio Button	| ${ManagerRadioButton}	| false
| | Proceed to the Previous Step of the Integration Point wizard
| | Fill out the Following Fields of the LDAP Integration Point Step 2 form	| ${ConnectionPath2}
| | Proceed to the Next Step of the Integration Point wizard

| | Verify that the Selected Workspace fields are in the following order	| Department	| Email
| | Verify that the following select2 Dropdown currently has the following value selected	| ${UniqueIdentifierList}	| Email
| | Verify that the Selected Source Attribute fields are in the following order	| objectguid
| | Radio button should be set to	| ${ManagerRadioButton}	| false


| Going back from Step 3 and changing Step 1's Destination RDO resets everything on Step 3 except the Source Attribute selections
| | [Documentation]	| Verify that if you fill out steps 1 through 3, go back to step 1 and change the Destination RDO, then when you return to step 3 
| | ...				| all of the Workspace Fields selections and the dropdowns will be reset, but the Source Attribute selections will be saved.
| | ...				| [id] 0ed08310-a89f-11e4-ae95-b8ca3a83048f
| | [Tags]			| automated
| | @{Step1Fields}=		| Create List	| ${IntegrationName1}	| LDAP	| Custodian	| No
| | @{Step2Fields}=		| Create List	| ${ConnectionPath1}	| ${ObjectFilterString}	| ${Authentication}	| ${IPUsername}	| ${IPPassword}
| | @{Step3Mapping}=	| Create List	|
| | ... | Department	| objectguid
| | ... | Email			| mail
| | Create LDAP Integration Point without Saving	| ${Step1Fields}	| ${Step2Fields}	| ${Step3Mapping}	| Append Only	| Email 
| | Select Radio Button	| ${ManagerRadioButton}	| false

| | Proceed to the Previous Step of the Integration Point wizard
| | Proceed to the Previous Step of the Integration Point wizard
| | Fill out the Following Fields of the Integration Point Step 1 form	| ${IntegrationName1}	| LDAP	| Questionnaire	| No
| | Proceed to the Next Step of the Integration Point wizard
| | Proceed to the Next Step of the Integration Point wizard

| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that the Selected Field Mapping list is empty
| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that the Selected Source Attributes list is empty


| Returning to Step 2 from Step 1 without changing anything Saves all Step 2 data
| | [Documentation]	| Verify that if you fill out steps 1 and 2, go back to step 1, then return to step 2, that all of the previously entered data on step 2 will still be present.
| | ...				| [id] 0ecbef30-a89f-11e4-81ef-b8ca3a83048f
| | [Tags]			| manual	| unautomateable
| | Open Create Integration Point
| | Fill out the Following Fields of the Integration Point Step 1 form	| ${IntegrationName1}	| LDAP	| Custodian	| No
| | Proceed to the Next Step of the Integration Point wizard

| | Fill out the Following Fields of the LDAP Integration Point Step 2 form	| 
| | ... | ConnectionPath=Connection Path
| | ... | ObjectFilterString=Object Filter String
| | ... | Authentication=Secure Socket Layer
| | ... | ConnectionUsername=User Name
| | ... | ConnectionPassword=Pass phrase
| | ... | ImportNestedItems=Yes

| | Proceed to the Previous Step of the Integration Point wizard
| | Proceed to the Next Step of the Integration Point wizard

| | Run the following manual steps:
| | ... | Expected Results:
| | ... | Verify that the Step 2 fields have the following values	| 
| | ... | - Connection Path: Connection Path
| | ... | - Object Filter String: Object Filter String 
| | ... | - Authentication: Secure Socket Layer
| | ... | - Username: User Name
| | ... | - Password: **********
| | ... | - Import Nested Items: Yes


| Returning to Step 3 from Step 2 without changing anything Saves all Step 3 data
| | [Documentation]	| Verify that if you fill out steps 1 through 3, go back to step 2, then return to step 3, that all of the previously entered data on step 3 will still be present.
| | ...				| [id] 0ecfbfc0-a89f-11e4-839a-b8ca3a83048f
| | [Tags]			| automated
| | @{Step1Fields}=		| Create List	| ${IntegrationName1}	| LDAP	| Custodian	| No
| | @{Step2Fields}=		| Create List	| ${ConnectionPath1}	| ${ObjectFilterString}	| ${Authentication}	| ${IPUsername}	| ${IPPassword}
| | @{Step3Mapping}=	| Create List	|
| | ... | First Name	| givenname
| | ... | Last Name		| sn
| | ... | Email			| mail
| | ... | Department	| countrycode
| | Create LDAP Integration Point without Saving	| ${Step1Fields}	| ${Step2Fields}	| ${Step3Mapping}	| Overlay Only	| Email 

| | Proceed to the Previous Step of the Integration Point wizard
| | Proceed to the Next Step of the Integration Point wizard

| | Verify that the Selected Workspace fields are in the following order		| First Name	| Last Name	| Email	| Department
| | Verify that the Selected Source Attribute fields are in the following order	| givenname		| sn		| mail	| countrycode
| | Verify that the following select2 Dropdown currently has the following value selected	| ${OverwriteList}			| Overlay Only
| | Verify that the following select2 Dropdown currently has the following value selected	| ${UniqueIdentifierList}	| Email


*** Keywords ***

| Verify that the Selected Source Attributes list is empty
| | [Documentation]	| A keyword that confirms that the Selected Source Attribute list does not have any fields allocated to it
| | Wait Until Keyword Succeeds	| 20s	| 2s	| Element should be visible	| xpath=//select[@id='selected-source-fields']	| The Selected Fields list box was not visible.

| | Element should not be visible	| xpath=//select[@id='selected-source-fields']/child::option

| Verify that the Selected Field Mapping list is empty
| | [Documentation]	| A keyword that confirms that the Selected Field Mapping list does not have any fields allocated to it
| | Wait Until Keyword Succeeds	| 20s	| 2s	| Element should be visible	| xpath=//select[@id='selected-workspace-fields']	| The Selected Fields list box was not visible.

| | Element should not be visible	| xpath=//select[@id='selected-workspace-fields']/child::option

| Create LDAP Integration Point without Saving
| | [Documentation]	| A keyword that creates an Integration Point but does not click Save.
| | ...				| The value of Step1Fields should be the arguments from "Fill out the Following Fields of the Integration Point Step 1 form" in a list variable.
| | ...				| The value of Step2Fields should be the arguments from "Fill out the Following Fields of the LDAP Integration Point Step 2 form" in a list variable.
| | ...				| The value of Step3Mapping should be the arguments from "Map the Following Fields on the Integration Point Step 3 form" in a list variable.
| | ... 			| The value of Overwrite should be "Append Only", "Overlay Only", or "ppend/Overlay".
| | [ARGUMENTS]		| ${Step1Fields}	| ${Step2Fields}	| ${Step3Mapping}	| ${Step3Overwrite}	| ${Step3UniqueID}
| | Open Create Integration Point
| | Fill out the Following Fields of the Integration Point Step 1 form	| @{Step1Fields}
| | Proceed to the Next Step of the Integration Point wizard

| | Fill out the Following Fields of the LDAP Integration Point Step 2 form	| @{Step2Fields}
| | Proceed to the Next Step of the Integration Point wizard
| | Wait Until Element Is Visible	| xpath=//select[@id='source-fields']/child::option	| 15s	| No Attribute fields were found.

| | Map the Following Fields on the Integration Point Step 3 form	| @{Step3Mapping}
| | Select from select2 Dropdown	| s2id_overwrite			| ${Step3Overwrite}
| | Select from select2 Dropdown	| s2id_overlay-identifier	| ${Step3UniqueID}


*** Settings ***
| Library	| Collections
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${KEYWORD_DIR}/LegalHold/Legal_Hold_Keywords_API.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/GridKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/MiscLegalHoldKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/GenericKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/StepOneKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/StepTwoKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/StepThreeKeywords.txt