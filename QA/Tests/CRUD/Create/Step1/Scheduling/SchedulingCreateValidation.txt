
*** Settings ***
| Documentation		| Verify the create process's validation is correct for the RIP Scheduler.
| Metadata			| Authors	| Daniel Oliver	| William Sehy
| Force Tags		| in-progress	| team.method
| Suite Setup		| Open and Log in to Method
| Test Setup		| Open Create Integration Point
| Test Teardown 	| Run Keyword if Test Failed	| Capture Page Screenshot
| Suite Teardown	| Close All Browsers


*** Variables ***

| ${WorkspaceName}			| LDAP Sync
| ${Frequency Field}		| s2id_frequency
| ${Reoccur Field}			| reoccur
| ${Weekly Send On Field}	| sendOn
| ${Start Date Field}		| scheduleRulesStartDate
| ${End Date Field}			| scheduleRulesEndDate
| ${Scheduled Time Field}	| scheduledTime


*** Test Cases ***

| Blank Required Fields show messages after clicking Next
| | [Documentation]	| Verify that Required Fields that are blank show the correct warning message after clicking Next.
| | ...				| [id] 0f830a30-a89f-11e4-9890-b8ca3a83048f
| | [Tags]		    | required	| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append	| SourceProvider=LDAP	| DestinationRDO=Custodian Role	| EnableScheduler=Yes
| | Click	| id=next

| | Run Keyword And Continue On Failure	| Verify the following Integration Point Field has the following Error	| ${Frequency Field}		| This field is required.
| | Run Keyword And Continue On Failure	| Verify the following Integration Point Field has the following Error	| ${Start Date Field}		| This field is required.
| | Run Keyword And Continue On Failure	| Verify the following Integration Point Field has the following Error	| ${Scheduled Time Field}	| This field is required.


| Proceeding Post-blank required fields message
| | [Documentation]	| Verify that a Scheduled Report can be created after the blank required fields message is shown.
| | ...				| [id] 0f881340-a89f-11e4-83fe-b8ca3a83048f
| | [Tags]			| automated
| | [Setup]			| Require Test Case	| Blank Required Fields show messages after clicking Next
| | Fill out the Following Fields of the Integration Point Step 1 form	| Frequency=Daily	| StartDate=11/23/2012	| EndDate=04/15/2016	| ScheduledTime=11:00 PM
| | Proceed to the Next Step of the Integration Point wizard
| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that you are on the following Step of the Create Integration Point Process	| Connect to Source


| Date Validation message are shown after clicking Next
| | [Documentation]	| Verify that the date fields show warning messages after clicking Next when invalid values are entered.
| | ...				| [id] 0f881341-a89f-11e4-b0af-b8ca3a83048f
| | [Tags]			| required	| automated	| dam795
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append	| SourceProvider=LDAP	| DestinationRDO=Custodian Role	| 
| | ... 																| EnableScheduler=Yes	| Frequency=Daily	| StartDate=1234567	| EndDate=3456789		| ScheduledTime=11:00 PM
| | Click	| id=next

| | Run Keyword And Continue On Failure	| Verify the following Integration Point Field has the following Error	| ${Start Date Field}	| The field Start Date must be a date.
| | Run Keyword And Continue On Failure	| Verify the following Integration Point Field has the following Error	| ${End Date Field}		| The field End Date must be a date.


| Proceeding Post-Date Invalid message
| | [Documentation]	| Verify that Step 2 can be reached after the invalid date fields message is shown.
| | ...				| [id] 0f88615e-a89f-11e4-b655-b8ca3a83048f
| | [Tags]			| automated	| dam795
| | [Setup]			| Require Test Case	| Date Validation message are shown after clicking Next
| | Fill out the Following Fields of the Integration Point Step 1 form	| StartDate=11/23/2012	| EndDate=04/15/2026
| | Proceed to the Next Step of the Integration Point wizard
| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that you are on the following Step of the Create Integration Point Process	| Connect to Source


| Start Date must be before the End Date Validation Message is displayed after clicking Next
| | [Documentation]	| Verify that the "Start Date must be before the End Date" validation message is displayed when the End Date is before the Start Date.
| | ...				| [id] 0f88615f-a89f-11e4-9e5d-b8ca3a83048f
| | [Tags]		    | required	| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append		| SourceProvider=LDAP	| DestinationRDO=Custodian Role	| 
| | ... 																| EnableScheduler=Yes	| Frequency=Daily	| StartDate=11/23/2020	| EndDate=04/15/2019	| ScheduledTime=11:32 PM
| | Click	| id=next
| | Verify the following Integration Point Field has the following Error	| ${End Date Field}		| The start date must come before the end date.


| Proceeding Post-Start Date must be before the End Date message
| | [Documentation]	| Verify that you can proceed to Step 2 after the "Start Date must be before the End Date" validation message has been shown.
| | ...				| [id] 0f886160-a89f-11e4-853f-b8ca3a83048f
| | [Tags]			| automated
| | [Setup]			| Require Test Case	| Start Date must be before the End Date Validation Message is displayed after clicking Next
| | Fill out the Following Fields of the Integration Point Step 1 form	| EndDate=04/15/2026
| | Proceed to the Next Step of the Integration Point wizard
| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that you are on the following Step of the Create Integration Point Process	| Connect to Source


| Scheduled Time Validation message is shown after clicking Next
| | [Documentation]	| Verify that the Scheduled Time field shows a warning message after clicking Next when an invalid value is entered.
| | ...				| [id] 0f886161-a89f-11e4-95bf-b8ca3a83048f
| | [Tags]			| required	| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append		| SourceProvider=LDAP	| DestinationRDO=Custodian Role	| 
| | ... 																| EnableScheduler=Yes	| Frequency=Daily	| StartDate=11/23/2002	| EndDate=04/15/2026	| ScheduledTime=230 PM
| | Click	| id=next
| | Verify the following Integration Point Field has the following Error	| ${Scheduled Time Field}	| Please enter a valid time (12-hour format).


| Proceeding Post-Time Invalid message
| | [Documentation]	| Verify that you can proceed to Step 2 after the invalid time validation message has been shown.
| | ...				| [id] 0f886162-a89f-11e4-ad17-b8ca3a83048f
| | [Tags]			| automated
| | [Setup]			| Require Test Case	| Scheduled Time Validation message is shown after clicking Next
| | Fill out the Following Fields of the Integration Point Step 1 form	| ScheduledTime=11:40 PM
| | Proceed to the Next Step of the Integration Point wizard
| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that you are on the following Step of the Create Integration Point Process	| Connect to Source


| Scheduled Time Validation message is shown when the hour value is greater than 23
| | [Documentation]	| Verify that the Scheduled Time field shows is shown when the hour value is greater than 23.
| | ...				| [id] 0f886163-a89f-11e4-96b8-b8ca3a83048f
| | [Tags]			| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append		| SourceProvider=LDAP	| DestinationRDO=Custodian Role	| 
| | ... 																| EnableScheduler=Yes	| Frequency=Daily	| StartDate=11/23/2002	| EndDate=04/15/2026	| ScheduledTime=13:30 PM
| | Click	| id=next
| | Verify the following Integration Point Field has the following Error	| ${Scheduled Time Field}	| Please enter a valid time (12-hour format).


| Scheduled Time Validation message is shown when the minute value is greater than 59
| | [Documentation]	| Verify that the Scheduled Time field shows is shown when the hour value is greater than 59.
| | ...				| [id] 0f88d68f-a89f-11e4-917d-b8ca3a83048f
| | [Tags]			| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append		| SourceProvider=LDAP	| DestinationRDO=Custodian Role	| 
| | ... 																| EnableScheduler=Yes	| Frequency=Daily	| StartDate=11/23/2002	| EndDate=04/15/2026	| ScheduledTime=11:60 PM
| | Click	| id=next
| | Verify the following Integration Point Field has the following Error	| ${Scheduled Time Field}	| Please enter a valid time (12-hour format).


| Send On Weekly Validation message is shown after trying to Proceed with nothing selected
| | [Documentation]	| Verify that the Weekly Send On field has a validation message displayed when no Day has been selected and you try to proceed to Step 2.
| | ...				| [id] 0f88d690-a89f-11e4-aeee-b8ca3a83048f
| | [Tags]			| required	| automated	| dam853
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append		| SourceProvider=LDAP	| DestinationRDO=Custodian Role		| EnableScheduler=Yes	| 
| | ... 																| Frequency=Weekly	| Reoccur=1				| StartDate=11/23/2002	| EndDate=04/15/2026	| ScheduledTime=11:50 PM
| | Click	| id=next
| | Verify that the Weekly Send On Field has a Required Field Error	| This field is required.


| Proceeding Post-Weekly Send On Validation
| | [Documentation]	| Verify that you can proceed to Step 2 after the Weekly Send On validation message has been shown.
| | ...				| [id] 0f88d691-a89f-11e4-ab86-b8ca3a83048f
| | [Tags]			| automated	| dam853
| | [Setup]			| Require Test Case	| Send On Weekly Validation message is shown after trying to Proceed with nothing selected
| | Fill out the Following Fields of the Integration Point Step 1 form	| SendOn=Monday
| | Proceed to the Next Step of the Integration Point wizard
| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that you are on the following Step of the Create Integration Point Process	| Connect to Source


| Reoccur Required message is displayed when blank
| | [Documentation]	| Verify that the Reoccur required message displays if you try to save with a blank Reoccur value.
| | ...				| [id] 0f88d692-a89f-11e4-8bd0-b8ca3a83048f
| | [Tags]			| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append	| SourceProvider=LDAP	| DestinationRDO=Custodian Role	| EnableScheduler=Yes	| 
| | ... 																| Frequency=Weekly	| SendOn=Monday			| Reoccur=${EMPTY}	| StartDate=11/23/2002	| EndDate=04/15/2026	| ScheduledTime=11:50 PM
| | Click	| id=next
| | Verify the following Integration Point Field has the following Error	| ${Reoccur Field}	| This field is required.


| Reoccur Validation message is displayed for Zero
| | [Documentation]	| Verify that the Reoccur validation does not accept Zero
| | ...				| [id] 0f88d693-a89f-11e4-94a6-b8ca3a83048f
| | [Tags]			| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append	| SourceProvider=LDAP	| DestinationRDO=Custodian Role	| EnableScheduler=Yes	| 
| | ... 																| Frequency=Weekly	| SendOn=Monday			| Reoccur=0			| StartDate=11/23/2002	| EndDate=04/15/2026	| ScheduledTime=11:50 PM
| | Click	| id=next
| | Verify the following Integration Point Field has the following Error	| ${Reoccur Field}	| Please enter a value between 1 and 999.


| Reoccur Validation message is displayed for negative integers
| | [Documentation]	| Verify that the Reoccur validation does not accept negative integers.
| | ...				| [id] 0f8924b0-a89f-11e4-8034-b8ca3a83048f
| | [Tags]			| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append	| SourceProvider=LDAP	| DestinationRDO=Custodian Role	| EnableScheduler=Yes	| 
| | ... 																| Frequency=Weekly	| SendOn=Monday			| Reoccur=-1		| StartDate=11/23/2002	| EndDate=04/15/2026	| ScheduledTime=11:50 PM
| | Click	| id=next
| | Verify the following Integration Point Field has the following Error	| ${Reoccur Field}	| Please enter a value between 1 and 999.


| Reoccur Validation message is displayed for integers above 999
| | [Documentation]	| Verify that the Reoccur validation does not accept integers above 999.
| | ...				| [id] 0f8924b1-a89f-11e4-949b-b8ca3a83048f
| | [Tags]			| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append	| SourceProvider=LDAP	| DestinationRDO=Custodian Role	| EnableScheduler=Yes	| 
| | ... 																| Frequency=Weekly	| SendOn=Monday			| Reoccur=1000		| StartDate=11/23/2002	| EndDate=04/15/2026	| ScheduledTime=11:50 PM
| | Click	| id=next
| | Verify the following Integration Point Field has the following Error	| ${Reoccur Field}	| Please enter a value between 1 and 999.


| Reoccur Validation message is displayed for non-integers
| | [Documentation]	| Verify that the Reoccur field's validation does not accept non-integers.
| | ...				| [id] 0f8924b2-a89f-11e4-97a6-b8ca3a83048f
| | [Tags]			| required	| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append	| SourceProvider=LDAP	| DestinationRDO=Custodian Role	| EnableScheduler=Yes	| 
| | ... 																| Frequency=Weekly	| SendOn=Monday			| Reoccur=a			| StartDate=11/23/2002	| EndDate=04/15/2026	| ScheduledTime=11:50 PM
| | Click	| id=next
| | Verify the following Integration Point Field has the following Error	| ${Reoccur Field}	| Please enter a value between 1 and 999.


| Proceeding Post-Reoccur Validation
| | [Documentation]	| 
| | ...				| [id] 0f8924b3-a89f-11e4-81ca-b8ca3a83048f
| | [Tags]			| automated
| | [Setup]			| Require Test Case	| Reoccur Validation message is displayed for non-integers
| | Fill out the Following Fields of the Integration Point Step 1 form	| Reoccur=2
| | Proceed to the Next Step of the Integration Point wizard
| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that you are on the following Step of the Create Integration Point Process	| Connect to Source


| Integration Points can be created with an End Date in the Past
| | [Documentation]	| Verify that Integration Points can be created with an End Date value that is in the Past.
| | ...				| [id] 0f8924b4-a89f-11e4-b19d-b8ca3a83048f
| | [Tags]			| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append		| SourceProvider=LDAP	| DestinationRDO=Custodian Role	| EnableScheduler=Yes	| 
| | ... 																| Frequency=Daily	| StartDate=11/23/2002	| EndDate=04/15/2006	| ScheduledTime=11:50 PM
| | Proceed to the Next Step of the Integration Point wizard
| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that you are on the following Step of the Create Integration Point Process	| Connect to Source


*** Keywords ***

| Verify that the Weekly Send On Field has a Required Field Error
| | [Documentation]	| Verifies that the given field has the given error.
| | ... 			| Must be called from the Integration Point Create/Edit pages.
| | [ARGUMENTS]		| ${ErrorText}
| | Element Should Be Visible	| css=div[id=sendOn] > div.icon-error.field-validation-error	| No error was found on the Send On field.
| | ${ActualText}=	| Get Text	| css=div[id=sendOn] > div.icon-error.field-validation-error
| | Run Keyword Unless	| '${ActualText}' == '${ErrorText}'	| Fail	| The Send On error was incorrect. It should have been "${ErrorText}" but it was actually "${ActualText}".


*** Settings ***
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${METHOD_KEYWORD_DIR}/MiscMethodKeywords.txt
| Resource	| ${METHOD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| C:/SourceCode/LDAPSync/QA/Keywords/IntegrationPointKeywords.txt