
*** Settings ***
| Documentation		| Verify the create process's validation is correct for the RIP Scheduler.
| Metadata			| Authors	| Daniel Oliver	| William Sehy
| Force Tags		| in-progress	| team.method
| Suite Setup		| Open and Log in to Method
| Test Setup		| Open Create Integration Point
| Test Teardown 	| Run Keyword if Test Failed	| Capture Page Screenshot
| Suite Teardown	| Close All Browsers


*** Variables ***

| ${WorkspaceName}			| Relativity Legal Hold
| ${Frequency Field}		| s2id_frequency
| ${Reoccur Field}			| reoccur
| ${Weekly Send On Field}	| sendOn
| ${Start Date Field}		| scheduleRulesStartDate
| ${End Date Field}			| scheduleRulesEndDate
| ${Scheduled Time Field}	| scheduledTime


*** Test Cases ***

| Blank Required Fields show messages after clicking Next
| | [Documentation]	| Verify that Required Fields that are blank show the correct warning message after clicking Next.
| | [Tags]		    | required	| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append	| SourceProvider=LDAP	| DestinationRDO=Agent Type	| EnableScheduler=Yes
| | Click	| id=next

| | Run Keyword And Continue On Failure	| Verify the following Integration Point Field has the following Error	| ${Frequency Field}		| This field is required.
| | Run Keyword And Continue On Failure	| Verify the following Integration Point Field has the following Error	| ${Start Date Field}		| This field is required.
| | Run Keyword And Continue On Failure	| Verify the following Integration Point Field has the following Error	| ${Scheduled Time Field}	| This field is required.


| Proceeding Post-blank required fields message
| | [Documentation]	| Verify that a Scheduled Report can be created after the blank required fields message is shown.
| | [Tags]			| automated
| | [Setup]			| Require Test Case	| Blank Required Fields show messages after clicking Next
| | Fill out the Following Fields of the Integration Point Step 1 form	| Frequency=Daily	| StartDate=11/23/2012	| EndDate=04/15/2016	| ScheduledTime=23:00
| | Proceed to the Next Page
| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that you are on the following Step of the Create Integration Point Process	| Connect to Source


| Date Validation message are shown after clicking Next
| | [Documentation]	| Verify that the date fields show warning messages after clicking Next when invalid values are entered.
| | [Tags]		    | required	| broken	| dam795
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append	| SourceProvider=LDAP	| DestinationRDO=Agent Type	| 
| | ... 																| EnableScheduler=Yes	| Frequency=Daily	| StartDate=1234		| EndDate=5678	| ScheduledTime=23:00
| | Click	| id=next

| | Run Keyword And Continue On Failure	| Verify the following Integration Point Field has the following Error	| ${Start Date Field}	| The field Start Date must be a date.
| | Run Keyword And Continue On Failure	| Verify the following Integration Point Field has the following Error	| ${End Date Field}		| The field End Date must be a date.


| Proceeding Post-Date Invalid message
| | [Documentation]	| Verify that Step 2 can be reached after the invalid date fields message is shown.
| | [Tags]			| broken	| dam795
| | [Setup]			| Require Test Case	| Date Validation message are shown after clicking Next
| | Fill out the Following Fields of the Integration Point Step 1 form	| StartDate=11/23/2012	| EndDate=04/15/2026
| | Proceed to the Next Page
| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that you are on the following Step of the Create Integration Point Process	| Connect to Source


| Start Date must be before the End Date Validation Message is displayed after clicking Next
| | [Documentation]	| Verify that the "Start Date must be before the End Date" validation message is displayed when the End Date is before the Start Date.
| | [Tags]		    | required	| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append		| SourceProvider=LDAP	| DestinationRDO=Agent Type	| 
| | ... 																| EnableScheduler=Yes	| Frequency=Daily	| StartDate=11/23/2020	| EndDate=04/15/2019	| ScheduledTime=23:32
| | Click	| id=next
| | Verify the following Integration Point Field has the following Error	| ${End Date Field}		| The start date must come before the end date.


| Proceeding Post-Start Date must be before the End Date message
| | [Documentation]	| Verify that you can proceed to Step 2 after the "Start Date must be before the End Date" validation message has been shown.
| | [Tags]			| automated
| | [Setup]			| Require Test Case	| Start Date must be before the End Date Validation Message is displayed after clicking Next
| | Fill out the Following Fields of the Integration Point Step 1 form	| EndDate=04/15/2026
| | Proceed to the Next Page
| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that you are on the following Step of the Create Integration Point Process	| Connect to Source


| Scheduled Time Validation message is shown after clicking Next
| | [Documentation]	| Verify that the Scheduled Time field shows a warning message after clicking Next when an invalid value is entered.
| | [Tags]			| required	| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append		| SourceProvider=LDAP	| DestinationRDO=Agent Type	| 
| | ... 																| EnableScheduler=Yes	| Frequency=Daily	| StartDate=11/23/2002	| EndDate=04/15/2026	| ScheduledTime=230
| | Click	| id=next
| | Verify the following Integration Point Field has the following Error	| ${Scheduled Time Field}	| Please enter a valid time (24-hour format).


| Proceeding Post-Time Invalid message
| | [Documentation]	| Verify that you can proceed to Step 2 after the invalid time validation message has been shown.
| | [Tags]			| automated
| | [Setup]			| Require Test Case	| Scheduled Time Validation message is shown after clicking Next
| | Fill out the Following Fields of the Integration Point Step 1 form	| ScheduledTime=23:40
| | Proceed to the Next Page
| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that you are on the following Step of the Create Integration Point Process	| Connect to Source


| Scheduled Time Validation message is shown when the hour value is greater than 23
| | [Documentation]	| Verify that the Scheduled Time field shows is shown when the hour value is greater than 23.
| | [Tags]			| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append		| SourceProvider=LDAP	| DestinationRDO=Agent Type	| 
| | ... 																| EnableScheduler=Yes	| Frequency=Daily	| StartDate=11/23/2002	| EndDate=04/15/2026	| ScheduledTime=24:30
| | Click	| id=next
| | Verify the following Integration Point Field has the following Error	| ${Scheduled Time Field}	| Please enter a valid time (24-hour format).


| Scheduled Time Validation message is shown when the minute value is greater than 59
| | [Documentation]	| Verify that the Scheduled Time field shows is shown when the hour value is greater than 59.
| | [Tags]			| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append		| SourceProvider=LDAP	| DestinationRDO=Agent Type	| 
| | ... 																| EnableScheduler=Yes	| Frequency=Daily	| StartDate=11/23/2002	| EndDate=04/15/2026	| ScheduledTime=23:60
| | Click	| id=next
| | Verify the following Integration Point Field has the following Error	| ${Scheduled Time Field}	| Please enter a valid time (24-hour format).


| Send On Weekly Validation message is shown after trying to Proceed with nothing selected
| | [Documentation]	| Verify that the Weekly Send On field has a validation message displayed when no Day has been selected and you try to proceed to Step 2.
| | [Tags]			| required	| broken	| dam853
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append		| SourceProvider=LDAP	| DestinationRDO=Agent Type		| EnableScheduler=Yes	| 
| | ... 																| Frequency=Weekly	| Reoccur=1				| StartDate=11/23/2002	| EndDate=04/15/2026	| ScheduledTime=23:50
| | Click	| id=next
| | Verify the following Integration Point Field has the following Error	| ${Weekly Send On Field}	| This field is required.


| Proceeding Post-Weekly Send On Validation
| | [Documentation]	| Verify that you can proceed to Step 2 after the Weekly Send On validation message has been shown.
| | [Tags]			| broken	| dam853
| | [Setup]			| Require Test Case	| Send On Weekly Validation message is shown after trying to Proceed with nothing selected
| | Fill out the Following Fields of the Integration Point Step 1 form	| SendOn=Monday
| | Proceed to the Next Page
| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that you are on the following Step of the Create Integration Point Process	| Connect to Source


| Reoccur Required message is displayed when blank
| | [Documentation]	| Verify that the Reoccur required message displays if you try to save with a blank Reoccur value.
| | [Tags]			| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append	| SourceProvider=LDAP	| DestinationRDO=Agent Type	| EnableScheduler=Yes	| 
| | ... 																| Frequency=Weekly	| SendOn=Monday			| Reoccur=${EMPTY}	| StartDate=11/23/2002	| EndDate=04/15/2026		| ScheduledTime=23:50
| | Click	| id=next
| | Verify the following Integration Point Field has the following Error	| ${Reoccur Field}	| This field is required.


| Reoccur Validation message is displayed for Zero
| | [Documentation]	| Verify that the Reoccur validation does not accept Zero
| | [Tags]			| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append	| SourceProvider=LDAP	| DestinationRDO=Agent Type	| EnableScheduler=Yes	| 
| | ... 																| Frequency=Weekly	| SendOn=Monday			| Reoccur=0			| StartDate=11/23/2002	| EndDate=04/15/2026		| ScheduledTime=23:50
| | Click	| id=next
| | Verify the following Integration Point Field has the following Error	| ${Reoccur Field}	| Please enter a value between 1 and 999.


| Reoccur Validation message is displayed for negative integers
| | [Documentation]	| Verify that the Reoccur validation does not accept negative integers.
| | [Tags]			| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append	| SourceProvider=LDAP	| DestinationRDO=Agent Type	| EnableScheduler=Yes	| 
| | ... 																| Frequency=Weekly	| SendOn=Monday			| Reoccur=-1		| StartDate=11/23/2002	| EndDate=04/15/2026		| ScheduledTime=23:50
| | Click	| id=next
| | Verify the following Integration Point Field has the following Error	| ${Reoccur Field}	| Please enter a value between 1 and 999.


| Reoccur Validation message is displayed for integers above 999
| | [Documentation]	| Verify that the Reoccur validation does not accept integers above 999.
| | [Tags]			| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append	| SourceProvider=LDAP	| DestinationRDO=Agent Type	| EnableScheduler=Yes	| 
| | ... 																| Frequency=Weekly	| SendOn=Monday			| Reoccur=1000		| StartDate=11/23/2002	| EndDate=04/15/2026		| ScheduledTime=23:50
| | Click	| id=next
| | Verify the following Integration Point Field has the following Error	| ${Reoccur Field}	| Please enter a value between 1 and 999.


| Reoccur Validation message is displayed for non-integers
| | [Documentation]	| Verify that the Reoccur field's validation does not accept non-integers.
| | [Tags]			| required	| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append	| SourceProvider=LDAP	| DestinationRDO=Agent Type	| EnableScheduler=Yes	| 
| | ... 																| Frequency=Weekly	| SendOn=Monday			| Reoccur=a			| StartDate=11/23/2002	| EndDate=04/15/2026		| ScheduledTime=23:50
| | Click	| id=next
| | Verify the following Integration Point Field has the following Error	| ${Reoccur Field}	| Please enter a value between 1 and 999.


| Proceeding Post-Reoccur Validation
| | [Documentation]	| 
| | [Tags]			| automated
| | [Setup]			| Require Test Case	| Reoccur Validation message is displayed for non-integers
| | Fill out the Following Fields of the Integration Point Step 1 form	| Reoccur=2
| | Proceed to the Next Page
| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that you are on the following Step of the Create Integration Point Process	| Connect to Source


| Integration Points can be created with an End Date in the Past
| | [Documentation]	| Verify that Integration Points can be created with an End Date value that is in the Past.
| | [Tags]			| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append		| SourceProvider=LDAP	| DestinationRDO=Agent Type	| EnableScheduler=Yes	| 
| | ... 																| Frequency=Daily	| StartDate=11/23/2002	| EndDate=04/15/2006	| ScheduledTime=23:50
| | Proceed to the Next Page
| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that you are on the following Step of the Create Integration Point Process	| Connect to Source


*** Settings ***
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${METHOD_KEYWORD_DIR}/MiscMethodKeywords.txt
| Resource	| ${METHOD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| C:/SourceCode/LDAPSync/QA/Keywords/IntegrationPointKeywords.txt