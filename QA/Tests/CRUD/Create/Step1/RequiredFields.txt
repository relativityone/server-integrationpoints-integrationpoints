
*** Settings ***
| Documentation 	| Verify that the fields of Step 1 on the Create Integration Point wizard have the correct Required Field validation.
| Metadata		    | Authors	| Daniel Oliver	| William Sehy
| Force Tags		| in-progress	| team.method
| Suite Setup		| Open and Log in to Method
| Test Setup		| Open Create Integration Point
| Test Teardown 	| Run Keyword if Test Failed	| Capture Page Screenshot
| Suite Teardown	| Close All Browsers


*** Variables ***

| ${WorkspaceName}			| Relativity Legal Hold
| ${Integration Name Field}	| name
| ${Destination RDO Field}	| s2id_destinationRdo


*** Test Cases ***

| The Integration Name field display an error message when empty
| | [Documentation]	| Verify that leaving the Integration Name field blank displays a warning message after clicking Next.
| | [Tags]			| required	| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| Overwrite=Append		| SourceProvider=LDAP	| DestinationRDO=Agent Type	| EnableScheduler=No
| | Click	| id=next
| | Verify the following Integration Point Field has the following Error	| ${Integration Name Field}	| This field is required.


| Proceeding Post-Integration Name Validation
| | [Documentation]	| Verify that you can proceed to Step 2 after the Integration Name validation message has been shown.
| | [Tags]			| automated
| | [Setup]			| Require Test Case	| The Integration Name field display an error message when empty
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing
| | Proceed to the Next Page
| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that you are on the following Step of the Create Integration Point Process	| Connect to Source


| The Destination RDO field displays an error message when empty
| | [Documentation]	| Verify that leaving the Integration Name field as Select... displays a warning message after clicking Next.
| | [Tags]			| required	| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append		| SourceProvider=LDAP	| EnableScheduler=No
| | Click	| id=next
| | Verify the following Integration Point Field has the following Error	| ${Destination RDO Field}	| This field is required.


| Proceeding Post-Destination RDO Validation
| | [Documentation]	| Verify that you can proceed to Step 2 after the Destination RDO validation message has been shown.
| | [Tags]			| automated
| | [Setup]			| Require Test Case	| The Destination RDO field displays an error message when empty
| | Fill out the Following Fields of the Integration Point Step 1 form	| DestinationRDO=Agent Type
| | Proceed to the Next Page
| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that you are on the following Step of the Create Integration Point Process	| Connect to Source


*** Settings ***
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${METHOD_KEYWORD_DIR}/MiscMethodKeywords.txt
| Resource	| ${METHOD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource  | C:/SourceCode/LDAPSync/QA/Keywords/IntegrationPointKeywords.txt