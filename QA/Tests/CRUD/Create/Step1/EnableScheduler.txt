
*** Settings ***
| Documentation 	| Verify that the Enable Scheduler field on Step 1 of the Create Integration Point wizard functions correctly.
| Metadata		    | Authors	| Daniel Oliver	| William Sehy
| Force Tags		| in-progress	| team.method
| Suite Setup		| Open and Log in to Method
| Test Setup		| Open Create Integration Point
| Test Teardown 	| Run Keyword if Test Failed	| Capture Page Screenshot
| Suite Teardown	| Close All Browsers


*** Variables ***

| ${WorkspaceName}	| LDAP Sync


*** Test Cases ***

| Setting the Enable Scheduler field to Yes shows the Schedule fields
| | [Documentation]	| Verify that changing the Enable Scheduler field to Yes shows the Schedule fields
| | ...				| [id] 0eec6f80-a89f-11e4-813e-b8ca3a83048f
| | [Tags]			| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| EnableScheduler=Yes

| | Verify that the following section contains the following fields	| Import Scheduling	| 
| | ... | Enable Scheduler:	| 
| | ... | Frequency:        | 
| | ... | Start Date:		| 
| | ... | End Date:		    | 
| | ... | Scheduled Time:	


| Setting the Enable Scheduler field back to No Hides the Schedule fields
| | [Documentation]	| Verify that changing the Enable Scheduler field back to No Hides the Schedule fields
| | ...				| [id] 0eefcade-a89f-11e4-bc08-b8ca3a83048f
| | [Tags]			| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| EnableScheduler=Yes
| | Fill out the Following Fields of the Integration Point Step 1 form	| EnableScheduler=No

| | Verify that the following section contains the following fields	| Import Scheduling	| 
| | ... | Enable Scheduler:


| Step 2 can be reached with the Enabled field set to No
| | [Documentation]	| Verify that Step 2 can be shown after filling out the required fields and leaving the Scheduler set to No
| | ...				| [id] 0eefcadf-a89f-11e4-a8b3-b8ca3a83048f
| | [Tags]			| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append	| SourceProvider=LDAP	| DestinationRDO=Document	| EnableScheduler=No
| | Proceed to the Next Step of the Integration Point wizard
| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that you are on the following Step of the Create Integration Point Process	| Connect to Source


| Step 2 can be reached with the Enabled field set to Yes
| | [Documentation]	| Verify that Step 2 can be shown after filling out the required fields and providing valid Scheduler settings
| | ...				| [id] 0eefcae0-a89f-11e4-8d1c-b8ca3a83048f
| | [Tags]			| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append		| SourceProvider=LDAP	| DestinationRDO=Document	| 
| | ... 																| EnableScheduler=Yes	| Frequency=Daily	| StartDate=11/23/2020	| EndDate=04/15/2021	| ScheduledTime=23:32
| | Proceed to the Next Step of the Integration Point wizard
| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that you are on the following Step of the Create Integration Point Process	| Connect to Source


| Create with the Scheduler set to Yes and only partly filled out, then set to No
| | [Documentation]	| Verify that Step 2 can be shown after filling out some of the Scheduler's required fields and then disabling the Scheduler
| | ...				| [id] 0eefcae1-a89f-11e4-a60b-b8ca3a83048f
| | [Tags]			| automated
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Scheduler Fields Testing	| Overwrite=Append		| SourceProvider=LDAP	| 
| | ... 																| DestinationRDO=Document					| EnableScheduler=Yes	| Frequency=Daily
| | Fill out the Following Fields of the Integration Point Step 1 form	| EnableScheduler=No
| | Proceed to the Next Step of the Integration Point wizard
| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that you are on the following Step of the Create Integration Point Process	| Connect to Source


*** Settings ***
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${METHOD_KEYWORD_DIR}/MiscMethodKeywords.txt
| Resource	| ${METHOD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource  | C:/SourceCode/LDAPSync/QA/Keywords/IntegrationPointKeywords.txt