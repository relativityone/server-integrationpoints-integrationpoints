
*** Settings ***
| Documentation 	| Verify that the Map Fields step of Integration Points allows RDO fields to be selected.
| Metadata			| Authors	| Daniel Oliver
| Force Tags		| in-progress	| team.game_of_holds	| feature.integration_points
| Suite Setup		| Open and Log in to Method
| Test Setup		| Go to Step 3 of the Create Integration Point Process
| Test Teardown 	| Capture screenshot if test fails
| Suite Teardown	| Close all browsers


*** Variables ***

| ${WorkspaceName}	| LDAP Sync
| ${Field1}			| Email
| ${Field2}			| Notes
| ${Workspace Field List}				| workspace-fields
| ${Selected Workspace List}			| selected-workspace-fields
| ${Add All Workspace Fields Icon}		| add-all-workspace-fields
| ${Add Workspace Field Icon}			| add-workspace-field
| ${Remove All Workspace Fields Icon}	| remove-all-workspace-fields


*** Test Cases ***

| An Individual Field can be selected and put into the Selected list
| | [Documentation] | Verify that one field can be moved from the Workspace list to the Selected Workspace list
| | ...				| [id] 116d9c1e-a89f-11e4-acd7-b8ca3a83048f
| | [Tags]			| automated
| | Move the Following Workspace Fields to the Selected List	| ${Field1}

| | Verify that the Assigned Workspace Listbox has the following Fields in order | ${Field1}


| Fields are removed from the Workspace list when they are put into the Selected Workspace list
| | [Documentation] | Verify that moving a field from the Workspace list to the Selected list, removes the field from the Workspace list
| | ...				| [id] 116f70e1-a89f-11e4-b72b-b8ca3a83048f
| | [Tags]			| automated
| | Move the Following Workspace Fields to the Selected List	| ${Field1}

| | Verify that the following field is not in the following list	| ${Field1} | ${Workspace Field List} 


| Multiple Individual fields can be selected and moved individually
| | [Documentation] | Verify that one field can be moved to the Selected Workspace list, and the process can then be repeated for another field with the newly moved field placed at the end of the list.
| | ...				| [id] 116f70e2-a89f-11e4-9d86-b8ca3a83048f
| | [Tags]			| automated
| | Move the Following Workspace Fields to the Selected List	| ${Field1}	| ${Field2}

| | Verify that the Assigned Workspace Listbox has the following Fields in order | ${Field1} | ${Field2}


| Mutliple Fields can be selected and moved with one click 
| | [Documentation] | Verify that mutliple Fields can be selected, and then they are all moved by clicking the Move to Selected list icon. The sort order of the newly moved fields is then alphabetical (assuming they were just in alphabetical order).
| | ...				| [id] 116f70e3-a89f-11e4-acb3-b8ca3a83048f
| | [Tags]			| automated
| | Click Workspace Field	| ${Field2}
| | Click Workspace Field	| ${Field1}
| | Click	| ${Add Workspace Field Icon}

| | Verify that the Assigned Workspace Listbox has the following Fields in order | ${Field1} | ${Field2}


| Fields are removed from the Selected Workspace list when they are put into the Workspace list
| | [Documentation] | Verify that moving a field from the Selected Workspace list to the Workspace list, removes the field from the Selected Workspace list
| | ...				| [id] 116f70e4-a89f-11e4-87b5-b8ca3a83048f
| | [Tags]			| automated
| | Move the Following Workspace Fields to the Selected List	| ${Field1}
| | Move the Following Workspace Fields from the Selected List	| ${Field1}

| | Verify that the following field is not in the following list	| ${Field1} | ${Selected Workspace List} 


| Moving a field back to Workspace Fields adds it to the bottom of the list
| | [Documentation] | Verify that moving a field back to the Workspace field list places the field at the bottom of the list.
| | ...				| [id] 116fe611-a89f-11e4-b602-b8ca3a83048f
| | [Tags]			| automated
| | Move the Following Workspace Fields to the Selected List	| ${Field1}
| | @{UnassignedFields}=	| Get the Unassigned Workspace Fields
| | Move the Following Workspace Fields from the Selected List	| ${Field1}

| | Verify that the Unassigned Workspace Listbox has the following Fields in order	| @{UnassignedFields}	| ${Field1}


| Clicking the Move All Workspace icon moves all fields from the Workspace Fields to Selected Fields 
| | [Documentation] | Verify that clicking the Move All Workspace icon moves all fields from the Workspace Fields to Selected Fields 
| | ...				| [id] 116fe612-a89f-11e4-ac0a-b8ca3a83048f
| | [Tags]			| automated
| | @{AllFields}=	| Get the Unassigned Workspace Fields
| | Click	| ${Add All Workspace Fields Icon}

| | Run Keyword and Continue on Failure	| Verify that the Unassigned Workspace Listbox has no Fields in it
| | Run Keyword and Continue on Failure	| Verify that the Assigned Workspace Listbox has the following Fields in order	| @{AllFields} 


| Clicking the Move All Selected icon moves all fields from the Selected Fields list to the Unselected list
| | [Documentation] | Verify that clicking the Move All Workspace icon moves all fields from the Selected Fields list to the Workspace Field list
| | ...				| [id] 116fe613-a89f-11e4-a08c-b8ca3a83048f
| | [Tags]			| automated
| | @{AllFields}=	| Get the Unassigned Workspace Fields
| | Click	| ${Add All Workspace Fields Icon}
| | Click	| ${Remove All Workspace Fields Icon}

| | Run Keyword and Continue on Failure	| Verify that the Assigned Workspace Listbox has no Fields in it
| | Run Keyword and Continue on Failure	| Verify that the Unassigned Workspace Listbox has the following Fields in order	| @{AllFields}


| Double clicking a field moves it
| | [Documentation]	| Verify that a double clicking a field moves it to the other field, in this case from Workspace to Selected Workspace.
| | ... 			| [id] 116fe618-a89f-11e4-b5da-b8ca3a83048f
| | [Tags]			| automated
| | Double Click Workspace Field	| ${Field1}

| | Verify that the Assigned Workspace Listbox has the following Fields in order | ${Field1}


| Multiple Fields can be selected be clicking and dragging
| | [Documentation] | Verify that multiple fields can be seleted by clicking one field and dragging.
| | ...				| [id] 116fe614-a89f-11e4-b2d2-b8ca3a83048f
| | [Tags]			| manual	| dragging
| | Run the following manual steps:
| | ... | Steps:
| | ... | 1. Click one field in the Workspace Fields list.
| | ... | 2. Drag over several other fields.
| | ... | 
| | ... | Expected Results:
| | ... | Verify that each field that was dragged over is highlighted (selected)


| Mutliple Fields can be Control Clicked
| | [Documentation] | Verify that multiple fields can be seleted by control clicking
| | ...				| [id] 116fe615-a89f-11e4-aac7-b8ca3a83048f
| | [Tags]			| manual	| control_clicking
| | Run the following manual steps:
| | ... | Steps:
| | ... | 1. Click one field in the Workspace Fields list.
| | ... | 2. Control click several other fields.
| | ... | 
| | ... | Expected Results:
| | ... | Verify that each field that was clicked is highlighted (selected)


| Selected field rows are blue
| | [Documentation] | Verify that clicking a field turns its row blue
| | ...				| [id] 116fe616-a89f-11e4-8867-b8ca3a83048f
| | [Tags]			| manual	| visual
| | Run the following manual steps:
| | ... | Steps:
| | ... | 1. Click one field in the Workspace Fields list.
| | ... | 
| | ... | Expected Results:
| | ... | Verify that the row color for the selected field is blue


| Selected field rows on inactive lists are grey
| | [Documentation] | Verify that a selected row turns grey when clicking anywhere else on the screen
| | ...				| [id] 116fe617-a89f-11e4-b36e-b8ca3a83048f
| | [Tags]			| manual	| visual
| | Run the following manual steps:
| | ... | Steps:
| | ... | 1. Click one field in a Workspace Fields list
| | ... | 2. Click any whitespace outside of the Workspace Fields list
| | ... | 
| | ... | Expected Results:
| | ... | Verify that the row color for the selected field is grey


| Selected field rows on inactive lists can still be moved
| | [Documentation] | Verify that a grey row can still be moved.
| | ...				| 
| | [Tags]			| manual	| visual
| | Run the following manual steps:
| | ... | Precondions:
| | ... | 1. The Test Case "Selected field rows on inactive lists are grey" passed
| | ... | 
| | ... | Steps:
| | ... | 1. Click one field in a Workspace Fields list
| | ... | 2. Click any whitespace outside of the Workspace Fields list
| | ... | 3. Click the icon to move that field to the other Workspace Fields list
| | ... | 
| | ... | Expected Results:
| | ... | Verify that the row is moved to the other Workspace Fields list


| Hovering over a Selected field with no mapped field
| | [Documentation] | Verify that hovering over a Selected Workspace field with no mapped Attribute field affects the Workspace field correctly.
| | [Tags]			| manual	| mouse_hovering
| | Run the following manual steps:
| | ... | Preconditions:
| | ... | 1. At least three fields are selected on each side
| | ... | 2. There are two Selected Workspace fields with no mapped Attribute field
| | ... | 
| | ... | Steps:
| | ... | 1. Hover over over both of the Selected Workspace fields with no mapped Attribute field
| | ... | 
| | ... | Expected Results:
| | ... | Verify that the only the field you are hovering over is underlined


| Hovering over a Selected field with a mapped field
| | [Documentation] | Verify that hovering over a Selected Workspace field with a mapped Attribute field affects the both fields correctly.
| | [Tags]			| manual	| mouse_hovering
| | Run the following manual steps:
| | ... | Preconditions:
| | ... | 1. At least three fields are selected on each side
| | ... | 
| | ... | Steps:
| | ... | 1. Hover over over each of the Selected Workspace fields with mapped Attributes field
| | ... | 
| | ... | Expected Results:
| | ... | Verify that the only the field you are hovering over is underlined


| Hovering over the whitespace below the last Selected Workspace field acts the same as hovering over the last Selected Workspace field
| | [Documentation] | Verify that hovering over the whitespace below the last Selected Workspace field acts the same as hovering over the last Selected Workspace field.
| | [Tags]			| manual	| mouse_hovering
| | Run the following manual steps:
| | ... | Preconditions:
| | ... | 1. At least three fields are selected on each side
| | ... | 2. There is one Selected Workspace field with no mapped Attribute field
| | ... | 
| | ... | Steps:
| | ... | 1. Hover over the whitespace below the last Selected Workspace field
| | ... | 2. Assign a new Attribute field
| | ... | 3. Hover over the whitespace below the last Selected Workspace field
| | ... | 
| | ... | Expected Results:
| | ... | Verify that the hovering over the whitespace below the last Selected Workspace field acts the same as hovering over the last Selected Workspace field


*** Keywords ***

| Verify that the Unassigned Workspace Listbox has no Fields in it
| | [Documentation]	| Verifies that the unassigned Workspace listbox currently has no fields in it.
| | Wait Until Page Contains Element	| id=workspace-fields	| 10s	| The unassigned Workspace Fields List Box never loaded.
| | Page Should Not Contain Element	| xpath=//select[@id='workspace-fields']/child::option	| The unassigned Workspace Listbox had a field in it when it should have been empty.

| Verify that the Assigned Workspace Listbox has no Fields in it
| | [Documentation]	| Verifies that the assigned Workspace listbox currently has no fields in it.
| | Wait Until Page Contains Element	| id=selected-workspace-fields	| 10s	| The assigned Workspace Fields List Box never loaded.
| | Page Should Not Contain Element	| xpath=//select[@id='selected-workspace-fields']/child::option	| The assigned Workspace Listbox had a field in it when it should have been empty.

| Double Click Workspace Field
| | [Documentation] | A keyword that double clicks an item from the (unselected) Workspace Field list.
| | [ARGUMENTS]		| ${Field}
| | Wait Until Keyword Succeeds	| 20s	| 2s	| Element should be visible	| xpath=//select[@id='workspace-fields']	| The Workspace Fields list box was not visible.
| | Click Element	| xpath=//select[@id='workspace-fields']/option[text()='${Field}']	| # The field must be selected before double clicking in automation works.
| | Double Click Element	| xpath=//select[@id='workspace-fields']/option[text()='${Field}']

| Verify that the following field is not in the following list
| | [Documentation] | A keyword that verifies that the specified field is not present in the specified list.
| | [ARGUMENTS]		| ${FieldName}	| ${ListName}
| | Element should not be visible	| xpath=//select[@id='${ListName}']/option[text()='${FieldName}']

| Go to Step 3 of the Create Integration Point Process
| | Wait Until Keyword Succeeds	| 10s	| 2s	| Open Create Integration Point
| | Fill out the Following Fields of the Integration Point Step 1 form | Moving Workspace Field tests | Append/Overlay | LDAP | Custodian | No
| | Proceed to the Next Step of the Integration Point wizard
| | Fill out the Following Fields of the LDAP Integration Point Step 2 form | ConnectionPath=testing.corp/CN=Users,DC=testing,DC=corp	| Authentication=FastBind
| | Proceed to the Next Step of the Integration Point wizard


*** Settings ***
| Library	| Collections
| Library	| ${KEYWORD_DIR}/ManualKeywords.py
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/MiscLegalHoldKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/IntegrationPointKeywords.txt
