
*** Settings ***
| Documentation 	| Verify that the Field Mappings card on ther Map Fields step of the Integration Points wizards shows up and has the correct fields and icons in the correct orders.
| Metadata			| Authors	| Daniel Oliver	| William Sehy
| Force Tags		| in-progress	| team.game_of_holds	| feature.integration_points
| Suite Setup		| Go to Step 3 of the Create Integration Point Process
#| Test Setup		| NONE
| Test Teardown 	| Capture Screenshot If Test Fails
| Suite Teardown	| Close All Browsers


*** Variables ***

| ${WorkspaceName}	| LDAP Sync
| ${WorkspaceFieldListID}					| workspace-fields
| ${SelectedFieldListID}					| selected-workspace-fields
| ${SelectedAttributeListID}				| selected-source-fields
| ${SourceAttributeListID}					| source-fields
| ${AddAllWorkspaceFieldsIcon}				| add-all-workspace-fields
| ${AddWorkspaceFieldIcon}					| add-workspace-field
| ${RemoveWorkspaceFieldIcon}				| remove-workspace-field
| ${RemoveWAllWorkspaceFieldsIcon}			| remove-all-workspace-fields
| ${Move Workspace Field to Top Icon}		| workspaceFieldTop
| ${MoveWorkspaceFieldUpIcon}				| workspace-field-up
| ${MoveWorkspaceFieldDownIcon}				| workspace-field-down
| ${Move Workspace Field to Bottom Icon}	| workspaceFieldBottom
| ${Move Attribute Field to Top Icon}		| attributeFieldTop
| ${MoveAttributeFieldUpIcon}				| attribute-field-up
| ${MoveAttributeFieldDownIcon}				| attribute-field-down
| ${Move Attribute Field to Bottom Icon}	| attributeFieldBottom
| ${AddAllSourceFieldsIcon}					| add-all-source-fields
| ${AddSourceFieldIcon}						| add-source-field
| ${RemoveSourceFieldIcon}					| remove-source-field
| ${RemoveAllSourceFieldsIcon}				| remove-all-source-fields


*** Test Cases ***

| The Map Fields screen has a card with the Title Text Field Mappings
| | [Documentation]	| Verify that the Map Fields screen has a card with the title text "Field Mappings".
| | ...				| [id] 115c5e11-a89f-11e4-b5fc-b8ca3a83048f
| | [Tags]			| automated
| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that a Card with the following Title Text is visible	| Field Mappings


| The Field Mappings fields are in the correct order
| | [Documentation]	| Verify that the fields on the Field Mappings card are sorted correctly.
| | ...				| [id] 115ea800-a89f-11e4-bc2e-b8ca3a83048f
| | [Tags]			| automated	| dan850
| | Verify that the following Card contains Fields with the following IDs	| Field Mappings	| 
| | ... | ${WorkspaceFieldListID}		| 
| | ... | ${SelectedFieldListID}		| 
| | ... | ${SelectedAttributeListID}	| 
| | ... | ${SourceAttributeListID}


| The Fields are of the correct type
| | [Documentation]	| Verify that the Fields on the Map Fields card are the correct type.
| | ...				| [id] 115ea802-a89f-11e4-abb8-b8ca3a83048f
| | [Tags]			| automated
| | Run keyword and continue on failure | Verify that the following field ID has a listbox input | ${WorkspaceFieldListID}
| | Run keyword and continue on failure | Verify that the following field ID has a listbox input | ${SelectedFieldListID} 
| | Run keyword and continue on failure | Verify that the following field ID has a listbox input | ${SelectedAttributeListID} 
| | Run keyword and continue on failure | Verify that the following field ID has a listbox input | ${SourceAttributeListID}


| The Workspace Fields List Change Icons are present and sorted correctly
| | [Documentation]	| Verify that the Workspace Fields listbox movement Icons are present and sorted correctly.
| | ...				| [id] 115ea803-a89f-11e4-9cfd-b8ca3a83048f
| | [Tags]			| automated
| | Verify that the following icons are grouped together and sorted correctly	| 
| | ... | ${AddAllWorkspaceFieldsIcon}	| ${AddWorkspaceFieldIcon}	| ${RemoveWorkspaceFieldIcon}	| ${RemoveWAllWorkspaceFieldsIcon}


| The Source Attribute List Change Icons are present and sorted correctly
| | [Documentation]	| Verify that the Source Attributes listbox movement Icons are present and sorted correctly.
| | ...				| [id] 115ea804-a89f-11e4-99aa-b8ca3a83048f
| | [Tags]			| automated	| dan850
| | Verify that the following icons are grouped together and sorted correctly	| 
| | ... | ${AddAllSourceFieldsIcon}	| ${AddSourceFieldIcon}	| ${RemoveSourceFieldIcon}	| ${RemoveAllSourceFieldsIcon}


| Workspace Fields Up and Down Icons are present and sorted correctly
| | [Documentation]	| Verify that the Workspace Fields Up and Down movement icons are present and sorted correctly.
| | ...				| [id] 115ea805-a89f-11e4-b854-b8ca3a83048f
| | [Tags]			| untested
| | Verify that the following icons are grouped together and sorted correctly	| 
| | ... | ${Move Workspace Field to Top Icon}		| ${MoveWorkspaceFieldUpIcon}	| ${MoveWorkspaceFieldDownIcon}	| ${Move Workspace Field to Bottom Icon}


| Source Attribute Up and Down Icons are present and sorted correctly
| | [Documentation]	| Verify that the Source Attributes Up and Down movement icons are present and sorted correctly.
| | ...				| [id] 115ea806-a89f-11e4-8e69-b8ca3a83048f
| | [Tags]			| untested
| | Verify that the following icons are grouped together and sorted correctly	| 
| | ... | ${Move Attribute Field to Top Icon}	| ${MoveAttributeFieldUpIcon}	| ${MoveAttributeFieldDownIcon}	| ${Move Attribute Field to Bottom Icon}


*** Keywords *** 

| Verify that the following Card contains Fields with the following IDs
| | [ARGUMENTS]		| ${CardTitle}	| @{ExpectedFieldIDs}
| | Verify that a Card with the following Title Text is visible	| ${CardTitle}
| | :FOR	| ${FieldID}	| IN	| @{ExpectedFieldIDs}
| | | Run Keyword and Continue on Failure	| Element Should Be Visible	| xpath=//div[@class='card' and descendant::text()="${CardTitle}"]/descendant::*[@id="${FieldID}"]

| Verify that the following icons are grouped together and sorted correctly
| | [Documentation]	| A keyword that verifies that a set of icons, on the same element, are present and in the specified order.
| | [ARGUMENTS]		| @{ExpectedIconIDs}
| | ${ExpectedXpath}=	| Set Variable  | xpath=//*[@id='${ExpectedIconIDs[0]}']
| | Element Should Be Visible | ${ExpectedXpath}  | The icon with the ID "${ExpectedIconIDs[0]}" was not visible.
| | ${IconCount}=	| Get Length	| ${ExpectedIconIDs}
| | :FOR  | ${i}  | IN RANGE  | 1 | ${IconCount}
| | | ${ExpectedXpath}= | Set Variable   | ${ExpectedXpath}/following-sibling::*[@id='${ExpectedIconIDs[${i}]}']
| | | Element Should Be Visible	| ${ExpectedIconIDs[${i}]} | The icon with the ID "${ExpectedIconIDs[${i}]}" was not visible.
| | | Element Should Be Visible	| ${ExpectedXpath} | The icon with the ID "${ExpectedIconIDs[${i}]}" was out of order.

| Go to Step 3 of the Create Integration Point Process
| | Open and Log in to Legal Hold
| | Open Create Integration Point
| | Fill out the Following Fields of the Integration Point Step 1 form	| 
| | ... | IntegrationName=Field Mapping Card Testing	| 
| | ... | Overwrite=Append/Overlay						| 
| | ... | SourceProvider=LDAP							| 
| | ... | DestinationRDO=Questionnaire Question			| 
| | ... | EnableScheduler=No
| | Proceed to the Next Step of the Integration Point wizard
| | Fill out the Following Fields of the LDAP Integration Point Step 2 form	| 
| | ... | ConnectionPath=testing.corp/CN=Users,DC=testing,DC=corp	| 
| | ... | ObjectFilterString=(objectClass=*)		| 
| | ... | Authentication=FastBind					| 
| | ... | ConnectionUsername=testing\\administrator	| 
| | ... | ConnectionPassword=P@ssw0rd@1				| 
| | ... | ImportNestedItems=Yes
| | Proceed to the Next Step of the Integration Point wizard


*** Settings ***
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/MiscLegalHoldKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/IntegrationPointKeywords.txt
