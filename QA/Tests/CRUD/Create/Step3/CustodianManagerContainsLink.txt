*** Settings ***
| Documentation 	| Verify that the Custodian Manager Contains Link field appears and functions correctly. 
| Metadata		    | Authors	| Daniel Oliver	| William Sehy
| Force Tags		| in-progress	| team.game_of_holds	| feature.integration_points
| Suite Setup		| Open and Log in to Legal Hold and Go to Step 3 using the Custodian Destination RDO
#| Test Setup		| NONE
| Test Teardown 	| Capture screenshot if test fails
| Suite Teardown	| Close All Browsers


*** Variables ***

| ${WorkspaceName}	| LDAP Sync
| ${Manager Contains Link Radio Buttons}	| enable


*** Test Cases ***

| Custodian Manager Contains Link is shown for Custodian Integration Points
| | [Documentation] | Verify that the "Custodian Manager Contains Link" field is shown on Step 3 
| | ... 			| of the Integration Point wizard when the Destination RDO is Custodian.
| | ... 			| [id] a4804940-b605-11e4-82f6-90b11c6c5ae4
| | [Tags]			| automated
| | Verify that the following Card contains the following Fields in Order	| Import Settings	| 
| | ... | Unique Identifier:				| 
| | ... | Custodian Manager\nContains Link:	| # \n = newline


| Custodian Manager Contains Link is a field of Yes/No Radio Buttons 
| | [Documentation] | Verify that the "Custodian Manager Contains Link" field is a Yes/No radio button field.
| | [Tags]			| automated
| | Verify that the following radio buttons are grouped together	| ${Manager Contains Link Radio Buttons}	| Yes	| No


| Custodian Manager Contains Link defaults to Yes
| | [Documentation] | Verify that the Custodian Manager Contains Link defaults to Yes.
| | ...				| [id] ae2ca28f-b605-11e4-a642-90b11c6c5ae4
| | [Tags]			| automated
| | Radio Button Should Be Set To	| ${Manager Contains Link Radio Buttons}	| true	| # true = Yes


| Custodian Manager Contains Link is not shown for Custodian Role Integration Points
| | [Documentation] | Verify that the Custodian Manager Contains Link is not shown on Step 3 of the Integration Point wizard when the Destination RDO is Custodian Role.
| | [Tags]			| automated
| | [Setup]			| Go to Step 3 of the Integration Point wizard using the following Destination RDO	| Custodian Role
| | Verify that the following Card contains the following Fields in Order	| Import Settings	| 
| | ... | Unique Identifier:	| 
| | ... | Parent Attribute:		| # shown because it is a child RDO


| Custodian Manager Contains Link is not shown for Custodian Status Integration Points
| | [Documentation] | Verify that the Custodian Manager Contains Link is not shown on Step 3 of the Integration Point wizard when the Destination RDO is Custodian Status.
| | [Tags]			| automated
| | [Setup]			| Go to Step 3 of the Integration Point wizard using the following Destination RDO	| Custodian Status
| | Verify that the following Card contains the following Fields in Order	| Import Settings	| 
| | ... | Unique Identifier:	| 
| | ... | Parent Attribute:		| # shown because it is a child RDO


| Custodian Manager Contains Link is not shown for Non-Custodian Integration Points
| | [Documentation] | Verify that the Custodian Manager Contains Link is not shown on Step 3 of the Integration Point wizard when the Destination RDO does not contain the word "Custodian".
| | [Tags]			| automated
| | [Setup]			| Go to Step 3 of the Integration Point wizard using the following Destination RDO	| Project
| | Verify that the following Card contains the following Fields in Order	| Import Settings	| 
| | ... | Unique Identifier:


*** Keywords *** 

| Open and Log in to Legal Hold and Go to Step 3 using the Custodian Destination RDO
| | Open and Log in to Legal Hold
| | Go to Step 3 of the Integration Point wizard using the following Destination RDO	| Custodian

| Go to Step 3 of the Integration Point wizard using the following Destination RDO
| | [Documentation]	| 
| | [ARGUMENTS] 	| ${DestinationRDO}
| | Open Create Integration Point
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=Manager Field Testing	| SourceProvider=LDAP	| DestinationRDO=${DestinationRDO}	| EnableScheduler=No
| | Proceed to the Next Step of the Integration Point wizard
| | Fill out the Following Fields of the LDAP Integration Point Step 2 form	| 
| | ... | ConnectionPath=testing.corp/CN=Users,DC=testing,DC=corp 
| | ... | ObjectFilterString=(objectClass=*)
| | ... | Authentication=FastBind
| | ... | ConnectionUsername=testing\\administrator
| | ... | ConnectionPassword=P@ssw0rd@1
| | ... | ImportNestedItems=Yes
| | Proceed to the Next Step of the Integration Point wizard


*** Settings ***
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/MiscLegalHoldKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource  | ${LDAP_KEYWORD_DIR}/DatabaseKeywords.txt
| Resource  | ${LDAP_KEYWORD_DIR}/IntegrationPointKeywords.txt
