
*** Settings ***
| Documentation 	| Verify that the Overlay Identifier dropdown has the correct field choices and the correct default field is selected for each Object Type.
| Metadata		    | Authors	| Daniel Oliver	| William Sehy
| Force Tags		| in-progress	| team.method
| Suite Setup		| Run Keywords	| Open and Log in to Method	| Make the Object Type List a Suite Variable
#| Test Setup		| Go to Step 3 of the Create Integration Point Process
| Test Teardown 	| Capture screenshot if test fails
| Suite Teardown	| Close all browsers


*** Variables ***

| ${WorkspaceName}			| Relativity Legal Hold
| ${ValidConnectionPath}	| 
| ${Overlay Identifier Dropdown}	| overlay-identifier


*** Test Cases ***

| The Default Value of Overlay Identifier is correct for all Object Types
| | [Template]	| Verify that the default Overlay Identifier populates correctly for the Object Type "${ObjectType}"
#| | Analytics Categorization Result
| | :FOR	| ${ObjectType}	| IN | @{ObjectTypes}
| | | ${ObjectType}

| The Options on the Overlay Identifier dropdown are correct for all Object Types
| | [Tags]		| broken	| uah066
| | [Template]	| Verify that the Overlay Identifier dropdown populates the correct fields for the Object Type "${ObjectType}"
#| | Analytics Categorization Result
| | :FOR	| ${ObjectType}	| IN | @{ObjectTypes}
| | | ${ObjectType}


*** Keywords *** 

| Determine the Unique Field on the following Object Type
| | [Documentation]	| Returns the Unique field for the given Object Type.
| | [ARGUMENTS]		| ${ObjectType}
| | ${2DGrid}=	| Query Workspace Database	|  
| | ... | SELECT f.DisplayName
| | ... | FROM [EDDSDBO].[Field] f
| | ... | JOIN [ObjectType] ot on ot.DescriptorArtifactTypeID = f.FieldArtifactTypeID
| | ... | WHERE ot.Name = '${ObjectType}'
| | ... | AND f.FieldCategoryID = 2	| # Unique Value
| | Should Not Be Empty	| ${2DGrid}	| No Unique fields were found for the Object Type "${ObjectType}".
| | [Return]		| ${2DGrid[0][0]}

| Verify that the default Overlay Identifier populates correctly for the Object Type "${ObjectType}"
| | Open Create Integration Point
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=${ObjectType} Overlay Options Test	| Overwrite=Append	| SourceProvider=LDAP	| DestinationRDO=${ObjectType}	| EnableScheduler=No
| | Proceed to the Next Page
| | Fill out the Following Fields of the Integration Point Step 2 form	| ConnectionPath=Dummy1	| Username=Dummy3	| Password=Dummy4	| ImportNestedItems=Yes	| # ObjectFilterString=Dummy2	| Authentication=Secure	
| | Proceed to the Next Page
| | ${UniqueField}=	| Determine the Unique Field on the following Object Type	| ${ObjectType}
| | Verify that the following select2 Dropdown currently has the following value selected	| ${Overlay Identifier Dropdown}	| ${UniqueField}
| | [Teardown]		| Run Keyword If Keyword Failed	| Capture Page Screenshot

| Verify that the Overlay Identifier dropdown populates the correct fields for the Object Type "${ObjectType}"
| | Open Create Integration Point
| | Fill out the Following Fields of the Integration Point Step 1 form	| IntegrationName=${ObjectType} Overlay Default Test	| Overwrite=Append	| SourceProvider=LDAP	| DestinationRDO=${ObjectType}	| EnableScheduler=No
| | Proceed to the Next Page
| | Fill out the Following Fields of the Integration Point Step 2 form	| ConnectionPath=Dummy1	| Username=Dummy3	| Password=Dummy4	| ImportNestedItems=Yes	| # ObjectFilterString=Dummy2	| Authentication=Secure	
| | Proceed to the Next Page
| | @{ExpectedFields}=	| Get the Non-System Fields for the following Object Type	| ${ObjectType}
| | Sort List	| ${ExpectedFields}
| | Click	| ${Overlay Identifier Dropdown}
| | Verify that the currently opened select2 Dropdown has the following choices in order	| @{ExpectedFields}
| | [Teardown]		| Run Keyword If Keyword Failed	| Capture Page Screenshot

| Make the Object Type List a Suite Variable
| | @{ObjectTypeList}=	| Get a List of All Object Types to Run Against
| | Set Suite Variable	| ${ObjectTypes}	| ${ObjectTypeList}

| Get a List of All Object Types to Run Against
| | ${2DGrid}=	| Query Workspace Database	|  
| | ... | SELECT Name
| | ... | FROM [EDDSDBO].[ObjectType]
| | ... | ORDER BY Name
| | @{1DGrid}=	| Flatten	| ${2DGrid}		| # Converts from 2D grid to 1D grid
| | @{ObjectFields}=	| Set Variable	| @{1DGrid}	| # Converts from 1D grid to List
| | @{SystemFields}=	| Create List	| 
| | ... | Agent Type			|
| | ... | Batch					|
| | ... | Batch Set				|
| | ... | Code					|
| | ... | Event Handler			|
| | ... | Field					|
| | ... | Folder				|
| | ... | Group					|
| | ... | History				|
| | ... | Install Event Handler	|
| | ... | Integration Point		| 
| | ... | Layout				|
| | ... | Markup Set			|
| | ... | Mass Operation		|
| | ... | Object Rule			|
| | ... | Object Type			|
| | ... | Production			|
| | ... | Relativity Script		|
| | ... | Report				|
| | ... | Search				|
| | ... | Search Container		|
| | ... | Search Index			|
| | ... | Source Provider		| 
| | ... | Tab					|
| | ... | View					|
| | ... | Workspace
| | Remove Values From List	| ${ObjectFields}	| @{SystemFields}
| | [Return]		| @{ObjectFields}

| Get the Non-System Fields for the following Object Type
| | [Documentation]	| Gets a list of all of the fields on the given object, removes the five system fields, and then returns the remaining fields.
| | [ARGUMENTS]		| ${ObjectType}
| | ${2DGrid}=	| Query Workspace Database	|  
| | ... | SELECT DisplayName
| | ... | FROM [EDDSDBO].[Field] f
| | ... | JOIN [ObjectType] ot on ot.DescriptorArtifactTypeID = f.FieldArtifactTypeID
| | ... | WHERE ot.Name = '${ObjectType}'
| | Should Not Be Empty	| ${2DGrid}	| No fields were found for the Object Type "${ObjectType}".
| | @{1DGrid}=	| Flatten	| ${2DGrid}		| # Converts from 2D grid to 1D grid
| | @{NonSystemFields}=	| Set Variable	| @{1DGrid}	| # Converts from 1D grid to List
| | @{SystemFields}=	| Create List	| System Created By	| System Created On	| System Last Modified By	| System Last Modified On	| Artifact ID
| | Remove Values From List	| ${NonSystemFields}	| @{SystemFields}
| | Should Not Be Empty	| ${NonSystemFields}	| No non-object and non-system fields were found for the Object Type "${ObjectType}".
| | [Return]		| @{NonSystemFields}


*** Settings ***
| Library   | Collections
| Library	| ${KEYWORD_DIR}/ManualKeywords.py
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${METHOD_KEYWORD_DIR}/MiscMethodKeywords.txt
| Resource	| ${METHOD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource  | C:/SourceCode/LDAPSync/QA/Keywords/IntegrationPointKeywords.txt
