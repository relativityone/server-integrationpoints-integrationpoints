*** Settings ***
| Documentation  | Verify that the Configuration table's kCura.IntegrationPoints: IntegrationPointsWebAPIPath is correctly populated after installing RIP
| Force Tags     | in-progress
| Metadata		 | Authors	| Daniel Oliver
| Suite Setup	 | Create Suite Setup Data
| Suite Teardown | Restore Configuration values
| Test Setup     | Setup environment
| Test Teardown  | Teardown environment

*** Variables ***
| ${RIP_SECTION}		| kCura.IntegrationPoints
| ${RIP_NAME}			| WebAPIPath
| ${DBMT_SECTION}		| kCura.EDDS.DBMT
| ${DBMT_NAME}			| WebAPIPath
| ${PROCESSING_SECTION} | Relativity.Core
| ${PROCESSING_NAME}	| ProcessingWebAPIPath
| ${EMPTY_STRING}		| \
| ${workspace_Name}		| Config Test workspace
| ${Template_workspace} | kCura Starter Template
| ${Processing_value}   | http://localhost
| ${APPLICATION_NAME}	| Integration Points

*** Test Cases ***
| The RIP configuration value uses the DBMT WebAPIPath when RIP is installed on an environment without Processing
| | [Documentation] | Verify that the RIP configuration value is the same as the kCura.EDDS.DBMT>WebAPIPath value
| | ...				| [id] e6eaf60f-b7b1-11e4-9fe6-90b11c6c5ae4
| | [Tags] | automation
| | Delete Configuration value | ${RIP_SECTION} | ${RIP_NAME}
| | Set Configuration value | ${PROCESSING_SECTION} | ${PROCESSING_NAME} | ${EMPTY_STRING}
| | ${dbmt_value} | Get Configuration Value by section and name	| ${DBMT_SECTION} | ${DBMT_NAME}
| | Install Integration Points application
| | ${rip_value} | Get Configuration Value by section and name	| ${RIP_SECTION} | ${RIP_NAME}
| | Should Be Equal As Strings | ${rip_value} | ${dbmt_value}

| The RIP configuration value uses the ProcessingWebAPIPath when RIP is installed on an environment with Processing
| | [Documentation] | Verify that the RIP configuration value is the same as the Relativity.Core>ProcessingWebAPIPath value
| | ...				| [id] 809d02c0-b7b3-11e4-b2be-90b11c6c5ae4
| | [Tags] | automation
| | Delete Configuration value | ${RIP_SECTION} | ${RIP_NAME}
| | Set Configuration value | ${PROCESSING_SECTION} | ${PROCESSING_NAME} | ${Processing_value}
| | Install Integration Points application
| | ${rip_value} | Get Configuration Value by section and name	| ${RIP_SECTION} | ${RIP_NAME}
| | Should Be Equal As Strings | ${rip_value} | ${Processing_value}

| Check that the IntegrationPointsWebAPIPath description is correct
| | [Documentation] | Verify that the Description is correct for the IntegrationPointsWebAPIPath
| | ...				| [id] e6eaf60f-b7b1-11e4-9fe6-90b11c6c5ae48313cf21-b7b3-11e4-8964-90b11c6c5ae4
| | [Tags] | automation
| | Install Integration Points application
| | ${description} | Get Configuration Description by section and name	| ${RIP_SECTION} | ${RIP_NAME}
| | Should Be Equal As Strings | ${description} | The URL for the Windows Authenticated Relativity Web API endpoint used by integration points.

*** Keywords ***
| Setup environment
| | Log in to Relativity and enable RSAPI | ${Template_workspace}
| | Create workspace with the following name from the following template workspace | ${workspace_Name} | ${Template_workspace}
| | Close All Browsers

| Teardown environment
| | Query Master Database | update [EDDS].[eddsdbo].[Artifact] set [DeleteFlag] = 1 where [TextIdentifier] = '${workspace_Name}'
| | Close All Browsers

| Install Integration Points application
| | ${path} | Get Path to Newest Integration Points
| | Log in to Relativity and import application | ${workspace_Name} | ${APPLICATION_NAME} | ${path}

| Get Configuration Description by section and name
| | [arguments] | ${section} | ${name}
| | ${OriginalValue} | Query Master Database | select [Description] from [EDDS].[eddsdbo].[Configuration] where [Section] = '${section}' AND [Name] = '${name}'
| | [return] | ${OriginalValue[0][0]}

| Create Suite Setup Data
| | [Documentation]	| Create the Suite Preconditions
| | Setup connection to the relativity database
| | ${exists} | Check if configuration value exists | ${RIP_SECTION} | ${RIP_NAME}
| | ${v1}= | Run Keyword If | '${exists}' == 'PASS' | Get Configuration Value by section and name | ${RIP_SECTION} | ${RIP_NAME}
| | Set Suite Variable | ${RIP_WEB_API_PATH} | ${v1}
| | ${v2}= | Get Configuration Value by section and name	| ${DBMT_SECTION} | ${DBMT_NAME}
| | Set Suite Variable | ${DBMT_WEB_API_PATH} | ${v2}
| | ${v3}= | Get Configuration Value by section and name	| ${PROCESSING_SECTION} | ${PROCESSING_NAME}
| | Set Suite Variable | ${PROCESSING_WEB_API_PATH} | ${v3}


| Restore Configuration values
| | [Documentation]	| Restore values in the configuration table
| | Set Configuration value | ${RIP_SECTION} | ${RIP_NAME} | ${RIP_WEB_API_PATH}
| | Set Configuration value | ${DBMT_SECTION} | ${DBMT_NAME} | ${DBMT_WEB_API_PATH}
| | Set Configuration value	| ${PROCESSING_SECTION} | ${PROCESSING_NAME} | ${PROCESSING_WEB_API_PATH}

| Set Configuration value
| | [Arguments] | ${section} | ${name} | ${value}
| | Query Master Database | update [EDDS].[eddsdbo].[Configuration] set [Value] = '${value}' where [Section] = '${section}' AND [name] = '${name}'

| Delete Configuration value
| | [Arguments] | ${section} | ${name}
| | Query Master Database | delete from [EDDS].[eddsdbo].[Configuration] where Section = '${section}' AND [name] = '${name}'

| Check if configuration value exists
| | [Arguments] | ${section} | ${name}
| | ${OriginalValue} | Query Master Database | IF NOT EXISTS (SELECT * FROM [eddsdbo].[Configuration] WHERE [Section] = 'kCura.IntegrationPoints' AND [Name] = 'WebAPIPath')
| | ... | BEGIN
| | ... |	SELECT 0
| | ... | END
| | ... | ELSE
| | ... | BEGIN
| | ... | 	SELECT 1
| | ... | END
| | ${value}	| ${message} | Run Keyword And Ignore Error | should be equal | ${OriginalValue[0][0]} | 1
| | [return] | ${value}


| Install an application to workspace through rsapi
| | [Documentation]	| Imports one application into a workspace. Can be used to upgrade the Application as well.
| | [ARGUMENTS] 	| ${applicationPath}	| ${applicationName}	| ${Workspace}
| | ${WorkspaceID}=	| Get Workspace IDs	| ${Workspace}
| | Import application to workspace through RSAPI	| ${applicationPath}	| False
| | Run Keyword And Ignore Error	| Wait Until Keyword Succeeds	| 120 seconds	| 1 seconds	|
| | ... | Application Is Installed | ${workspaceID[0]} | ${applicationName}


*** Settings ***
| Library	| ${KEYWORD_DIR}/ManualKeywords.py
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Library	| ${METHOD_KEYWORD_DIR}/MethodPythonKeywords.py
| Library	| C:/SourceCode/LDAPSync/QA/Keywords/LDAPPythonKeywords.py
| Resource	| ${METHOD_KEYWORD_DIR}/BackendKeywords.txt
| Resource	| ${METHOD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| ${KEYWORD_DIR}/Relativity/Platform/DataGrid/Verificationkeyword.txt
| Resource	| ${KEYWORD_DIR}/Relativity/Platform/RelativityApplications/ADSSetupAndTeardownKeywords.txt
| Resource	| ${KEYWORD_DIR}/API/SDK/SDKKeywords.txt
| Resource	| ${KEYWORD_DIR}/API/SDK/SDKCreateKeywords.txt
| Resource  | ${KEYWORD_DIR}/Relativity/Search/Analytics/StructuredAnalyticsKeywords.txt