
*** Settings ***
| Documentation 	| This suite of tests verifies the Objects created upon the installation of the LDAP Sync application.
| ...				| This should be run immediately after installing the LDAP Sync application on the LDAP Sync workspace.
| Metadata			| Authors	| William Sehy
| Force Tags		| team.game_of_holds	| feature.integration_points	| in-progress
| Suite Setup		| Create a new Workspace and Import the Applications into it
#| Test Setup 		| NONE
| Test Teardown 	| Run Keyword If Test Failed	| Capture Page Screenshot
| Suite Teardown	| Delete the Workspace used in this Suite


*** Variables ***

| ${WorkspaceName}	| New LDAP Workspace


*** Test Cases ***

| Verify that the default Source Providers are created
| | [Documentation]	| Verifies that the default Source Provider created is "LDAP" and that no other Source Providers were created.
| | [Tags]			| automated
| | Verify that only the following Source Providers exist in the workspace	| LDAP


| Verify that the default Destination Providers are created
| | [Documentation]	| Verifies that the default Destination Provider created is "RDO" and that no other Destination Providers were created.
| | [Tags]			| automated
| | Verify that only the following Destination Providers exist in the workspace	| RDO


| Verify that the no default Integration Points are created
| | [Documentation]	| Verifies that no Integration Point was created by default. 
| | [Tags]			| automated
| | Verify that no Integration Points exist in the workspace


*** Keywords ***

| Verify that only the following Source Providers exist in the workspace
| | [Documentation]	| Verifies that only the given Source Provider(s) exist in the current workspace.
| | [ARGUMENTS]		| @{ExpectedSourceProviders}
| | ${2DGrid}=	| Query workspace database	| 
| | ... | SELECT Name
| | ... | FROM EDDSDBO.SourceProvider
| | ${1DGrid}=					| Flatten		| ${2DGrid}	| # Converts from 2D grid to 1D grid
| | @{ActualSourceProviders}=	| Create List	| @{1DGrid}	| # Converts from 1D grid to List
| | Lists Should Be Equal	| ${ExpectedSourceProviders}	| ${ActualSourceProviders}

| Verify that only the following Destination Providers exist in the workspace
| | [Documentation]	| Verifies that only the given Destination Provider(s) exist in the current workspace.
| | [ARGUMENTS]		| @{ExpectedDestinationProviders}
| | ${2DGrid}=	| Query workspace database	| 
| | ... | SELECT Name
| | ... | FROM EDDSDBO.DestinationProvider
| | ${1DGrid}=						| Flatten		| ${2DGrid}	| # Converts from 2D grid to 1D grid
| | @{ActualDestinationProviders}=	| Create List	| @{1DGrid}	| # Converts from 1D grid to List
| | Lists Should Be Equal	| ${ExpectedDestinationProviders}	| ${ActualDestinationProviders}

| Verify that no Integration Points exist in the workspace
| | [Documentation]	| Verifies that no Integration Points exist in the current workspace.
| | ${2DGrid}=	| Query workspace database	| 
| | ... | SELECT Name
| | ... | FROM EDDSDBO.IntegrationPoint
| | ${1DGrid}=						| Flatten		| ${2DGrid}	| # Converts from 2D grid to 1D grid
| | @{ActualIntegrationPoints}=	| Create List	| @{1DGrid}	| # Converts from 1D grid to List
| | @{EmptyList}=					| Create List
| | Lists Should Be Equal	| ${EmptyList}	| ${ActualIntegrationPoints}

| Create a new Workspace and Import the Applications into it
| | [Documentation]	| Creates the Workspace used in this suite and installs the Relativity Legal Hold and Integration Points applications on it.
| | Given we are Logged in to Relativity

| | ${Random}=	| Generate Random String
| | Set Suite Variable	| ${WorkspaceName}		| ${WorkspaceName} - (${Random})

| # The last variable in the next line is environment dependent and incalculable
| | Create New Workspace	| ${WorkspaceName}	| Relativity Template	| Relativity Template	| kCura Starter Template	| Active	| Default	| ${DEFAULTFILEREPOSITORY}
| | Go to the Workspace	| ${WorkspaceName}
| | Import Relativity Application	| Relativity Legal Hold	| Library
| | Import Relativity Application	| Integration Points	| Library
| | Go to the Tab	| Integration Points
| | [Teardown]		| Run Keyword If Keyword Failed	| Capture Page Screeenshot

| Delete the Workspace used in this Suite
| | [Documentation]	| Deletes the Workspace used in this Suite if it was successfully created.
| | Delete Workspace	| ${WorkspaceName}
| | Close All Browsers


*** Settings ***
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${KEYWORD_DIR}/Relativity/Platform/RelativityApplications/ADSSetupAndTeardownKeywords.txt	| # Delete Workspace
| Resource	| ${KEYWORD_DIR}/Relativity/Platform/RelativityApplications/RelativityApplicationKeywords.txt	| # Import Relativity Application
| Resource	| ${KEYWORD_DIR}/Relativity/SmokeTest/SmokeTestKeywords.txt	| # Create New Workspace
| Resource	| ${METHOD_KEYWORD_DIR}/NavigationKeywords.txt