
*** Settings ***
| Documentation 	| Verify that the Import Now button is placed and functions correctly.
| Metadata			| Authors	| Daniel Oliver	| William Sehy
| Force Tags		| in-progress	| team.game_of_holds	| feature.integration_points
| Suite Setup		| Open the Browser and Create the Integration Points needed by the Suite
#| Test Setup		| NONE
| Test Teardown 	| Capture screenshot if test fails
| Suite Teardown	| Delete the Integration Points created in this Suite then Close All Browsers


*** Variables ***

| ${WorkspaceName}		| LDAP Sync
| ${IntegrationPoint1}	| Enabled Integration Point
| ${IntegrationPoint2}	| Disabled Integration Point


*** Test Cases ***

| Import Now button UI
| | [Documentation] | Verify that both the IMPORT column and Import Now buttons are present
| | ...				| [id] 16edffcf-ad51-11e4-8b0c-90b11c6c5ae4
| | [Tags]			| manual	| visual
| | Run the following manual steps:
| | ... | Steps:
| | ... | 1. Go to the Tab	| Integration Points
| | ... | 2. View Integration Point	| ${IntegrationPoint1}
| | ... | 
| | ... | Expected Results:
| | ... | 1. Verify that the IMPORT column is on the right of the screen
| | ... | 2. Verify that the Import Now button is in the IMPORT column


| Import Now is active when there is no Schedule
| | [Documentation] | Verify that the Import Now button is active when Enable Scheduler is set to No
| | ...				| [id] 7e0125a1-ad54-11e4-b89d-90b11c6c5ae4
| | [Tags]			| automated
| | View Integration Point	| ${IntegrationPoint2}
| | Verify console buttons are enabled or disabled	| Import Now	| Enabled


| Import Now is active when there is a Schedule
| | [Documentation] | Verify that the Import Now button is active when Enable Scheduler is set to Yes 
| | ...				| [id] 7fadc430-ad54-11e4-be8a-90b11c6c5ae4
| | [Tags]			| automated
| | View Integration Point	| ${IntegrationPoint1}
| | Verify console buttons are enabled or disabled	| Import Now	| Enabled


| Import Now Dialog UI format is correct
| | [Documentation] | Verify that the Import Now dialogs UI is correct.
| | ...				| [id] 8988c740-ad57-11e4-969f-90b11c6c5ae4
| | [Tags]			| automated
| | View Integration Point	| ${IntegrationPoint1}
| | Click	| Import Now
| | Verify that the Import Now dialog box has the correct UI Elements


| Clicking Cancel from the Dialog does not create a new Import job
| | [Documentation] | Verify that clicking Cancel from the Dialog does not create a new Import job.
| | ...				| [id] 8b5dae51-ad57-11e4-bcb9-90b11c6c5ae4
| | [Tags]			| automated
| | View Integration Point	| ${IntegrationPoint1}
| | Click	| Import Now

| | Click	| Cancel
| | Repeat Keyword	| 100 times	| Verify that the following Integration Point does not have a Running Job	| ${IntegrationPoint1}	| # Runs over ~10 seconds


| Clicking OK from the Dialog creates a new Job
| | [Documentation] | Verify that clicking OK from the Dialog creates a new Job.
| | ...				| [id] 873c6d9e-ad54-11e4-9396-90b11c6c5ae4
| | [Tags]			| automated
| | View Integration Point	| ${IntegrationPoint1}
| | Click	| Import Now
| | Click	| OK
| | Wait Until Keyword Succeeds	| 20s	| 2s	| Verify that the following Integration Point has a Running Job	| ${IntegrationPoint1}


| The Importing Now success message is correct
| | [Documentation] | Verify that using the Import Now button shows a success message with the text "Data will now be imported from the source provider.".
| | [Tags]			| automated
| | View Integration Point	| ${IntegrationPoint1}
| | Click	| Import Now
| | Click	| Import Now
| | Click	| OK
| | Wait Until Keyword Succeeds	| 20s	| 2s	| Verify that the following Method Feedback Message is Present	| Data will now be imported from the source provider.


*** Keywords ***

| Verify that the Import Now dialog box has the correct UI Elements
| | [Documentation]	| Verifies that the three main pieces of the Import Now dialog box, the Title, the body text, and the buttons, are present and correct.
| | Run Keyword and Continue on Failure	| Element Text Should Be	| id=ui-dialog-title-msgDiv		| Import Now	| # Title
| | Run Keyword and Continue on Failure	| Element Text Should Be	| css=div[id=msgDiv] > label	| Are you sure you want to import data now?	| # Body
| | Run Keyword and Continue on Failure	| Verify that the current Dialog Box contains the following Buttons	| OK	| Cancel

| Open the Browser and Create the Integration Points needed by the Suite
| | [Documentation]	| Opens the browser and then creates both of the Integration Points used in this suite.
| | ...				| Run as the Suite Setup.
| | Setup connection to the relativity database
| | Open and Log in to Method
| | Setup connection to the Relativity Services API for Legal Hold	| ${WorkspaceName}

#------------------------------------------------------------------------------------------------------------------------

| | ${IntegrationPointExists}=	| Determine if the Following Integration Point Exists in the current Workspace	| ${IntegrationPoint1}
| | Run Keyword If	| ${${IntegrationPointExists}}	| Delete the following Integration Point using API 	| ${IntegrationPoint1}

| | ${IntegrationPointExists}=	| Determine if the Following Integration Point Exists in the current Workspace	| ${IntegrationPoint2}
| | Run Keyword If	| ${${IntegrationPointExists}}	| Delete the following Integration Point using API 	| ${IntegrationPoint2}

#------------------------------------------------------------------------------------------------------------------------

| | @{Step1Fields}=	| Create List	| ${IntegrationPoint1}	| Junk Data		| LDAP	| Custodian	| 
| | ...	| Yes		| Daily	| NULL	| NULL	| 02/25/2035	| 02/26/2045	| 11:25 AM

| | @{Step2Fields}=	| Create List	| gameofholds.testing.corp/CN=Users,DC=gameofholds,DC=testing,DC=corp	| 
| | ... | (department=Import Now Tester)	| FastBind	| testing\\rellockdown	| P@ssw0rd@1

| | @{Step3Mapping}=	| Create List	|
| | ... | First Name	| givenname		| 
| | ... | Last Name		| sn			| 
| | ... | Email			| mail			| 
| | ... | Department	| department	| 
| | ... | UniqueID		| objectguid

| | Create LDAP Integration Point	| ${Step1Fields}	| ${Step2Fields}	| ${Step3Mapping} | Append Only	| UniqueID

#------------------------------------------------------------------------------------------------------------------------

| | @{Step1Fields}=	| Create List	| ${IntegrationPoint2}	| Junk Data	| LDAP	| Custodian	| No

| | Create LDAP Integration Point	| ${Step1Fields}	| ${Step2Fields}	| ${Step3Mapping} | Append Only	| UniqueID
| | [Teardown]		| Drop connection to the relativity services api

| Delete the Integration Points created in this Suite then Close All Browsers
| | [Documentation]	| Delete the Integration Points that were created in the Suite and then closes any open browsers.
| | ...				| Run as the Suite Teardown.
| | Setup connection to the Relativity Services API for Legal Hold	| ${WorkspaceName}

| | Delete the following Integration Point using API	| ${IntegrationPoint1}

| | Delete the following Integration Point using API	| ${IntegrationPoint2}

| | Delete the Custodians in the Following Department using API	| Import Now Tester

| | Close All Browsers
| | Drop connection to the relativity services api


*** Settings ***
| Library	| ${KEYWORD_DIR}/ManualKeywords.py
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${KEYWORD_DIR}/LegalHold/Legal_Hold_Keywords_API.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/MiscLegalHoldKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/DatabaseKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/IntegrationPointKeywords.txt