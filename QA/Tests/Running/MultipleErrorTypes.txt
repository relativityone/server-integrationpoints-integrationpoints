
*** Settings ***
| Documentation 	| Verify that getting the suppressed "Append encountering existing Objects" errors do not interfere with getting a different type of error.
| Metadata			| Authors	| William Sehy
| Force Tags		| in-progress	| team.game_of_holds	| feature.integration_points
| Suite Setup		| Create Suite Setup Data
#| Test Setup		| NONE
| Test Teardown 	| Run Keyword if Test Failed	| Capture Page Screenshot
| Suite Teardown	| Delete the Objects created in the Suite then Close All Browsers


*** Variables ***

| ${WorkspaceName}				| LDAP Sync
| ${IntegrationName}			| Multiple Error Testing
| ${NewObject}					| User, New
| ${ExistingAndUpdatedObject}	| User, Not Updated
| ${ExistingNoChangeObject}		| User, Pre-Existing
| ${DuplicateUsers}				| Email, Duplicate
| ${Custodians Grid}	| gbox_custodianGrid


*** Test Cases ***

| Other Errors are still counted and created when using Append and encountering existing User(s)
| | [Documentation] | Verify that the Error counts and error logs are reported correctly when getting both 
| | ...				| the suppressed Append encountering existing Objects errors and a different type of error.
| | [Tags]			| manual	| partially_automated
| | Go to the Tab	| Job History

| | Run the following manual steps:
| | ... | Steps:
| | ... | 1. View the latest Job History entry for "${IntegrationName}"
| | ... |
| | ... | Expected Results:
| | ... | 1. Verify that the statistics are as following:
| | ... | - Records Imported: 4
| | ... | - Records with Errors: 1
| | ... | 2. Verify that only the following error is present:
| | ... | - "Document '(dEmail@lh.kcura.com)' has been previously processed in this file on line 4."


| Custodians are still Imported when using Append and encountering existing User(s) and another error Type
| | [Documentation] | Verify that new Custodians are Appended when getting both  the suppressed Append encountering existing Objects
| | ...				| errors and a different type of error.
| | [Tags]			| automated
| | Go to the Method Tab	| Custodian
| | Filter the following Grid on the following Column by the following Value	| ${Custodians Grid}	| Department	| Multi Error Test

| | @{Header}=	| Create List	| Name
| | @{Values}=	| Create List	| ${NewObject}	| ${DuplicateUsers}
| | Verify that the following grid has the following headers and rows	| ${Custodians Grid}	| ${Header}	| ${Values}


| Custodians are still not Updated when using Append and encountering existing User(s) and another error Type
| | [Documentation] | Verify that existing Custodians are not updated when getting both the suppressed Append encountering existing Objects
| | ...				| errors and a different type of error.
| | [Tags]			| automated
| | Go to the Method Tab	| Custodian
| | Filter the following Grid on the following Column by the following Value	| ${Custodians Grid}	| Department	| Multi Error Test

| | @{Header}=	| Create List	| Name
| | @{Values}=	| Create List	| ${ExistingAndUpdatedObject}	| ${ExistingNoChangeObject}
| | Verify that the following grid has the following headers and rows	| ${Custodians Grid}	| ${Header}	| ${Values}


*** Keywords *** 

| Create Suite Setup Data
| | [Documentation]	| Create the Suite Preconditions
| | Setup connection to the relativity database
| | Open and Log in to Method
| | Setup connection to the Relativity Services API for Legal Hold	| ${WorkspaceName}

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${ExistingNoChangeObject}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${ExistingNoChangeObject}
| | Create a Custodian using API	| 
| | ... | Full Name		| ${ExistingNoChangeObject}	|
| | ... | Email			| eUser@lh.kcura.com		|
| | ... | Department	| Multi Error Test

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${ExistingAndUpdatedObject}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${ExistingAndUpdatedObject}
| | Create a Custodian using API	| 
| | ... | Full Name		| ${ExistingAndUpdatedObject}	|
| | ... | Email			| uUser@lh.kcura.com			|
| | ... | Department	| Multi Error Test

| | @{Step1Fields}=		| Create list | 
| | ... | ${IntegrationName} | Append | LDAP | Custodian | No
| | @{Step2Fields}=		| Create list | gameofholds.testing.corp/CN=Users,DC=gameofholds,DC=testing,DC=corp | 
| | ... | (department=Multi Error Test)	| FastBind | testing\\rellockdown | P@ssw0rd@1
| | @{Step3Mapping}=	| Create List	|
| | ... | Email			| mail			| 
| | ... | Department	| department	| 
| | ... | First Name	| givenname		| 
| | ... | Last Name		| sn			| 
| | ... | UniqueID		| objectguid
| | Create LDAP Integration Point | ${Step1Fields} | ${Step2Fields} | ${Step3Mapping} | Email

| | Run Import Now on the Following Integration Point	| ${IntegrationName}
| | [Teardown]		| Drop connection to the relativity services api

| Delete the Objects created in the Suite then Close All Browsers
| | [Documentation]	| Delete the Suite test data of Integration Point and Custodians.
| | Setup connection to the Relativity Services API for Legal Hold	| ${WorkspaceName}

| | Delete the following Integration Point using API | ${IntegrationName}
| | Delete the Custodians in the Following Department using API	| Multi Error Test

| | Drop connection to the relativity services api
| | Close All Browsers


*** Settings ***
| Library	| Collections
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${KEYWORD_DIR}/LegalHold/Legal_Hold_Keywords_API.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/GridKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/MiscMethodKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/DatabaseKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/IntegrationPointKeywords.txt