*** Settings ***
| Documentation 	| Verify that running an Append/Overlay RIP creates new objects, updates existing modified objects and ignores existing unmodified objects. These are generic tests that can be run against any RDO, as such, the navigation and search steps are not as details as usual.
| Metadata		    | Authors	| Daniel Oliver
| Force Tags		| in-progress | team.method
| Suite Setup		| Create Suite Setup Data
| Test Teardown 	| Run Keyword if Test Failed	| Capture Page Screenshot
| Suite Teardown	| Delete the Objects created in the Suite then Close All Browsers

*** Variables ***

| ${WorkspaceName}	          | LDAP Sync
| ${IntegrationName}          | Append and Overlay
| ${NewObject}                | Sun, Early
| ${ExistingAndUpdatedObject} | Sun, Midday    
| ${ExistingNoChangeObject}   | Sun, Rise 
| ${Custodians Grid}		  | gbox_custodianGrid

*** Test Cases ***

| An Append and Overlay import Creates new records
| | [Documentation] | Verify that when an Append and Overlay RIP is run which has new Source records, that it creates new records in the Target RDO.
| | ...				| [id] 130ad15e-a89f-11e4-9aa5-b8ca3a83048f
| | [Tags] | automated
| | Filter the following Grid on the following Column by the following Value | ${Custodians Grid} | Email | esun@gameofholdstesting.corp

| | Verify that the following Grid contains the following number of Rows | ${Custodians Grid} | 1

| An Append and Overlay import updates existing modified records
| | [Documentation] | Verify that when an Append and Overlay RIP is run with an existing record in the Target RDO with updated details in the Source, that it does update the existing records in the Target RDO.
| | ...				| [id] 130fda6e-a89f-11e4-8313-b8ca3a83048f
| | [Tags] | manual
| | Run the following manual steps:
| | ... | Preconditions:
| | ... | 1. Require Test Case: An Append and Overlay import Creates new records
| | ... |
| | ... | Steps:
| | ... | 1. View the History records for the ${RIPAppendOverlay1} import
| | ... |
| | ... | Expected Results:
| | ... | 1. Verify that there is an Update entry for the object ${ExistingAndUpdatedObject}

| An Append and Overlay import does not update existing unmodified records
| | [Documentation] | Verify that when an Append and Overlay RIP is run with an existing record in the Target RDO with identical details in the Source, that it does not update the existing record in the Target RDO.
| | ...				| [id] 130fda6f-a89f-11e4-b15c-b8ca3a83048f
| | [Tags] | manual
| | Run the following manual steps:
| | ... | Preconditions:
| | ... | 1. Require Test Case: An Append and Overlay import Creates new records
| | ... |
| | ... | Steps:
| | ... | 1. View the History records for the ${RIPAppendOverlay1} import
| | ... |
| | ... | Expected Results:
| | ... | 1. Verify that there is no Update entry for the object ${ExistingNoChangeObject}

*** Keywords *** 

| Create Suite Setup Data
| | [Documentation]	| Create the Suite Preconditions
| | Setup connection to the relativity database
| | Open and Log in to Method
| | Setup connection to the Relativity Services API for Method	| ${WorkspaceName}

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${ExistingNoChangeObject}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${ExistingNoChangeObject}
| | Create a Custodian using API	| 
| | ... | Full Name		| ${ExistingNoChangeObject}	|
| | ... | Email			| rsun@gameofholdstesting.corp |
| | ... | Department	| AppendAndOverlay

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${ExistingAndUpdatedObject}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${ExistingAndUpdatedObject}
| | Create a Custodian using API	| 
| | ... | Full Name		| ${ExistingAndUpdatedObject}	|
| | ... | Email			| msun@gameofholdstesting.corp |
| | ... | Department	| AppendAndOverla

| | @{Step1Fields}  | Create list | ${IntegrationName} | Append/Overlay |
| | ... | LDAP | Custodian | no
| | @{Step2Fields}  | Create list | gameofholds.testing.corp/CN=Users,DC=gameofholds,DC=testing,DC=corp | 
| | ... | (department=AppendAndOverlay) | FastBind 
| | @{Step3Mapping}=	| Create List	|
| | ... | Email			| mail			| 
| | ... | Department	| department	| 
| | ... | First Name	| givenname		| 
| | ... | Last Name		| sn			| 
| | ... | UniqueID		| objectguid
| | Create Integration Point | ${Step1Fields} | ${Step2Fields} | ${Step3Mapping} | Email

| | Run Import Now on the Following Integration Point | ${IntegrationName}  

| | Go to the Method tab | Custodian

| Delete the Objects created in the Suite then Close All Browsers
| | [Documentation]	| Delete the Suite test data of Integration Point and Custodians.
| | Setup connection to the Relativity Services API for Method	| ${WorkspaceName}

| | Delete the Following Integration Point | ${IntegrationName}
| | Delete the Custodians in the Following Department using API	| AppendAndOverlay

| | Drop connection to the relativity services api
| | Close All Browsers

*** Settings ***
| Library	| Collections
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${KEYWORD_DIR}/Method/Method_Keywords_API.txt
| Resource	| ${METHOD_KEYWORD_DIR}/GridKeywords.txt
| Resource	| ${METHOD_KEYWORD_DIR}/MiscMethodKeywords.txt
| Resource	| ${METHOD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| C:/SourceCode/LDAPSync/QA/Keywords/IntegrationPointKeywords.txt
