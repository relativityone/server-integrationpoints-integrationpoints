*** Settings ***
| Documentation 	| Verify that when Managers are imported and Custodian Manager Contains Link is set to Yes, that the Custodian records are created and linked to the Manager Custodians.
| Metadata		    | Authors	| Daniel Oliver
| Force Tags		| in-progress | team.method
| Suite Setup		| Create Suite Setup Data
| Test Teardown 	| Run Keyword if Test Failed	| Capture Page Screenshot
| Suite Teardown	| Delete the Objects created in the Suite then Close All Browsers

*** Variables ***

| ${WorkspaceName}	   | LDAP Sync
| ${Custodian1}        | Worker, Alpha
| ${Custodian2}        | Worker, Bravo
| ${CustodianB2}       | Worker, BravoTwo
| ${Custodian3}        | Worker, Charlie
| ${Custodian4}        | Worker, Delta
| ${Custodian5}        | Worker, AlphaTwo
| ${Custodian6}        | Worker, Echo
| ${Manager1}          | Manager, Alpha
| ${Manager2}          | Manager, Bravo
| ${Manager3}          | Manager, Charlie
| ${Manager4}          | Manager, Delta
| ${Manager6}          | Manager, Echo
| ${Manager7}          | Manager, Foxtrot
| ${IntegrationName}   | Managers
| ${Department}        | Managers
| ${CustodianGrid}     | gbox_custodianGrid

*** Test Cases ***

| Manager names are shown on their Report's Custodian Detail View
| | [Documentation] | Verify that an imported Custodian who has a manager is linked to the Manager on the Custodian Detail View
| | ... | [id] b8e6c261-b605-11e4-aaf4-90b11c6c5ae4
| | [Tags] | automated
| | Go to the Method Tab | Custodian
| | View Custodian | ${Custodian1} 

| | Verify the Custodian has the Following Manager | ${Manager1}

| Managers that did not previously exist are Created
| | [Documentation] | Verify that new Managers have their own Custodian record created
| | ... | [id] bb77584f-b605-11e4-8ae0-90b11c6c5ae4
| | [Tags] | automated
| | Go to the Method Tab | Custodian
| | Filter the following Grid on the following Column by the following Value | ${CustodianGrid}  | Email | alphamanager@gameofholds.testing.corp

| | Verify that the following Grid contains the following number of Rows | ${CustodianGrid}  | 1

| Imported Managers have the correct name format on their own Custodian record
| | [Documentation] | Verify that newly imported Managers have the correct name format on their own Custodian record
| | ... | [id] bf3068b0-b605-11e4-8b69-90b11c6c5ae4
| | [Tags] | automated 
| | Go to the Method Tab | Custodian
| | View Custodian | ${Manager1} 

| | Verify the current Custodian's Details have the following values | Alpha	| Manager	| alphamanager@gameofholds.testing.corp	| ${Department}	| ${EMPTY}

| Existing Managers names are shown on their Report's Custodian Detail View
| | [Documentation] | Verify that a Manager who already existed is linked to their Reports on the Custodian Detail View
| | ... | [id] c28279de-b605-11e4-b74f-90b11c6c5ae4
| | [Tags] | automated
| | Go to the Method Tab | Custodian
| | View Custodian | ${Custodian2} 

| | Verify the Custodian has the Following Manager | ${Manager2}

| Existing Custodians can become Managers to New Custodians
| | [Documentation] | Verify that Existing Custodians can become Managers to New Custodians
| | ... | [id] c7a41aee-b605-11e4-91e0-90b11c6c5ae4
| | [Tags] | automated
| | Go to the Method Tab | Custodian
| | View Custodian | ${Custodian3} 

| | Verify the Custodian has the Following Manager | ${Manager3}

| Existing Custodians can become Managers of Existing Custodians
| | [Documentation] | Verify that Existing Custodians can become Managers of Existing Custodians
| | ... | [id] cab886de-b605-11e4-bdd1-90b11c6c5ae4
| | [Tags] | automated
| | Go to the Method Tab | Custodian
| | View Custodian | ${Custodian4} 

| | Verify the Custodian has the Following Manager | ${Manager4}

| Existing Custodians can be linked to Newly created Managers
| | [Documentation] | Verify that Existing Custodians can be linked to Newly created Managers
| | ... | [id] cf715400-b605-11e4-a0ec-90b11c6c5ae4
| | [Tags] | automated
| | Go to the Method Tab | Custodian
| | View Custodian | ${Custodian5} 

| | Verify the Custodian has the Following Manager | ${Manager1}

| Existing Custodians can be given new existing Managers
| | [Documentation] | Verify that Existing Custodians can be given new existing Managers
| | ... | [id]  d20656c0-b605-11e4-8c82-90b11c6c5ae4
| | [Tags] | automated
| | Go to the Method Tab | Custodian
| | View Custodian | ${Custodian6} 

| | Verify the Custodian has the Following Manager | ${Manager7}

*** Keywords *** 

| Create Suite Setup Data
| | [Documentation]	| Create the Suite Preconditions
| | Setup connection to the relativity database
| | Open and Log in to Method
| | Setup connection to the Relativity Services API for Method	| ${WorkspaceName}

#-----------------------------------------------------------

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${Manager2}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${Manager2}
| | Create a Custodian using API	| 
| | ... | Full Name		| ${Manager2}	|
| | ... | Email			| bm@gameofholds.testing.corp |
| | ... | Department	| ${Department}

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${Manager7}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${Manager7}
| | Create a Custodian using API	| 
| | ... | Full Name		| ${Manager7}	|
| | ... | Email			| FoxtrotManager@gameofholds.testing.corp |
| | ... | Department	| ${Department}

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${Custodian5}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${Custodian5}
| | Create a Custodian using API	| 
| | ... | Full Name		| ${Custodian5}	|
| | ... | Email			| AlphaTwoWorker@gameofholds.testing.corp |
| | ... | Department	| ${Department}

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${CustodianB2}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${CustodianB2}
| | Create a Custodian using API	| 
| | ... | Full Name		| ${CustodianB2}	|
| | ... | Email			| BravoTwoWorker@gameofholds.testing.corp |
| | ... | Manager       | ${Manager2}	|
| | ... | Department	| ${Department}

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${Manager3}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${Manager3}
| | Create a Custodian using API	| 
| | ... | Full Name		| ${Manager3}	|
| | ... | Email			| CharlieManager@gameofholds.testing.corp |
| | ... | Department	| ${Department}

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${Custodian4}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${Custodian4}
| | Create a Custodian using API	| 
| | ... | Full Name		| ${Custodian4}	|
| | ... | Email			| DeltaWorker@gameofholds.testing.corp |
| | ... | Department	| ${Department}

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${Manager4}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${Manager4}
| | Create a Custodian using API	| 
| | ... | Full Name		| ${Manager4}	|
| | ... | Email			| DeltaManager@gameofholds.testing.corp |
| | ... | Department	| ${Department}

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${Custodian6}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${Custodian6}
| | Create a Custodian using API	| 
| | ... | Full Name		| ${Custodian6}	|
| | ... | Email			| EchoWorker@gameofholds.testing.corp |
| | ... | Manager       | ${Manager7}	|
| | ... | Department	| ${Department}

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${Manager6}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${Manager6}
| | Create a Custodian using API	| 
| | ... | Full Name		| ${Manager6}	|
| | ... | Email			| EchoManager@gameofholds.testing.corp |
| | ... | Department	| ${Department}

#-----------------------------------------------------------

| | @{Step1Fields}  | Create list | ${IntegrationName} | Append/Overlay |
| | ... | LDAP | Custodian | no

| | @{Step2Fields}  | Create list | gameofholds.testing.corp/CN=Users,DC=gameofholds,DC=testing,DC=corp | 
| | ... | (department=${Department}) | FastBind | testing\\rellockdown | P@ssw0rd@1

| | @{Step3Mapping}=	| Create List	|
| | ... | Email			| mail			| 
| | ... | Department	| department	| 
| | ... | First Name	| givenname		| 
| | ... | Last Name		| sn			| 
| | ... | Manager       | manager       | 
| | ... | UniqueID		| objectguid

| | Create Integration Point | ${Step1Fields} | ${Step2Fields} | ${Step3Mapping} | Email

| | Run Import Now on the Following Integration Point | ${IntegrationName}  

| | Go to the Method tab | Custodian

| | [Teardown] | Drop connection to the relativity services api

| Delete the Objects created in the Suite then Close All Browsers
| | [Documentation]	| Delete the Suite test data of Integration Point and Custodians.
| | Setup connection to the Relativity Services API for Method	| ${WorkspaceName}

| | Delete the following Integration Point using API | ${IntegrationName}
| | Delete the Custodians in the Following Department using API	| ${Department}

| | Drop connection to the relativity services api
| | Close All Browsers

*** Settings ***
| Library	| Collections
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${KEYWORD_DIR}/Method/Method_Keywords_API.txt
| Resource	| ${METHOD_KEYWORD_DIR}/GridKeywords.txt
| Resource	| ${METHOD_KEYWORD_DIR}/MiscMethodKeywords.txt
| Resource	| ${METHOD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/IntegrationPointKeywords.txt

