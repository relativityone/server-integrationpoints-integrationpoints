*** Settings ***
| Documentation | Verify that Custodians that have both a blank first name and blank last name will not be imported
| Metadata		| Authors	  | Daniel Oliver
| Force Tags    | in-progress | team.method
| Suite Setup		| Create Suite Setup Data
| Test Setup		| Open and Log in to Method
| Test Teardown 	| Capture screenshot on failure and run keyword	| Close All Browsers
| Suite Teardown	| Delete Suite Setup Data

*** Variables ***

| ${WorkspaceName}	                    | LDAP Sync
| ${ConnectionPath}                     | gameofholds.testing.corp/CN=Users,DC=gameofholds,DC=testing,DC=corp 
| ${AuthenticationType}                 | FastBind
| ${Object Filter String}               | (department=BlankNames)
| ${IntegrationPoint1}                  | Blank Custodian Names 
| ${Add Workspace Field Icon}			| add-workspace-field
| ${Add Source Attribute Field Icon}    | add-source-field
| ${OKButton}                           | xpath=//span[@class='ui-button-text' and text()='OK'] 
| ${ImportNowButton}                    | xpath=//a[@class='consoleButtonEnabled' and text()='Import Now'] 
| ${IntegrationPointLink}               | xpath=//a[@class='itemListPrimaryLink' and text()='${IntegrationPoint1}']

*** Test Cases ***

| Suite Precondition Test
| | [Documentation] | Verify that the Import process functions. This is the suite-precondition that can't be automated nicely.
| | ... | [id] 901a5bc0-ad46-11e4-9a02-90b11c6c5ae4
| | [Tags] | manual  |  required
| | Go to the Tab	| Integration Points
| | Set Filter      | Name | ${IntegrationPoint1}
| | Click Element   | ${IntegrationPointLink}

| | Run the following manual steps:
| | ... | Steps:
| | ... | 1. Click Element   | ${ImportNowButton} 
| | ... | 2. Click Element   | ${OKButton}  
| | ... | 3. Wait for the Import job to be processed; view the eddsbo.ScheduleAgentQueue for the TaskType of SyncWorker to complete
| | ... | 4. Go to the Method Tab  | Custodian
| | ... | 5. Filter by the Department of Blank Custodian Names
| | ... | 
| | ... | Expected Results:
| | ... | 1. Verify that there are 2 custodians. Now that you know the import has been performed, the remaining automated tests will verify the data is correct.

| Custodians with a blank First Name are imported
| | [Documentation] | Verify that Custodians that do not have their first name populated in LDAP are imported into RLH.
| | ... | [id] b66ea8d1-b0a7-11e4-b386-90b11c6c5ae4
| | Require Test Case | Suite Precondition Test
| | Go to the Method Tab | Custodian
| | Filter a Text Filter | gbox_custodianGrid | Email | BlankFirstName@law.com

| | @{Headers}=	| Create List	| Name
| | @{Values}=	| Create List	| BlankFirstName

| | Verify that the following Grid contains the following number of Rows  | gbox_custodianGrid | 1
| | Verify that the following grid has the following headers and rows	  | gbox_custodianGrid | ${Headers}	| ${Values}

| Custodians without a Last Name not imported
| | [Documentation] | Verify that Custodians that do not have their last name populated are imported into RLH.
| | ... | [id] 93c625b0-ad46-11e4-993c-90b11c6c5ae4
| | Require Test Case | Suite Precondition Test
| | Go to the Method Tab | Custodian
| | Filter a Text Filter | gbox_custodianGrid | Name | BlankLastName

| | @{Headers}=	| Create List	| Name
| | @{Values}=	| Create List	| BlankLastName

| | Verify that the following Grid contains the following number of Rows  | gbox_custodianGrid | 1
| | Verify that the following grid has the following headers and rows	  | gbox_custodianGrid | ${Headers}	| ${Values}

| Custodians without both a First Name and Last Name are not imported
| | [Documentation] | Verify that Custodians that do not have both a First Name and Last Name are not imported into RLH.
| | ... | [id] 98e1d34f-ad46-11e4-8e0c-90b11c6c5ae4
| | Require Test Case | Suite Precondition Test
| | Go to the Method Tab | Custodian
| | Filter a Text Filter | gbox_custodianGrid | Email | BlankFirstAndLastName@law.com

| | Verify that the following Grid contains the following number of Rows  | gbox_custodianGrid | 0

*** Keywords *** 

| Create Suite Setup Data
| | [Documentation]	| Create the Suite Preconditions
| | Setup connection to the relativity database
| | Open and Log in to Method
| | Setup connection to the Relativity Services API for Method	| ${WorkspaceName}
| | Create Integration Point
| | Close All Browsers

| Create Integration Point
| | [Documentation] | A keyword that creates an Integration Point for this suite. Not suiteable as a generic keyword for other suites.
| | Open Create Integration Point
| | Fill out the Following Fields of the Integration Point Step 1 form | ${IntegrationPoint1} | Append/Overlay | LDAP | Custodian | no
| | Proceed to the Next Step of the Integration Point wizard
| | Fill out the Following Fields of the Integration Point Step 2 form | ConnectionPath=${ConnectionPath} | ObjectFilterString=${Object Filter String} | Authentication=${AuthenticationType} 
| | Proceed to the Next Step of the Integration Point wizard
| | Click Workspace Field  | Department
| | Click Workspace Field  | Email
| | Click Workspace Field  | UniqueID
| | Move fields to the Selected Workspace list
| | Click Workspace Field  | First Name
| | Click Workspace Field  | Last Name
| | Move fields to the Selected Workspace list  | #Have to do batches of multiple field selections as RIP arranges the results on the Selected list in alphabetical order
| | Click Source Attribute Field  | department
| | Click Source Attribute Field  | mail
| | Click Source Attribute Field  | objectguid
| | Move fields to the Selected Source Attribute list
| | Click Source Attribute Field  | givenname
| | Click Source Attribute Field  | sn
| | Move fields to the Selected Source Attribute list
| | Click | Save

| Delete Suite Setup Data
| | [Documentation]	| Delete the Suite test data of Integration Point and Custodians.
| | Open and Log in to Method
| | Setup connection to the Relativity Services API for Method	| ${WorkspaceName}
| | Delete Integration Point | ${IntegrationPoint1}
| | Delete the Custodians in the Following Department using API	| BlankNames

| | Drop connection to the relativity services api
| | Close All Browsers

| Delete Integration Point
| | [Documentation] | A keyword that deletes the specified Integration Point; can be called from anywhere.
| | [Arguments]     | ${IntegrationPointToDelete}
| | Go to the Tab	| Integration Points
| | Set Filter      | Name | ${IntegrationPointToDelete}
| | Click Element   | xpath=//a[@class='itemListPrimaryLink' and text()='${IntegrationPointToDelete}']
| | Delete this object

*** Settings ***
| Library	| Collections
| Library	| ${KEYWORD_DIR}/ManualKeywords.py
| Library	| ${KEYWORD_DIR}/kWebDriver.py
#| Library	| ${KEYWORD_DIR}/RelativityKeywords.py
| Resource	| ${METHOD_KEYWORD_DIR}/MiscMethodKeywords.txt
| Resource	| ${METHOD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| ${KEYWORD_DIR}/Method/Method_Keywords_API.txt
| Resource	| C:/SourceCode/LDAPSync/QA/Keywords/IntegrationPointKeywords.txt