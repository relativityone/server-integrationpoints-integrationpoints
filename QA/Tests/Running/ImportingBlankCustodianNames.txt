
*** Settings ***
| Documentation		| Verify that Custodians that have both a blank first name and blank last name will not be imported, 
| ...				| but Custodians with only one blank value are imported.
| Metadata			| Authors	| Daniel Oliver
| Force Tags		| in-progress	| team.method
| Suite Setup		| Create Suite Setup Data
| Test Teardown 	| Run Keyword if Test Failed	| Capture Page Screenshot
| Suite Teardown	| Delete the Objects created in the Suite then Close All Browsers

*** Variables ***

| ${WorkspaceName}			| LDAP Sync
| ${IntegrationName}		| Blank Custodian Names 
| ${Custodians Grid}		| gbox_custodianGrid

*** Test Cases ***

| Custodians with a blank First Name are imported
| | [Documentation] | Verify that Custodians that do not have their first name populated in LDAP are imported into RLH.
| | ...				| [id] b66ea8d1-b0a7-11e4-b386-90b11c6c5ae4
| | @{Headers}=	| Create List	| Name
| | @{Values}=	| Create List	| BlankFirstName
| | Verify that the following grid has the following headers and rows	| ${Custodians Grid}	| ${Headers}	| ${Values}


| Custodians without a Last Name imported
| | [Documentation] | Verify that Custodians that do not have their last name populated are imported into RLH.
| | ...				| [id] 93c625b0-ad46-11e4-993c-90b11c6c5ae4
| | @{Headers}=	| Create List	| Name
| | @{Values}=	| Create List	| BlankLastName
| | Verify that the following grid has the following headers and rows	| ${Custodians Grid}	| ${Headers}	| ${Values}


| Custodians without both a First Name and Last Name are not imported
| | [Documentation] | Verify that Custodians that do not have both a First Name and Last Name are not imported into RLH.
| | ...				| [id] 98e1d34f-ad46-11e4-8e0c-90b11c6c5ae4
| | @{Headers}=	| Create List	| Name
| | @{Values}=	| Create List	| ${EMPTY}
| | Verify that the following grid with the following headers does not have the following rows | ${Custodians Grid}	| ${Headers}	| ${Values}


*** Keywords *** 

| Create Suite Setup Data
| | [Documentation]	| Create the Suite Preconditions
| | Setup connection to the relativity database
| | Open and Log in to Method

| | Update a User's Show Filter Value	| ${USERNAME}	| Visible

| | @{Step1Fields}=		| Create List | ${IntegrationName} | Append/Overlay |
| | ... | LDAP | Custodian | No

| | @{Step2Fields}=		| Create List | 
| | ... | gameofholds.testing.corp/CN=Users,DC=gameofholds,DC=testing,DC=corp	| 
| | ... | (department=BlankNames)	| 
| | ... | FastBind | 
| | ... | testing\\rellockdown | P@ssw0rd@1

| | @{Step3Mapping}=	| Create List	|
| | ... | Email			| mail			| 
| | ... | Department	| department	| 
| | ... | First Name	| givenname		| 
| | ... | Last Name		| sn			| 
| | ... | UniqueID		| objectguid

| | Create Integration Point | ${Step1Fields} | ${Step2Fields} | ${Step3Mapping} | UniqueID

| | Run Import Now on the Following Integration Point | ${IntegrationName}

| | Go to the Method tab | Custodian
| | Filter the following Grid on the following Column by the following Value | ${Custodians Grid} | Department | BlankNames
| | [Teardown]		| Run Keyword If Keyword Failed	| Capture Page Screenshot

| Delete the Objects created in the Suite then Close All Browsers
| | [Documentation]	| Delete the Suite test data of Integration Point and Custodians.
| | Setup connection to the Relativity Services API for Method	| ${WorkspaceName}

| | Delete the Following Integration Point | ${IntegrationName}
| | Delete the Custodians in the Following Department using API	| BlankNames

| | Drop connection to the relativity services api
| | Close All Browsers


*** Settings ***
| Library	| Collections
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${KEYWORD_DIR}/Method/Method_Keywords_API.txt
| Resource	| ${METHOD_KEYWORD_DIR}/GridKeywords.txt
| Resource	| ${METHOD_KEYWORD_DIR}/MiscMethodKeywords.txt
| Resource	| ${METHOD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/IntegrationPointKeywords.txt