
*** Settings ***
| Documentation 	| Verify that Custodians and Managers from a nested folder/outside of the proivded filter, are imported when Import Nested Items is set to Yes.
| Metadata			| Authors	| William Sehy	| Daniel Oliver
| Force Tags		| in-progress	| team.game_of_holds	| feature.integration_points
| Suite Setup		| Create Suite Setup Data
| Test Teardown 	| Run Keyword if Test Failed	| Capture Page Screenshot
| Suite Teardown	| Delete the Objects created in the Suite then Close All Browsers


*** Variables ***

| ${WorkspaceName}			| LDAP Sync
| ${IntegrationName}		| Nested Folders
| ${RootCustodian}			| Nest, Rat		| # Manager is ${NestedManager}
| ${RootCustodian2}			| Nest, Cat		| # Manager is ${AnotherFolderManager}
| ${NestedCustodian}		| Nest, Bird	| # Manager is ${RootManager}
| ${NestedManager}			| Manager, Middle
| ${AnotherFolderManager}	| Manager, AnotherFolder
| ${RootManager}			| Manager, Top
| ${Custodians Grid}	| gbox_custodianGrid


*** Test Cases ***

| Root Custodians are imported
| | [Documentation] | Verify that Custodians in the rorr folder are imported.
| | ...				| [id] 653348c0-b2fc-11e4-bc4d-90b11c6c5ae4
| | [Tags]			| automated
| | Go to the Method Tab	| Custodian
| | Filter the following Grid on the following Column by the following Value	| ${Custodians Grid}	| Name	| ${RootCustodian} 
| | Verify that the following Grid contains the following number of Rows	| ${Custodians Grid}	| 1


| Nested Custodians are imported
| | [Documentation] | Verify that Custodians in a nested folder are imported.
| | ...				| [id] 78e9da30-b2f9-11e4-b2bb-90b11c6c5ae4
| | [Tags]			| automated
| | Go to the Method Tab	| Custodian
| | Filter the following Grid on the following Column by the following Value | ${Custodians Grid} | Name	| ${NestedCustodian} 

| | Verify that the following Grid contains the following number of Rows | ${Custodians Grid} | 1


| Nested Managers are Imported
| | [Documentation] | Verify that Managers in a nested folder are imported.
| | ...				| [id] d9bdbdde-b605-11e4-b062-90b11c6c5ae4
| | [Tags]			| automated
| | Go to the Method Tab	| Custodian
| | Filter the following Grid on the following Column by the following Value | ${Custodians Grid} | Name	| ${NestedManager} 

| | Verify that the following Grid contains the following number of Rows | ${Custodians Grid} | 1


| Managers are Imported when they do not meet the Object Filter String
| | [Documentation] | Verify that Managers are Imported when they would normally be excluded due to a non-matching Object Filter String.
| | ...				| [id] df7a8d80-b605-11e4-9c30-90b11c6c5ae4
| | [Tags]			| dap827	| automated
| | Go to the Method Tab	| Custodian
| | Filter the following Grid on the following Column by the following Value | ${Custodians Grid} | Name	| ${RootManager} 

| | Verify that the following Grid contains the following number of Rows | ${Custodians Grid} | 1


| Managers are Imported when they do not reside in the current Folder Branch
| | [Documentation] | Verify that Managers are Imported when they reside outside of the connection path folder or sub-folders.
| | ...				| [id] dc2b898f-b605-11e4-bfb4-90b11c6c5ae4
| | [Tags]			| dap827	| automated
| | Go to the Method Tab	| Custodian
| | Filter the following Grid on the following Column by the following Value | ${Custodians Grid} | Name	| ${AnotherFolderManager}

| | Verify that the following Grid contains the following number of Rows | ${Custodians Grid} | 1


*** Keywords *** 

| Create Suite Setup Data
| | [Documentation]	| Create the Suite Preconditions
| | Setup connection to the relativity database
| | Open and Log in to Method
| | Setup connection to the Relativity Services API for Legal Hold	| ${WorkspaceName}

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${NestedCustodian}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${NestedCustodian}

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${RootCustodian}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${RootCustodian}

| | ${IntegrationPointExists}=	| Determine if the Following Integration Point Exists in the current Workspace	| ${IntegrationName}
| | Run Keyword If	| ${${IntegrationPointExists}}	| Delete the following Integration Point using API 	| ${IntegrationName}

| | @{Step1Fields}=		| Create List | ${IntegrationName} | Junk Data	|
| | ... | LDAP | Custodian | No
| | @{Step2Fields}=		| Create List | testing.corp/OU=Testing - Users,DC=testing,DC=corp |
| | ... | (department=Nested) | FastBind | testing\\rellockdown | P@ssw0rd@1 | Yes
| | @{Step3Mapping}=	| Create List	| 
| | ... | Email			| mail			| 
| | ... | First Name	| givenname		| 
| | ... | Last Name		| sn			| 
| | ... | Department	| department	| 
| | ... | UniqueID		| objectguid	| 
| | ... | Manager		| manager

| | Create LDAP Integration Point | ${Step1Fields} | ${Step2Fields} | ${Step3Mapping} | Append/Overlay | Email

| | Run Import Now on the Following Integration Point	| ${IntegrationName}
| | [Teardown]		| Run Keywords | Drop connection to the relativity services api | Capture Page Screenshot

| Delete the Objects created in the Suite then Close All Browsers
| | [Documentation]	| Delete the Suite test data of Integration Point and Custodians.
| | Setup connection to the Relativity Services API for Legal Hold	| ${WorkspaceName}

| | Delete the following Integration Point using API | ${IntegrationName}

| | Delete the Custodians in the Following Department using API	| Nested
| | Delete the Custodians in the Following Department using API	| RootNested

| | Drop connection to the relativity services api
| | Close All Browsers


*** Settings ***
| Library	| Collections
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${KEYWORD_DIR}/LegalHold/Legal_Hold_Keywords_API.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/GridKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/MiscLegalHoldKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/QuestionnaireKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/IntegrationPointKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/DatabaseKeywords.txt