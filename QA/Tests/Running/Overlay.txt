
*** Settings ***
| Documentation 	| Verify that running an Overlay RIP updates existing records but does not creates new records.
| Metadata			| Authors	| Daniel Oliver	| William Sehy
| Force Tags		| in-progress	| team.game_of_holds	| feature.integration_points
| Suite Setup		| Create Suite Setup Data
| Test Teardown 	| Run Keyword if Test Failed	| Capture Page Screenshot
| Suite Teardown	| Delete the Objects created in the Suite then Close All Browsers


*** Variables ***

| ${WorkspaceName}				| LDAP Sync
| ${IntegrationName}			| Overlay 
| ${ExistingAndUpdatedObject}	| Time, Night
| ${NewObject}					| Time, Sleep
| ${ExistingNoChangeObject}		| Time, Day
| ${ExistingObjectNulled}		| Time, Lunch
| ${Custodians Grid}	| gbox_custodianGrid


*** Test Cases ***

| An Overlay import updates existing modified records
| | [Documentation]	| Verify that when an Overlay RIP is run with an existing record in the Target RDO with updated details in the Source, 
| | ...				| that it does update the existing records in the Target RDO. In this test, the Custodian's Department is updated.
| | ...				| [id] 139cff40-a89f-11e4-975e-b8ca3a83048f
| | [Tags]			| automated
| | Go to the Tab	| Custodian
| | Filter the following Grid on the following Columns by the following Values	| ${Custodians Grid}	| 
| | ... | Name			| ${ExistingAndUpdatedObject}	| 
| | ... | Department	| Time

| | Verify that the following Grid contains the following number of Rows	| ${Custodians Grid}	| 1


| An Overlay import does not Create new records
| | [Documentation]	| Verify that when an Overlay RIP is run with an existing record in the Target RDO with identical details in the Source, 
| | ...				| that it does not update the existing record in the Target RDO.
| | ...				| [id] 13a00c80-a89f-11e4-a4a8-b8ca3a83048f
| | [Tags]			| automated
| | Go to the Tab	| Custodian
| | Filter the following Grid on the following Column by the following Value	| ${Custodians Grid}	| Name	| ${NewObject}

| | Verify that the following Grid contains the following number of Rows	| ${Custodians Grid}	| 0


| Updates a Value to Null
| | [Documentation]	| Verify that when an Overlay RIP is run with an existing record in the Target RDO with one field popuplated, 
| | ...				| that it does update the existing record in the Target RDO to Null.
| | ...				| [id] 13a00c83-a89f-11e4-b392-b8ca3a83048f
| | [Tags]			| automated
| | Go to the Tab	| Custodian
| | Filter the following Grid on the following Columns by the following Values	| ${Custodians Grid}	| Name	| ${ExistingObjectNulled}

| | @{Headers}=	| Create List	| Name						| Department
| | @{Values}=	| Create List	| ${ExistingObjectNulled} 	| ${EMPTY}
| | Verify that the following grid with the following headers does not have the following rows	| ${Custodians Grid}	| ${Headers}	| ${Values}


| Does not Update when there are no Changes
| | [Documentation]	| Verify that when an Overlay RIP is run with an existing record in the Target RDO with identical details in the Source, 
| | ...				| that it does not update the existing record in the Target RDO.
| | ...				| [id] 13a00c81-a89f-11e4-8795-b8ca3a83048f
| | [Tags]			| manual
| | Go to the Tab	| History

| | Run the following manual steps:
| | ... | Steps:
| | ... | 1. Change to the All History View
| | ... | 2. View the latest History records for the Custodian "${ExistingNoChangeObject}" import "Update - Import"
| | ... |
| | ... | Expected Results:
| | ... | Verify that the "Update - Import" entry for the Custodian "${ExistingNoChangeObject}" did not change the existing values to new values, 
| | ... | they just changed from the existing values to the same existing values (except for UniqueID which may or may not have updated).


*** Keywords *** 

| Create Suite Setup Data
| | [Documentation]	| Create the Suite Preconditions
| | Setup connection to the relativity database
| | Open and Log in to Legal Hold
| | Setup connection to the Relativity Services API for Legal Hold	| ${WorkspaceName}

#------------------------------------------------------------------------------------------------------------------------

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${ExistingAndUpdatedObject}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${ExistingAndUpdatedObject}
| | Create a Custodian using API	| 
| | ... | Full Name		| ${ExistingAndUpdatedObject}
| | ... | Email			| ntime@gameofholdstesting.corp 

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${ExistingNoChangeObject}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${ExistingNoChangeObject}
| | Create a Custodian using API	| 
| | ... | Full Name		| ${ExistingNoChangeObject}
| | ... | Email			| dtime@gameofholdstesting.corp
| | ... | Department	| Time

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${ExistingObjectNulled}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${ExistingObjectNulled}
| | Create a Custodian using API	| 
| | ... | Full Name		| ${ExistingObjectNulled}
| | ... | Email			| ltime@gameofholdstesting.corp
| | ... | Department	| Time

#------------------------------------------------------------------------------------------------------------------------

| | @{Step1Fields}=	| Create List	| 
| | ... | ${IntegrationName}
| | ... | LDAP
| | ... | Custodian
| | ... | No

| | @{Step2Fields}=	| Create List	| 
| | ... | gameofholds.testing.corp/CN=Users,DC=gameofholds,DC=testing,DC=corp
| | ... | (department=Time) 
| | ... | FastBind
| | ... | testing\\rellockdown
| | ... | P@ssw0rd@1

| | @{Step3Mapping}=	| Create List	|
| | ... | Email			| mail
| | ... | Department	| department
| | ... | First Name	| givenname
| | ... | Last Name		| sn
| | ... | UniqueID		| objectguid

| | Create LDAP Integration Point	| ${Step1Fields}	| ${Step2Fields}	| ${Step3Mapping} | Overlay Only	| Email

| | Run Import Now on the Following Integration Point	| ${IntegrationName}
| | [Teardown]		| Drop connection to the relativity services api

| Delete the Objects created in the Suite then Close All Browsers
| | [Documentation]	| Delete the Suite test data of Integration Point and Custodians.
| | Setup connection to the Relativity Services API for Legal Hold	| ${WorkspaceName}

| | Delete the following Integration Point using API	| ${IntegrationName}

| | Delete the Custodians in the Following Department using API	| Time

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${ExistingObjectNulled}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${ExistingObjectNulled}

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${ExistingAndUpdatedObject}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${ExistingAndUpdatedObject}

| | Drop connection to the relativity services api
| | Close All Browsers


*** Settings ***
| Library	| Collections
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${KEYWORD_DIR}/LegalHold/Legal_Hold_Keywords_API.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/GridKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/MiscLegalHoldKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/GenericKeywords.txt
