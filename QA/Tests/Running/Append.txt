
*** Settings ***
| Documentation 	| Verify that running an Append RIP creates new objects, but does not update existing objects.
| Metadata			| Authors	| Daniel Oliver	| William Sehy
| Force Tags		| in-progress	| team.game_of_holds	| feature.integration_points
| Suite Setup		| Create Suite Setup Data
| Test Teardown 	| Run Keyword if Test Failed	| Capture Page Screenshot
| Suite Teardown	| Delete the Objects created in the Suite then Close All Browsers


*** Variables ***

| ${WorkspaceName}				| LDAP Sync
| ${IntegrationName}			| Append
| ${NewObject}					| Man, Cat
| ${ExistingAndUpdatedObject}	| Man, Dog
| ${ExistingNoChangeObject}		| Man, Bird 
| ${Manager}					| Man, Bigdog


*** Test Cases ***

| An Append import Creates new objects
| | [Documentation] | Verify that when an Append RIP is run which has new Source objects, that it creates new objects in the Target RDO.
| | ...				| [id] 9632b8ae-b203-11e4-8099-90b11c6c5ae4
| | [Tags]			| automated
| | View Custodian | ${NewObject}	
| | Verify the current layout has fields with the following values | Email | catman@gameofholdstesting.corp


| An Append imports will Overlay Managers
| | [Documentation] | Verify that Append imports will Overlay Manager information by linking an Existing Custodians to a new Manager.
| | ...				| [id] 65ef890f-e46e-11e4-8722-90b11c6c5ae4
| | [Tags]			| broken
| | View Custodian	| ${ExistingAndUpdatedObject} 

| | Verify the current layout has fields with the following values | Manager | ${Manager}


| An Append import Creates new History objects
| | [Documentation] | Verify that when an Append RIP is run which has new source recods, 
| | ...				| that there are history entries for the newly created objects in the target RDO.
| | ...				| [id] 12e9dbe1-a89f-11e4-b55e-b8ca3a83048f
| | [Tags]			| manual
| | Go to the Tab	| History

| | Run the following manual steps:
| | ... | Steps:
| | ... | 1. Change to the All History View
| | ... | 2. View the History objects for "${NewObject}"
| | ... |
| | ... | Expected Results:
| | ... | Verify that there is an entry for the Custodian "${NewObject}" with the Action of "Create"


| An Append import does not update existing objects
| | [Documentation] | Verify that when an Append RIP is run which has updated Source objects that match to existing objects, 
| | ...				| that it does not update the existing objects in the target RDO.
| | ...				| [id] 12ea02f0-a89f-11e4-b98c-b8ca3a83048f
| | [Tags]			| manual | broken 
# Each of the three existing records should have an "error" as they already existed.
| | Go to the Tab	| History

| | Run the following manual steps:
| | ... | Steps:
| | ... | 1. Change to the All History View
| | ... | 2. View the History objects for "${ExistingAndUpdatedObject}"
| | ... |
| | ... | Expected Results:
| | ... | Verify that there is no entry for "${ExistingAndUpdatedObject}" with the Action of "Create"


| An Append import does not update existing unmodified objects
| | [Documentation] | Verify that when an Append RIP is run which has Source objects that match to existing objects with no changes, 
| | ...				| that it does not update the existing objects in the target RDO.
| | ...				| [id] 12ea02f1-a89f-11e4-a719-b8ca3a83048f
| | [Tags]			| manual
| | Go to the Tab	| History

| | Run the following manual steps:
| | ... | Steps:
| | ... | 1. Change to the All History View
| | ... | 2. View the History objects for "${ExistingNoChangeObject}"
| | ... |
| | ... | Expected Results:
| | ... | Verify that there is no entry for "${ExistingNoChangeObject}" with the Action of "Create"


| An Append import's Job History does not fail when there are Existing Objects
| | [Documentation] | Verify that when an Append RIP is run which has Source objects that match to existing objects,
| | ...				| that the Import's Job History has not errors.
| | ...				| 
| | [Tags]			| manual
| | Go to the Tab	| Job History

| | Run the following manual steps:
| | ... | Expected Results:
| | ... | Verify that the newest entry for "${IntegrationName}" has no Errors


*** Keywords *** 

| Create Suite Setup Data
| | [Documentation]	| Create the Suite Preconditions
| | Setup connection to the relativity database
| | Open and Log in to Legal Hold
| | Setup connection to the Relativity Services API for Legal Hold	| ${WorkspaceName}

# --------------------------------------------------------------------------------------------------------

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${ExistingNoChangeObject}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${ExistingNoChangeObject}
| | Create a Custodian using API	| 
| | ... | Full Name		| ${ExistingNoChangeObject}			|
| | ... | Email			| birdman@gameofholdstesting.corp	|
| | ... | Department	| Append

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${ExistingAndUpdatedObject}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${ExistingAndUpdatedObject}
| | Create a Custodian using API	| 
| | ... | Full Name		| ${ExistingAndUpdatedObject}		|
| | ... | Email			| dogman@gameofholdstesting.corp	|
| | ... | Department	| Append

# --------------------------------------------------------------------------------------------------------

| | @{Step1Fields}=	| Create List	|
| | ... | ${IntegrationName}
| | ... | LDAP
| | ... | Custodian
| | ... | No

| | @{Step2Fields}=		| Create list | gameofholds.testing.corp/CN=Users,DC=gameofholds,DC=testing,DC=corp | 
| | ... | (department=Append) | FastBind | testing\\rellockdown | P@ssw0rd@1

| | @{Step3Mapping}=	| Create List	|
| | ... | Email			| mail			| 
| | ... | Department	| department	| 
| | ... | First Name	| givenname		| 
| | ... | Last Name		| sn			| 
| | ... | Manager       | manager       | 
| | ... | UniqueID		| objectguid
| | Create LDAP Integration Point | ${Step1Fields} | ${Step2Fields} | ${Step3Mapping} | Append Only | Email

| | Run Import Now on the Following Integration Point | ${IntegrationName}
| | [Teardown]		| Run Keywords | Drop connection to the relativity services api | Capture Page Screenshot

| Delete the Objects created in the Suite then Close All Browsers
| | [Documentation]	| Delete the Suite test data of Integration Point and Custodians.
| | Setup connection to the Relativity Services API for Legal Hold	| ${WorkspaceName}

| | Delete the following Integration Point | ${IntegrationName}
| | Delete the Custodians in the Following Department using API | Append

| | Drop connection to the relativity services api
| | Close All Browsers


*** Settings ***
| Library	| Collections
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${KEYWORD_DIR}/LegalHold/Legal_Hold_Keywords_API.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/GridKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/MiscLegalHoldKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/StepOneKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/StepTwoKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/StepThreeKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/GenericKeywords.txt