
*** Settings ***
| Documentation 	| Verify that Step 2 of FTP field validation works correctly.
| Metadata		    | Authors | Daniel Oliver	| William Sehy
| Force Tags		| in-progress	| team.game_of_holds	| feature.integration_points
| Suite Setup		| Open and Log in to Workspace
| Test Setup        | Go to Step 2 of the Create Integration Point Process
| Test Teardown 	| Capture screenshot if test fails
| Suite Teardown	| Close All Browsers


*** Variables ***

| ${WorkspaceName}			| LDAP Sync


*** Test Cases ***

| A Blank Host field shows an error message after clicking Next
| | [Documentation]	| Verify that when the Host field is blank the correct warning message is shown when clicking Next.
| | ...				| [id] 047cb540-2f36-11e6-8ebb-989096e24b46
| | [Tags]			| automated	| required
| | Fill out the Following Fields of the FTP Integration Point Step 2 form | ${EMPTY} | FTP | 21 | rip | GameOfHolds | /ftp/LegalHold/AppendFTP.csv
| | Click | Next
| | Wait Until Keyword Succeeds	| 30s	| 2s	| 
| | ... | Verify that a Page Error with the following Text is present on the Wizard	| Please enter a valid host name. e.g. 172.31.24.97


| Proceeding Post-blank Host required field message
| | [Documentation]	| Verify that the next screen can be reached after the balnk Host message is shown.
| | ...				| [id] 06807480-2f36-11e6-a127-989096e24b46
| | [Tags]			| automated
| | [Setup]			| Require Test Case	| A Blank Host field shows an error message after clicking Next
| | Fill out the Following Fields of the FTP Integration Point Step 2 form | files.kcura.com
| | Click | Next
| | Wait Until Keyword Succeeds	| 30s | 2s | Verify that you are on the following Step of the Create Integration Point Process	| Map Fields


| An invalid Host shows an error message after clicking Next
| | [Documentation]	| Verify that when the Host field is invalid the correct warning message is shown when clicking Next.
| | ...				| [id] 26f882f0-2f3d-11e6-8498-989096e24b46
| | [Tags]			| automated	| required
| | Fill out the Following Fields of the FTP Integration Point Step 2 form | Cats | FTP | 21 | rip | GameOfHolds | /ftp/LegalHold/AppendFTP.csv
| | Click | Next
| | Wait Until Keyword Succeeds	| 30s	| 2s	| 
| | ... | Verify that a Page Error with the following Text is present on the Wizard	| The remote name could not be resolved: 'cats'


| Proceeding Post-Invalid Host required field message
| | [Documentation]	| Verify that the next screen can be reached after the invalid Host message is shown.
| | ...				| [id] 06807480-2f36-11e6-a127-989096e24b46
| | [Tags]			| automated
| | [Setup]			| Require Test Case	| An invalid Host shows an error message after clicking Next
| | Fill out the Following Fields of the FTP Integration Point Step 2 form | files.kcura.com
| | Click | Next
| | Wait Until Keyword Succeeds	| 30s | 2s | Verify that you are on the following Step of the Create Integration Point Process	| Map Fields


| An invalid CSV Filepath shows a message after clicking Next
| | [Documentation]	| Verify that when the CSV Filepath field is invalid the correct warning message is shown when clicking Next.
| | ...				| [id] 1f8438f0-2f35-11e6-80cc-989096e24b46
| | [Tags]			| automated	| required
| | Fill out the Following Fields of the FTP Integration Point Step 2 form | files.kcura.com | FTP | 21 | rip | GameOfHolds | /ftp/LegalHold/JUNK.csv
| | Click | Next
| | Wait Until Keyword Succeeds	| 30s	| 2s	| 
| | ... | Verify that a Page Error with the following Text is present on the Wizard | File not found.


| Proceeding Post-File Not Found CSV Filepath field message
| | [Documentation]	| Verify that the next screen can be reached after the File not found message is shown.
| | ...				| [id] 0d1aabcf-2f36-11e6-954a-989096e24b46
| | [Tags]			| automated
| | [Setup]			| Require Test Case	| An invalid CSV Filepath shows a message after clicking Next
| | Fill out the Following Fields of the FTP Integration Point Step 2 form | files.kcura.com | FTP | 21 | rip | GameOfHolds | /ftp/LegalHold/AppendFTP.csv
| | Proceed to the Next Step of the Integration Point wizard
| | Wait Until Keyword Succeeds	| 30s | 2s | Verify that you are on the following Step of the Create Integration Point Process	| Map Fields


| Unable to connect to the remote server message is displayed when unable to connect using the given values
| | [Documentation]	| Verify that the "Unable to connect to source using the specified settings" warning message is displayed after clicking Next while the connection details are invalid
| | ...				| [id] 4260a38f-2f44-11e6-9f17-989096e24b46
| | [Tags]			| automated	| required
| | Fill out the Following Fields of the FTP Integration Point Step 2 form | files.kcura.com | FTP | 212 | rip | GameOfHolds | /ftp/LegalHold/AppendFTP.csv
| | Click | Next
| | Wait Until Keyword Succeeds	| 30s	| 2s	| 
| | ... | Verify that a Page Error with the following Text is present on the Wizard | Unable to connect to the remote server 


| Proceeding Post- Unable to connect to the remote server message
| | [Documentation]	| Verify that the next screen can be reached after the Invalid Connection message is shown.
| | ...				| [id] 46280bcf-2f44-11e6-9436-989096e24b46
| | [Tags]			| automated
| | [Setup]			| Require Test Case	| Unable to connect to the remote server message is displayed when unable to connect using the given values
| | Fill out the Following Fields of the FTP Integration Point Step 2 form | files.kcura.com | FTP | 21 | rip | GameOfHolds | /ftp/LegalHold/AppendFTP.csv
| | Click | Next
| | Wait Until Keyword Succeeds	| 30s | 2s | Verify that you are on the following Step of the Create Integration Point Process	| Map Fields


*** Keywords *** 

| Verify that a Page Error with the following Text is present on the Wizard
| | [Documentation]	| Verifies that the Integration Points Wizard has the given page level error.
| | [ARGUMENTS] 	| ${ExpectedErrorText}
| | Element Should Be Visible	| css=div.page-message.page-error	| No Page Error was present on the page.
| | ${ActualErrorText}=	| Get text	| css=div.page-message.page-error
| | Run Keyword Unless	| "${ExpectedErrorText}" == "${ActualErrorText}"	| Fail	| The Error text was incorrect. It was "${ActualErrorText}" when it should have been "${ExpectedErrorText}".

| Go to Step 2 of the Create Integration Point Process
| | Open Create Integration Point
| | Fill out the Following Fields of the Integration Point Step 1 form | Step 2 Testing | FTP (CSV File) | Custodian | No
| | Proceed to the Next Step of the Integration Point wizard

| Verify the following Integration Point Field does not have the following Error
| | [Documentation]	| Verifies that the given field does not have the given error.
| | ... 			| Must be called from the Integration Point Create/Edit pages.
| | [ARGUMENTS] 	| ${FieldID}	| ${ErrorText}
| | ${FieldID}=	| fetch from right	| ${FieldID}	| s2id_
| | Element Should Not Be Visible	| xpath=//*[@id='${FieldID}']/following-sibling::div/child::*[contains(@class, 'icon-error') and contains(@class, 'field-validation-error')]	| An error message was found on the field with the ID "${FieldID}".
| | ${ActualText}=	| Get Text	| xpath=//*[@id='${FieldID}']/following-sibling::div/child::*[contains(@class, 'icon-error') and contains(@class, 'field-validation-error')]
| | Run Keyword Unless	| '${ActualText}' == '${ErrorText}'	| Fail	| The error was incorrect. It should have been "${ErrorText}" but it was actually "${ActualText}".


*** Settings ***
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${KEYWORD_DIR}/LegalHold/Legal_Hold_Keywords_API.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/MiscLegalHoldKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/GenericKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/StepOneKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/StepTwoKeywords.txt