
*** Settings ***
| Documentation		| Verify that the Next and Back buttons on the Integration Point Wizard
| ...				| have the correct states (enabled/disabled) under various conditions when using FTP.
| Metadata			| Authors	| William Sehy
| Force Tags		| in-progress	| team.game_of_holds	| feature.integration_points
| Suite Setup		| Open and Log in to Legal Hold
#| Test Setup		| NONE
| Test Teardown		| Capture Screenshot If Test Fails
| Suite Teardown	| Close All Browsers


*** Variables ***

| ${WorkspaceName}		| LDAP Sync
| ${Host}				| filetransfer.kcura.com
| ${Protocol}			| FTP
| ${Port}				| 21
| ${FTPUsername}		| admin
| ${FTPPassword}		| C@m3l!ts
| ${Filepath}			| /Custom Development/ftp/LegalHold/AppendFTP.csv
| ${Back Button}		| back
| ${Next Button}		| next
| ${Save Button}		| save


*** Test Cases ***

| Only the Back button is disabled when you return to Step 1 from Step 2
| | [Documentation] | Verify that The Back button is disabled when you return to Step 1 from Step 2.
| | ...				| 
| | [Tags]			| automated
| | Open Create Integration Point
| | Fill out the Following Fields of the Integration Point Step 1 form	| FTP Progress Bar Button Testing | FTP | Custodian | No
| | Proceed to the Next Step of the Integration Point wizard
| | Proceed to the Previous Step of the Integration Point wizard

| | Run Keyword and Continue on Failure	| Verify that the following Progress Bar Button is Disabled	| ${Back Button}
| | Run Keyword and Continue on Failure	| Verify that the following Progress Bar Button is Enabled	| ${Next Button}


| Both Buttons are enabled when you first arrive on Step 2 (FTP)
| | [Documentation] | Verify that the buttons is enabled when you first arrive on Step 2.
| | ...				| 
| | [Tags]			| automated
| | Open Create Integration Point
| | Fill out the Following Fields of the Integration Point Step 1 form	| FTP Progress Bar Button Testing | FTP | Custodian | No
| | Proceed to the Next Step of the Integration Point wizard

| | Run Keyword and Continue on Failure	| Verify that the following Progress Bar Button is Enabled	| ${Back Button}
| | Run Keyword and Continue on Failure	| Verify that the following Progress Bar Button is Enabled	| ${Next Button}


| The Buttons are both enabled when you return to Step 2 from Step 3
| | [Documentation] | Verify that both buttons are enabled when you return to Step 2 from Step 3.
| | ...				| 
| | [Tags]			| automated
| | Open Create Integration Point
| | Fill out the Following Fields of the Integration Point Step 1 form	| FTP Progress Bar Button Testing | FTP | Custodian | No
| | Proceed to the Next Step of the Integration Point wizard
| | Fill out the Following Fields of the FTP Integration Point Step 2 form	| 
| | ... | Host=${Host}
| | ... | Protocol=${Protocol}
| | ... | Port=${Port}
| | ... | Username=${FTPUsername}
| | ... | Password=${FTPPassword}
| | ... | Filepath=${Filepath}
| | Proceed to the Next Step of the Integration Point wizard
| | Proceed to the Previous Step of the Integration Point wizard

| | Run Keyword and Continue on Failure	| Verify that the following Progress Bar Button is Enabled	| ${Back Button}
| | Run Keyword and Continue on Failure	| Verify that the following Progress Bar Button is Enabled	| ${Next Button}


| The Buttons are both Enabled when you first arrive on Step 3
| | [Documentation] | Verify that both Buttons are Enabled when you first arrive on Step 3.
| | ...				| 
| | [Tags]			| automated
| | Open Create Integration Point
| | Fill out the Following Fields of the Integration Point Step 1 form	| FTP Progress Bar Button Testing | FTP | Custodian | No
| | Proceed to the Next Step of the Integration Point wizard
| | Fill out the Following Fields of the FTP Integration Point Step 2 form	| 
| | ... | Host=${Host}
| | ... | Protocol=${Protocol}
| | ... | Port=${Port}
| | ... | Username=${FTPUsername}
| | ... | Password=${FTPPassword}
| | ... | Filepath=${Filepath}
| | Proceed to the Next Step of the Integration Point wizard

| | Run Keyword and Continue on Failure	| Verify that the following Progress Bar Button is Enabled	| ${Back Button}
| | Run Keyword and Continue on Failure	| Verify that the following Progress Bar Button is Enabled	| ${Save Button}


*** Keywords ***

| Verify that the following Progress Bar Button is Enabled
| | [ARGUMENTS]	| ${ButtonID}
| | Element Should Be Visible		| xpath=//button[@id='${ButtonID}']	| The button with the ID "${ButtonID}" was not visible.
| | Element Should Not Be Visible	| xpath=//button[@id='${ButtonID}' and @disabled]	| The button with the ID "${ButtonID}" was Disabled.

| Verify that the following Progress Bar Button is Disabled
| | [ARGUMENTS]	| ${ButtonID}
| | Element Should Be Visible	| xpath=//button[@id='${ButtonID}']	| The button with the ID "${ButtonID}" was not visible.
| | Element Should Be Visible	| xpath=//button[@id='${ButtonID}' and @disabled]	| The button with the ID "${ButtonID}" was Enabled.


*** Settings ***
| Library	| ${KEYWORD_DIR}/ManualKeywords.py
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/GenericKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/StepOneKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/StepTwoKeywords.txt