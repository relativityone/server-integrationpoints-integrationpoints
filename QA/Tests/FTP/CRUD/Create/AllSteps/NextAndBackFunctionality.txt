
*** Settings ***
| Documentation 	| Verify that Next and Back buttons on the Integration Point Wizard affect the data correctly when using FTP.
| Metadata			| Authors	| William Sehy	| Daniel Oliver
| Force Tags		| in-progress	| team.game_of_holds	| feature.integration_points
| Suite Setup		| Run Keywords	| Setup connection to the relativity database	| Open and Log in to Legal Hold
#| Test Setup		| NONE
| Test Teardown 	| Run Keyword if Test Failed	| Capture Page Screenshot
| Suite Teardown	| Close All Browsers


*** Variables ***

| ${WorkspaceName}			| LDAP Sync
| ${IntegrationName}		| FTP Next and Back Storage Test
| ${Host}					| filetransfer.kcura.com
| ${Protocol}				| FTP
| ${Port}					| 21
| ${FTPUsername}			| admin
| ${FTPPassword}			| C@m3l!ts
| ${Filepath}				| /Custom Development/ftp/LegalHold/AppendFTP.csv
| ${OverwriteList}			| s2id_overwrite
| ${UniqueIdentifierList}	| s2id_overlay-identifier


*** Test Cases ***


| Returning to Step 3 from Step 2 without changing anything Saves all Step 3 data
| | [Documentation]	| Verify that if you fill out steps 1 through 3 (FTP), go back to step 2 (FTP), then return to step 3 (FTP), that all of the previously entered data on step 3 (FTP) will still be present.
| | ...				| 
| | [Tags]			| automated
| | @{Step1Fields}=		| Create List	| ${IntegrationName}	| FTP	| Custodian	| No
| | @{Step2Fields}=		| Create List	| ${Host}	| ${Protocol}	| ${Port}	| ${FTPUsername}	| ${FTPPassword}	| ${Filepath}
| | @{Step3Mapping}=	| Create List	| 
| | ... | UniqueID		| unique_id
| | ... | First Name	| FirstName

| | Create FTP Integration Point without Saving	| ${Step1Fields}	| ${Step2Fields}	| ${Step3Mapping}	| Overlay Only	| Email 

| | Proceed to the Previous Step of the Integration Point wizard
| | Proceed to the Next Step of the Integration Point wizard

| | Verify that the Selected Workspace fields are in the following order	| UniqueID	| First Name
| | Verify that the Selected Source Attribute fields are in the following order	| unique_id	| FirstName
| | Verify that the following select2 Dropdown currently has the following value selected	| ${OverwriteList}			| Overlay Only
| | Verify that the following select2 Dropdown currently has the following value selected	| ${UniqueIdentifierList}	| Email


| Returning to Step 2 from Step 1 without changing anything Saves all Step 2 data
| | [Documentation]	| Verify that if you fill out steps 1 and 2 (FTP), go back to step 1, then return to step 2 (FTP), that all of the previously entered data on step 2 (FTP) will still be present.
| | ...				| 
| | [Tags]			| partially_automated	| manual	| field_reading	| day508
| | Open Create Integration Point
| | Fill out the Following Fields of the Integration Point Step 1 form	| ${IntegrationName}	| FTP	| Custodian	| No
| | Proceed to the Next Step of the Integration Point wizard

| | Fill out the Following Fields of the FTP Integration Point Step 2 form |
| | ... | Host=${Host}
| | ... | Protocol=${Protocol}
| | ... | Port=${Port}
| | ... | Username=${FTPUsername}
| | ... | Password=${FTPPassword}
| | ... | Filepath=${Filepath}

| | Proceed to the Previous Step of the Integration Point wizard

| | Proceed to the Next Step of the Integration Point wizard

| | Run the following manual steps:
| | ... | Expected Results:
| | ... | Verify that the Fields have the following data entered: 
| | ... | - Host: ${Host}
| | ... | - Protocol: ${Protocol}
| | ... | - Port: ${Port}
| | ... | - Username: ${FTPUsername}
| | ... | - Password: [A number of dots equal to the number of characters in "${FTPPassword}"]
| | ... | - Filepath: ${Filepath}


| Returning to Step 3 from Step 1 without changing anything Saves all Step 3 data
| | [Documentation]	| Verify that if you fill out steps 1 through (FTP) 3, go back to step 1, then return to step 3, that all of the previously entered data on step 3 (FTP) will still be present.
| | ...				| 
| | [Tags]			| automated
| | @{Step1Fields}=		| Create List	| ${IntegrationName}	| FTP	| Custodian	| No
| | @{Step2Fields}=		| Create List	| ${Host}	| ${Protocol}	| ${Port}	| ${FTPUsername}	| ${FTPPassword}	| ${Filepath}
| | @{Step3Mapping}=	| Create List	| 
| | ... | UniqueID		| unique_id
| | ... | First Name	| FirstName

| | Create FTP Integration Point without Saving	| ${Step1Fields}	| ${Step2Fields}	| ${Step3Mapping}	| Overlay Only	| Email 

| | Proceed to the Previous Step of the Integration Point wizard
| | Proceed to the Previous Step of the Integration Point wizard
| | Proceed to the Next Step of the Integration Point wizard
| | Proceed to the Next Step of the Integration Point wizard

| | Verify that the Selected Workspace fields are in the following order	| UniqueID	| First Name
| | Verify that the Selected Source Attribute fields are in the following order	| unique_id	| FirstName
| | Verify that the following select2 Dropdown currently has the following value selected	| ${OverwriteList}			| Overlay Only
| | Verify that the following select2 Dropdown currently has the following value selected	| ${UniqueIdentifierList}	| Email


#| Going back from Step 3 and changing the Connection Path in Step 2 to remove some of the Source Attributes selections on Step 3 maintains the Mappings
#| | [Documentation]	| Verify that if you fill out steps 1 through 3, go back to step 2 and change the Connection Path in step 2 to 
#| | ...				| something that does not have some of the selected Source Attributes, then when you return to step 3 the remaining Source Attribute selections 
#| | ...				| will still be correctly mapped with the Workspace Fields selections. Also the Unique Identifier dropdown value will be saved.
#| | ...				| 
#| | [Tags]			| automated	| dan682
#| | @{Step1Fields}=		| Create List	| ${IntegrationName1}	| LDAP	| Custodian	| No
#| | @{Step2Fields}=		| Create List	| ${ConnectionPath1}	| ${ObjectFilterString}	| ${Authentication}	| ${IPUsername}	| ${IPPassword}
#| | @{Step3Mapping}=	| Create List	|
#| | ... | Department	| objectguid	|
#| | ... | Email			| mail 
#| | Create FTP Integration Point without Saving	| ${Step1Fields}	| ${Step2Fields}	| ${Step3Mapping}	| Overlay Only	| Email 
#| | Select Radio Button	| ${ManagerRadioButton}	| false
#| | Proceed to the Previous Step of the Integration Point wizard
#| | Fill out the Following Fields of the LDAP Integration Point Step 2 form	| ${ConnectionPath2}
#| | Proceed to the Next Step of the Integration Point wizard
#
#| | Verify that the Selected Workspace fields are in the following order	| Department	| Email
#| | Verify that the following select2 Dropdown currently has the following value selected	| ${UniqueIdentifierList}	| Email
#| | Verify that the Selected Source Attribute fields are in the following order	| objectguid
#| | Radio button should be set to	| ${ManagerRadioButton}	| false


| Going back from Step 3 and changing Step 1's Destination RDO resets everything on Step 3
| | [Documentation]	| Verify that if you fill out steps 1 through (FTP) 3, go back to step 1 and change the Destination RDO, then when you return to step 3 (FTP)
| | ...				| all of the Fields selections and the dropdowns will be reset.
| | ...				| 
#| | [Tags]			| automated
| | @{Step1Fields}=		| Create List	| ${IntegrationName}	| FTP	| Custodian	| No
| | @{Step2Fields}=		| Create List	| ${Host}	| ${Protocol}	| ${Port}	| ${FTPUsername}	| ${FTPPassword}	| ${Filepath}
| | @{Step3Mapping}=	| Create List	| 
| | ... | UniqueID		| unique_id
| | ... | First Name	| FirstName

| | Create FTP Integration Point without Saving	| ${Step1Fields}	| ${Step2Fields}	| ${Step3Mapping}	| Overlay Only	| Email
 
| | Proceed to the Previous Step of the Integration Point wizard
| | Proceed to the Previous Step of the Integration Point wizard
| | Fill out the Following Fields of the Integration Point Step 1 form	| ${IntegrationName}	| FTP	| Questionnaire	| No

| | Proceed to the Next Step of the Integration Point wizard
| | Proceed to the Next Step of the Integration Point wizard

| | Run Keyword And Continue On Failure	| Verify that the Selected Field Mapping list is empty
| | Run Keyword And Continue On Failure	| Verify that the Selected Source Attributes list is empty
| | Run Keyword And Continue On Failure	| Verify that the following select2 Dropdown currently has the following value selected	| ${OverwriteList}			| Append Only				| # Default Value
| | Run Keyword And Continue On Failure	| Verify that the following select2 Dropdown currently has the following value selected	| ${UniqueIdentifierList}	| Name [Object Identifier]	| # Default Value


*** Keywords ***

| Verify that the Selected Source Attributes list is empty
| | [Documentation]	| A keyword that confirms that the Selected Source Attribute list does not have any fields allocated to it
| | Wait Until Keyword Succeeds	| 20s	| 2s	| Element should be visible	| xpath=//select[@id='selected-source-fields']	| The Selected Fields list box was not visible.

| | Element should not be visible	| xpath=//select[@id='selected-source-fields']/child::option

| Verify that the Selected Field Mapping list is empty
| | [Documentation]	| A keyword that confirms that the Selected Field Mapping list does not have any fields allocated to it
| | Wait Until Keyword Succeeds	| 20s	| 2s	| Element should be visible	| xpath=//select[@id='selected-workspace-fields']	| The Selected Fields list box was not visible.

| | Element should not be visible	| xpath=//select[@id='selected-workspace-fields']/child::option

| Create FTP Integration Point without Saving
| | [Documentation]	| A keyword that creates an Integration Point but does not click Save.
| | ...				| The value of Step1Fields should be the arguments from "Fill out the Following Fields of the Integration Point Step 1 form" in a list variable.
| | ...				| The value of Step2Fields should be the arguments from "Fill out the Following Fields of the FTP Integration Point Step 2 form" in a list variable.
| | ...				| The value of Step3Mapping should be the arguments from "Map the Following Fields on the Integration Point Step 3 form" in a list variable.
| | ... 			| The value of Overwrite should be "Overlay Only", "Overlay Only", or "ppend/Overlay".
| | [ARGUMENTS]		| ${Step1Fields}	| ${Step2Fields}	| ${Step3Mapping}	| ${Step3Overwrite}	| ${Step3UniqueID}
| | Open Create Integration Point
| | Fill out the Following Fields of the Integration Point Step 1 form	| @{Step1Fields}
| | Proceed to the Next Step of the Integration Point wizard

| | Fill out the Following Fields of the FTP Integration Point Step 2 form	| @{Step2Fields}
| | Proceed to the Next Step of the Integration Point wizard
| | Wait Until Element Is Visible	| xpath=//select[@id='source-fields']/child::option	| 15s	| No Attribute fields were found.

| | Map the Following Fields on the Integration Point Step 3 form	| @{Step3Mapping}
| | Select from select2 Dropdown	| s2id_overwrite			| ${Step3Overwrite}
| | Select from select2 Dropdown	| s2id_overlay-identifier	| ${Step3UniqueID}


*** Settings ***
| Library	| Collections
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${KEYWORD_DIR}/LegalHold/Legal_Hold_Keywords_API.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/MiscLegalHoldKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/GenericKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/StepOneKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/StepTwoKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/StepThreeKeywords.txt