
*** Settings ***
| Documentation 	| Verify that the FTP Relativity Integration Points application can import into objects besides Custodians.
| Metadata			| Authors	| Daniel Oliver
| Force Tags		| in-progress	| team.game_of_holds	| feature.integration_points
| Suite Setup		| Create Suite Setup Data
| Test Teardown 	| Run Keyword if Test Failed	| Capture Page Screenshot
| Suite Teardown	| Delete the Objects created in the Suite then Close All Browsers


*** Variables ***

| ${WorkspaceName}		| LDAP Sync
| ${IntegrationName}	| Projects


*** Test Cases ***

| Project Objects can be created
| | [Documentation]	| Verify that the FTP Relativity Integration Points can be used to create Projects objects.
| | ...				| [id] ccc8600f-325d-11e6-a0a0-989096e24b46
| | [Tags]			| automated
| | View Project | Imported 1

| | Verify the current layout has fields with the following values | 
| | ... | Name			| Imported 1 
| | ... | Type			| Legal Hold 
| | ... | Owner			| Fred Bob
| | ... | Owner Email	| fb@law.com
| | ... | Description	| First
| | ... | Subject Matter Start Date	| 1/1/2016
| | ... | Subject Matter End Date	| 2/2/2017


*** Keywords *** 

| Create Suite Setup Data
| | [Documentation]	| Create the Suite Preconditions
| | Setup connection to the relativity database
| | Open and Log in to Legal Hold
| | Setup connection to the Relativity Services API for Legal Hold	| ${WorkspaceName}

#------------------------------------------------------------------------------------------------------------------------

| | ${ProjectExists}=	| Determine if the Following Project Exists in the current Workspace | Imported 1
| | Run Keyword If 	| ${${ProjectExists}}	| Delete the Following Project using API | Imported 1

#------------------------------------------------------------------------------------------------------------------------

| | ${IntegrationPointExists}=	| Determine if the Following Integration Point Exists in the current Workspace	| ${IntegrationName}
| | Run Keyword If	| ${${IntegrationPointExists}}	| Delete the following Integration Point 	| ${IntegrationName}

| | @{Step1Fields}=	| Create List	| 
| | ... | ${IntegrationName}
| | ... | FTP (CSV File)
| | ... | Project
| | ... | No

| | @{Step2Fields}=	| Create List	|
| | ... | filetransfer.kcura.com
| | ... | FTP
| | ... | 21
| | ... | admin
| | ... | C@m3l!ts
| | ... | /Custom Development/ftp/LegalHold/ProjectsFTP.csv

| | @{Step3Mapping}=	| Create List	|
| | ... | Project Description		| Description
| | ... | Name [Object Identifier]	| Name
| | ... | Owner Email				| Owner Email
| | ... | Project Owner				| Owner
| | ... | Type						| Type
| | ... | Subject Matter Start Date	| Start Date
| | ... | Subject Matter End Date	| End Date
| | ... | Favorite					| Favorite
| | ... | Use as Template			| Template

| | Create FTP Integration Point	| ${Step1Fields}	| ${Step2Fields}	| ${Step3Mapping}	| Append Only	| Name [Object Identifier]

| | Run Import Now on the Following Integration Point	| ${IntegrationName}
| | [Teardown]		| Drop connection to the relativity services api

| Delete the Objects created in the Suite then Close All Browsers
| | [Documentation]	| Delete the Suite test data of Integration Point and Projects.
| | Setup connection to the Relativity Services API for Legal Hold	| ${WorkspaceName}

| | Delete the following Integration Point | ${IntegrationName}

| | Delete the Following Project using API | Imported 1

| | Drop connection to the relativity services api
| | Close All Browsers


*** Settings ***
| Library	| Collections
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${KEYWORD_DIR}/LegalHold/Legal_Hold_Keywords_API.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/MiscLegalHoldKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/DatabaseKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/GenericKeywords.txt