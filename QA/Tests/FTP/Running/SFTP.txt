
*** Settings ***
| Documentation 	| Verify that running an Append SFTP import creates new objects, but does not update existing objects.
| Metadata			| Authors	| Daniel Oliver
| Force Tags		| in-progress	| team.game_of_holds	| feature.integration_points
| Suite Setup		| Run Keywords	| Setup connection to the relativity database	| Open and Log in to Legal Hold
| Test Teardown 	| Run Keyword if Test Failed	| Capture Page Screenshot
| Suite Teardown	| Delete the Objects created in the Suite then Close All Browsers


*** Variables ***

| ${WorkspaceName}		| LDAP Sync
| ${IntegrationName}	| SFTP


*** Test Cases ***

| An SFTP Import Can be Created
| | [Documentation] | Verify that an Import can be created for a file on an SFTP Server
| | ...	| [id] 04689530-31a0-11e6-87ca-989096e24b46
| | [Tags]			| untested | required
| | @{Step1Fields}=	| Create List	| 
| | ... | ${IntegrationName}
| | ... | FTP (CSV File)
| | ... | Custodian
| | ... | No
| | @{Step2Fields}=	| Create List	|
| | ... | kcurallc.smartfile.com
| | ... | SFTP
| | ... | 22
| | ... | mkhan@kcura.com
| | ... | C@m3l!ts
| | ... | /LegalHold/SFTP.csv
| | @{Step3Mapping}=	| Create List	|
| | ... | Department	| Department 
| | ... | Email			| Email		 
| | ... | First Name	| FirstName 
| | ... | Last Name		| LastName	 
| | ... | Manager       | Manager
| | ... | UniqueID		| objectguid

| | Create FTP Integration Point	| ${Step1Fields}	| ${Step2Fields}	| ${Step3Mapping}	| Append Only	| Email
| | Wait Until Keyword Succeeds	| 10s	| 2s	| Verify that the Following Integration Point Exists in the current Workspace	| ${IntegrationName}


| An SFTP Import can be Run
| | [Documentation] | Verify that an SFTP import can be run
| | ...	| [id] 35d248b0-31a4-11e6-8747-989096e24b46
| | [Tags] | untested
| | [setup] | Require Test Case | An SFTP Import Can be Created
| | Run Import Now on the Following Integration Point | ${IntegrationName}

| | Go to the Tab	| Custodians
| | View Custodian	| ManSFTP, Cat
| | Verify the current layout has fields with the following values | 
| | ... | First Name	| Cat
| | ... | Last Name		| ManSFTP
| | ... | Department	| SFTP
| | ... | Email			| cmansftp@gameofholdstesting.corp


*** Keywords *** 

| Delete the Objects created in the Suite then Close All Browsers
| | [Documentation]	| Delete the Suite test data of Integration Point and Custodians.
| | Setup connection to the Relativity Services API for Legal Hold	| ${WorkspaceName}

| | Delete the Custodians in the Following Department using API	| SFTP
| | Delete the following Integration Point  | ${IntegrationName}

| | Drop connection to the relativity services api
| | Close All Browsers


*** Settings ***
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${KEYWORD_DIR}/LegalHold/Legal_Hold_Keywords_API.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/MiscLegalHoldKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/GenericKeywords.txt