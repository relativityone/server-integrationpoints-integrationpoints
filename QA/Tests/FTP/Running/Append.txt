
*** Settings ***
| Documentation 	| Verify that running an Append FTP import creates new objects, but does not update existing objects.
| Metadata			| Authors	| Daniel Oliver
| Force Tags		| in-progress | team.game_of_holds	| feature.integration_points
| Suite Setup		| Create Suite Setup Data
| Test Teardown 	| Run Keyword if Test Failed	| Capture Page Screenshot
| Suite Teardown	| Delete the Objects created in the Suite then Close All Browsers


*** Variables ***

| ${WorkspaceName}				| LDAP Sync
| ${IntegrationName}			| Append FTP
| ${NewObject}					| ManFTP, Cat
| ${ExistingAndUpdatedObject}	| ManFTP, Dog
| ${ExistingNoChangeObject}		| ManFTP, Bird 
| ${Manager}					| ManFTP, Bigdog
| ${Custodians Grid}			| gbox_custodianGrid


*** Test Cases ***

| An Append import Creates new objects
| | [Documentation] | Verify that when an Append RIP is run which has new Source objects, that it creates new objects in the Target RDO.
| | ...	| [id] 5994fc0f-2e7b-11e6-b4a3-989096e24b46
| | [Tags]			| automated
| | Go to the Tab | Custodians
| | View Custodian | ${NewObject}	
| | Verify the current layout has fields with the following values | First Name | Cat | Last Name | ManFTP | Department | AppendFTP | Email | cmanftp@gameofholdstesting.corp

| An Append imports will Overlay Managers
| | [Documentation] | Verify that Append imports will Overlay Manager information by linking an Existing Custodians to a new Manager.
| | ...				| [id] e1208a00-2e7b-11e6-8fa2-989096e24b46
| | [Tags]			| automated
| | Go to the Tab | Custodians
| | View Custodian | ${ExistingAndUpdatedObject} 

| | Verify the current layout has fields with the following values | Manager | ${Manager}

| An Append import Creates new History objects
| | [Documentation] | Verify that when an Append RIP is run which has new source recods, 
| | ...				| that there are history entries for the newly created objects in the target RDO.
| | ...				| [id] 7f9c4930-2e7c-11e6-aa89-989096e24b46
| | [Tags]			| manual
| | Go to the Tab	| History

| | Run the following manual steps:
| | ... | Steps:
| | ... | 1. Change to the All History View
| | ... | 2. View the History objects for "${NewObject}"
| | ... |
| | ... | Expected Results:
| | ... | Verify that there is an entry for the Custodian "${NewObject}" with the Action of "Update - Import"

| An Append import does not update existing objects
| | [Documentation] | Verify that when an Append RIP is run which has updated Source objects that match to existing objects, 
| | ...				| that it does not update the existing objects in the target RDO.
| | ...				| [id] 83486140-2e7c-11e6-a9b4-989096e24b46
| | [Tags]			| manual
| | Go to the Tab	| History

| | Run the following manual steps:
| | ... | Steps:
| | ... | 1. Change to the All History View
| | ... | 2. View the History objects for "${ExistingAndUpdatedObject}"
| | ... |
| | ... | Expected Results:
| | ... | Verify that there is no entry for "${ExistingAndUpdatedObject}" with the Action of "Update - Import"

| An Append import does not update existing unmodified objects
| | [Documentation] | Verify that when an Append RIP is run which has Source objects that match to existing objects with no changes, 
| | ...				| that it does not update the existing objects in the target RDO.
| | ...				| [id] 87597cb0-2e7c-11e6-83f1-989096e24b46
| | [Tags]			| manual
| | Go to the Tab	| History

| | Run the following manual steps:
| | ... | Steps:
| | ... | 1. Change to the All History View
| | ... | 2. View the History objects for "${ExistingNoChangeObject}"
| | ... |
| | ... | Expected Results:
| | ... | Verify that there is no entry for "${ExistingNoChangeObject}" with the Action of "Update - Import"

| An Append import's Job History does not fail when there are Existing Objects
| | [Documentation] | Verify that when an Append RIP is run which has Source objects that match to existing objects,
| | ...				| that the Import's Job History has not errors.
| | ...				| [id] 8dd7c791-2e7c-11e6-b1f6-989096e24b46
| | [Tags]			| manual
| | Run the following manual steps:
| | ... | Steps:
| | ... | Go to the Tab: Job History
| | ... | 
| | ... | Expected Results:
| | ... | Verify that the newest entry for "${IntegrationName}" has no Errors


*** Keywords *** 

| Create Suite Setup Data
| | [Documentation]	| Create the Suite Preconditions
| | Setup connection to the relativity database
| | Open and Log in to Legal Hold
| | Setup connection to the Relativity Services API for Legal Hold	| ${WorkspaceName}

# --------------------------------------------------------------------------------------------------------

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${ExistingNoChangeObject}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${ExistingNoChangeObject}
| | Create a Custodian using API	| 
| | ... | Full Name		| ${ExistingNoChangeObject}
| | ... | Email			| BirdManFTP@gameofholdstesting.corp
| | ... | Department	| AppendFTP

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${ExistingAndUpdatedObject}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${ExistingAndUpdatedObject}
| | Create a Custodian using API	| 
| | ... | Full Name		| ${ExistingAndUpdatedObject}		
| | ... | Email			| DogManFTP@gameofholdstesting.corp
| | ... | Department	| AppendFTP

# --------------------------------------------------------------------------------------------------------

| | @{Step1Fields}=	| Create list | ${IntegrationName} | FTP (CSV File) | Custodian | No
| | @{Step2Fields}=	| Create list | filetransfer.kcura.com | FTP - File Transfer Protocol | 21 | admin | C@m3l!ts | /Custom Development/ftp/LegalHold/AppendFTP.csv

| | @{Step3Mapping}=	| Create List	|
| | ... | Department	| Department 
| | ... | Email			| Email		 
| | ... | First Name	| FirstName 
| | ... | Last Name		| LastName	 
| | ... | Manager       | Manager
| | ... | UniqueID		| objectguid
| | Create LDAP Integration Point | ${Step1Fields} | ${Step2Fields} | ${Step3Mapping} | Append Only | Email

| | Run Import Now on the Following Integration Point	| ${IntegrationName}
| | [Teardown]		| Run Keywords | Drop connection to the relativity services api | Capture Page Screenshot

| Delete the Objects created in the Suite then Close All Browsers
| | [Documentation]	| Delete the Suite test data of Integration Point and Custodians.
| | Setup connection to the Relativity Services API for Legal Hold	| ${WorkspaceName}

| | Delete the Custodians in the Following Department using API	| AppendFTP
| | Delete the following Integration Point using API | ${IntegrationName}

| | Drop connection to the relativity services api
| | Close All Browsers


*** Settings ***
| Library	| Collections
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${KEYWORD_DIR}/LegalHold/Legal_Hold_Keywords_API.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/GridKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/MiscLegalHoldKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/IntegrationPointKeywords.txt