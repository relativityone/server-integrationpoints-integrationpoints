
*** Settings ***
| Documentation		| Verify that running an Append/Overlay for FTP files creates new objects, updates existing modified objects and ignores existing unmodified objects. 
| Metadata			| Authors	| Daniel Oliver
| Force Tags		| in-progress	| team.game_of_holds	| feature.integration_points
| Suite Setup		| Create Suite Setup Data
| Test Teardown		| Run Keyword if Test Failed	| Capture Page Screenshot
| Suite Teardown	| Delete the Objects created in the Suite then Close All Browsers


*** Variables ***

| ${WorkspaceName}				| LDAP Sync
| ${IntegrationName}			| Append and Overlay FTP
| ${NewObject}					| SunFTP, Set
| ${ExistingAndUpdatedObject}	| SunFTP, Midday
| ${ExistingNoChangeObject}		| SunFTP, Rise


*** Test Cases ***

| An Append and Overlay import Creates new records
| | [Documentation] | Verify that when an Append and Overlay RIP is run which has new Source records, that it creates new records in the Target RDO.
| | ...				| [id] 5b2347d1-2db8-11e6-bce7-989096e24b46
| | [Tags]			| automated	| rewrite_me
| | View Custodian	| ${NewObject}
| | Verify the current layout has fields with the following values	| 
| | ... | First Name	| Set
| | ... | Last Name		| SunFTP
| | ... | Department	| AppendAndOverlayFTP
| | ... | Email			| ssunftp@gameofholdstesting.corp


| An Append and Overlay import updates existing modified records
| | [Documentation] | Verify that when an Append and Overlay RIP is run with an existing record in the Target RDO with updated details in the Source, 
| | ...				| that it does update the existing records in the Target RDO. In this test, the Custodian's Department has been updated.
| | ...				| [id] 5d8f3ec0-2db8-11e6-a67c-989096e24b46
| | [Tags]			| automated	| rewrite_me
| | View Custodian	| ${ExistingAndUpdatedObject}
| | Verify the current layout has fields with the following values	| # If it didn't update then Department will be wrong
| | ... | First Name	| Midday
| | ... | Last Name		| SunFTP
| | ... | Department	| AppendAndOverlayFTP
| | ... | Email			| msunftp@gameofholdstesting.corp


| An Append and Overlay import does update existing unmodified records
| | [Documentation] | Verify that when an Append and Overlay RIP is run with an existing record in the Target RDO with identical details in the Source, 
| | ...				| that it does update the existing record in the Target RDO.
| | ...				| [id] 606ff080-2db8-11e6-8f57-989096e24b46
| | [Tags]			| manual
| | Go to the Tab	| Custodians

| | Run the following manual steps:
| | ... | Steps:
| | ... | 1. View Custodian: ${ExistingNoChangeObject} (If multiple exist, this suite fails)
| | ... | 2. Click: View Audit
| | ... |
| | ... | Expected Results:
| | ... | Verify that the most recent action is "Update - Import"	| # Exact Match?


*** Keywords *** 

| Create Suite Setup Data
| | [Documentation]	| Create the Suite Preconditions
| | Setup connection to the relativity database
| | Open and Log in to Legal Hold
| | Setup connection to the Relativity Services API for Legal Hold	| ${WorkspaceName}

# --------------------------------------------------------------------------------------------------------

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${NewObject}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${NewObject}

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${ExistingNoChangeObject}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${ExistingNoChangeObject}
| | Create a Custodian using API	| 
| | ... | Full Name		| ${ExistingNoChangeObject}
| | ... | Email			| rsunftp@gameofholdstesting.corp
| | ... | Department	| AppendAndOverlayFTP
| | ... | UniqueID		| 1a1e7445-cab1-428c-a1ee-ba502c96a6d0

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${ExistingAndUpdatedObject}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${ExistingAndUpdatedObject}
| | Create a Custodian using API	| 
| | ... | Full Name		| ${ExistingAndUpdatedObject}
| | ... | Email			| msunftp@gameofholdstesting.corp
| | ... | Department	| AppendAndOverlayFT
| | ... | UniqueID		| ab381d6d-d29d-4058-a989-a0ea7f10707f

# --------------------------------------------------------------------------------------------------------

#| | ${IntegrationPointExists}=	| Determine if the Following Integration Point Exists in the current Workspace	| ${IntegrationName}
#| | Run Keyword If	| ${${IntegrationPointExists}}	| Delete the following Integration Point	| ${IntegrationName}
#
#| | @{Step1Fields}=	| Create List	| 
#| | ... | ${IntegrationName}
#| | ... | FTP (CSV File)
#| | ... | Custodian
#| | ... | No
#
#| | @{Step2Fields}=	| Create List	| 
#| | ... | filetransfer.kcura.com
#| | ... | FTP
#| | ... | 21
#| | ... | admin
#| | ... | C@m3l!ts
#| | ... | /Custom Development/ftp/LegalHold/AppendOverlayFTP.csv
#
#| | @{Step3Mapping}=	| Create List	|
#| | ... | Department	| Department
#| | ... | Email			| Email
#| | ... | First Name	| FirstName
#| | ... | Last Name		| LastName
#| | ... | UniqueID		| unique_id
#
#| | Create FTP Integration Point	| ${Step1Fields}	| ${Step2Fields}	| ${Step3Mapping}	| Append/Overlay	| Email

| | Run Import Now on the Following Integration Point	| ${IntegrationName}
| | [Teardown]		| Run Keywords	| Drop connection to the relativity services api	| Capture Page Screenshot

| Delete the Objects created in the Suite then Close All Browsers
| | [Documentation]	| Delete the Suite test data of Integration Point and Custodians.
| | Setup connection to the Relativity Services API for Legal Hold	| ${WorkspaceName}

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${NewObject}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${NewObject}

| | Delete the following Custodian using API	| ${ExistingNoChangeObject}
| | Delete the following Custodian using API	| ${ExistingAndUpdatedObject}

#| | Delete the following Integration Point	| ${IntegrationName}

| | Drop connection to the relativity services api
| | Close All Browsers


*** Settings ***
| Library	| Collections
| Library	| ${KEYWORD_DIR}/ManualKeywords.py
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${KEYWORD_DIR}/LegalHold/Legal_Hold_Keywords_API.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/MiscLegalHoldKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/GenericKeywords.txt