
*** Settings ***
| Documentation 	| Verify that running an Append/Overlay for FTP files creates new objects, updates existing modified objects and ignores existing unmodified objects. 
| Metadata			| Authors | Daniel Oliver
| Force Tags		| in-progress	| team.game_of_holds | feature.integration_points | untested
| Suite Setup		| Create Suite Setup Data
| Test Teardown 	| Run Keyword if Test Failed	| Capture Page Screenshot
| Suite Teardown	| Delete the Objects created in the Suite then Close All Browsers


*** Variables ***

| ${WorkspaceName}				| LDAP Sync
| ${IntegrationName}			| Append and Overlay
| ${NewObject}					| Sun, Early
| ${ExistingAndUpdatedObject}	| Sun, Midday
| ${ExistingNoChangeObject}		| Sun, Rise 
| ${Custodians Grid}	| gbox_custodianGrid


*** Test Cases ***

| An Append and Overlay import Creates new records
| | [Documentation] | Verify that when an Append and Overlay RIP is run which has new Source records, that it creates new records in the Target RDO.
| | ...				| [id] 5b2347d1-2db8-11e6-bce7-989096e24b46
| | [Tags]			| automated
| | Go to the Tab | Custodian
| | Filter the following Grid on the following Columns by the following Values | ${Custodians Grid} | 
| | ... | Name			| ${NewObject}	| 
| | ... | Department	| AppendAndOverlay

| | Verify that the following Grid contains the following number of Rows | ${Custodians Grid} | 1


| An Append and Overlay import updates existing modified records
| | [Documentation] | Verify that when an Append and Overlay RIP is run with an existing record in the Target RDO with updated details in the Source, 
| | ...				| that it does update the existing records in the Target RDO. In this test, the Custodian's Department has been updated.
| | ...				| [id] 5d8f3ec0-2db8-11e6-a67c-989096e24b46
| | [Tags]			| automated
| | Go to the Tab | Custodian
| | Filter the following Grid on the following Columns by the following Values | ${Custodians Grid} | 
| | ... | Name			| ${ExistingAndUpdatedObject}	| 
| | ... | Department	| AppendAndOverlay

| | Verify that the following Grid contains the following number of Rows | ${Custodians Grid} | 1


| An Append and Overlay import does not update existing unmodified records
| | [Documentation] | Verify that when an Append and Overlay RIP is run with an existing record in the Target RDO with identical details in the Source, 
| | ...				| that it does not update the existing record in the Target RDO.
| | ...				| [id] 606ff080-2db8-11e6-8f57-989096e24b46
| | [Tags]			| manual
| | [Setup]			| Require Test Case	| An Append and Overlay import Creates new records
| | Go to the Tab	| History

| | Run the following manual steps:
| | ... | Steps:
| | ... | 1. Change to the All History View
| | ... | 2. View the History records for the "${ExistingNoChangeObject}" Custodian
| | ... |
| | ... | Expected Results:
| | ... | Verify that the "Update - Import" entry for the Custodian "${ExistingNoChangeObject}" does not modify the data to different values (it just updates to the existing values), except for uniquid which is changed from blank to a value.


*** Keywords *** 

| Create Suite Setup Data
| | [Documentation]	| Create the Suite Preconditions
| | Setup connection to the relativity database
| | Open and Log in to Method
| | Setup connection to the Relativity Services API for Legal Hold	| ${WorkspaceName}

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${ExistingNoChangeObject}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${ExistingNoChangeObject}
| | Create a Custodian using API	| 
| | ... | Full Name		| ${ExistingNoChangeObject}		|
| | ... | Email			| rsun@gameofholdstesting.corp	|
| | ... | Department	| AppendAndOverlay

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${ExistingAndUpdatedObject}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${ExistingAndUpdatedObject}
| | Create a Custodian using API	| 
| | ... | Full Name		| ${ExistingAndUpdatedObject}	|
| | ... | Email			| msun@gameofholdstesting.corp	|
| | ... | Department	| AppendAndOverla

| | ${IntegrationPointExists}=	| Determine if the Following Integration Point Exists in the current Workspace	| ${IntegrationName}
| | Run Keyword If	| ${${IntegrationPointExists}}	| Delete the following Integration Point using API 	| ${IntegrationName}

| | @{Step1Fields}=		| Create list | 
| | ... | ${IntegrationName} | Append/Overlay | LDAP | Custodian | No
| | @{Step2Fields}=		| Create list | gameofholds.testing.corp/CN=Users,DC=gameofholds,DC=testing,DC=corp | 
| | ... | (department=AppendAndOverlay) | FastBind | testing\\rellockdown | P@ssw0rd@1
| | @{Step3Mapping}=	| Create List	|
| | ... | Email			| mail			| 
| | ... | Department	| department	| 
| | ... | First Name	| givenname		| 
| | ... | Last Name		| sn			| 
| | ... | UniqueID		| objectguid
| | Create LDAP Integration Point | ${Step1Fields} | ${Step2Fields} | ${Step3Mapping} | Email

| | Run Import Now on the Following Integration Point	| ${IntegrationName}
| | [Teardown]		| Drop connection to the relativity services api

| Delete the Objects created in the Suite then Close All Browsers
| | [Documentation]	| Delete the Suite test data of Integration Point and Custodians.
| | Setup connection to the Relativity Services API for Legal Hold	| ${WorkspaceName}

| | Delete the following Integration Point using API | ${IntegrationName}
| | Delete the Custodians in the Following Department using API	| AppendAndOverlay

| | Drop connection to the relativity services api
| | Close All Browsers


*** Settings ***
| Library	| Collections
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${KEYWORD_DIR}/LegalHold/Legal_Hold_Keywords_API.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/GridKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/MiscMethodKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/IntegrationPointKeywords.txt