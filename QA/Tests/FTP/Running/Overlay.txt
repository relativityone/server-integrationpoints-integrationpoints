
*** Settings ***
| Documentation		| Verify that running an Overlay FTP import updates existing records but does not creates new records.
| Metadata			| Authors	| Daniel Oliver	| William Sehy
| Force Tags		| in-progress	| team.game_of_holds	| feature.integration_points
| Suite Setup		| Create Suite Setup Data
| Test Teardown		| Run Keyword if Test Failed	| Capture Page Screenshot
| Suite Teardown	| Delete the Objects created in the Suite then Close All Browsers


*** Variables ***

| ${WorkspaceName}				| LDAP Sync
| ${IntegrationName}			| OverlayFTP
| ${ExistingAndUpdatedObject}	| TimeFTP, Night
| ${NewObject}					| TimeFTP, Sleep
| ${ExistingNoChangeObject}		| TimeFTP, Day
| ${ExistingObjectNulled}		| TimeFTP, Lunch


*** Test Cases ***

| An Overlay import updates existing modified records
| | [Documentation]	| Verify that when an Overlay FTP Impoty is run with an existing record in the Target RDO with updated details in the Source, 
| | ...				| that it does update the existing records in the Target RDO. In this test, the Custodian's Department is updated.
| | ...				| [id] 512b9b4f-2e81-11e6-ac4c-989096e24b46
| | [Tags]			| automated
| | View Custodian	| ${ExistingAndUpdatedObject}
| | Verify the current layout has fields with the following values	| 
| | ... | First Name	| Night
| | ... | Last Name		| TimeFTP
| | ... | Department	| TimeFTP
| | ... | Email			| ntimeFTP@gameofholdstesting.corp 


| An Overlay import does not Create new records
| | [Documentation]	| Verify that when an Overlay FTP Impotrt is run with an existing record in the Target RDO with identical details in the Source, 
| | ...				| that it does not update the existing record in the Target RDO.
| | ...				| [id] 5515a6c0-2e81-11e6-8d3f-989096e24b46
| | [Tags]			| automated
| | Verify that the Following Custodian does not Exist in the current Workspace	| ${NewObject}


| Updates a Value to Null
| | [Documentation]	| Verify that when an Overlay RIP is run with an existing record in the Target RDO with one field popuplated, 
| | ...				| that it does update the existing record in the Target RDO to Null.
| | ...				| [id] 58b25580-2e81-11e6-be4e-989096e24b46
| | [Tags]			| automated
| | View Custodian	| ${ExistingObjectNulled}	
| | Verify the current layout has fields with the following values	| 
| | ... | First Name	| Lunch
| | ... | Last Name		| TimeFTP
| | ... | Department	| ${EMPTY}
| | ... | Email			| ltimeFTP@gameofholdstesting.corp


| Does not Update when there are no Changes
| | [Documentation]	| Verify that when an Overlay RIP is run with an existing record in the Target RDO with identical details in the Source, 
| | ...				| that it does not update the existing record in the Target RDO.
| | ...				| [id] 6081d880-2e81-11e6-aec0-989096e24b46
| | [Tags]			| manual
| | Go to the Tab	| Custodians

| | Run the following manual steps:
| | ... | Steps:
| | ... | 1. View Custodian: ${ExistingNoChangeObject} (If more than one is present this test Fails)
| | ... | 2. Click: View Audit
| | ... |
| | ... | Expected Results:
| | ... | Verify that most recent action was "Update - Import"


*** Keywords *** 

| Create Suite Setup Data
| | [Documentation]	| Create the Suite Preconditions
| | Setup connection to the relativity database
| | Open and Log in to Legal Hold
| | Setup connection to the Relativity Services API for Legal Hold	| ${WorkspaceName}

#------------------------------------------------------------------------------------------------------------------------

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${NewObject}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${NewObject}

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${ExistingAndUpdatedObject}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${ExistingAndUpdatedObject}
| | Create a Custodian using API	| 
| | ... | Full Name		| ${ExistingAndUpdatedObject}
| | ... | Email			| ntimeFTP@gameofholdstesting.corp
| | ... | UniqueID		| 1a1e7445-cab1-428c-a1ee-ba502c96a6d4

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${ExistingNoChangeObject}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${ExistingNoChangeObject}
| | Create a Custodian using API	| 
| | ... | Full Name		| ${ExistingNoChangeObject}
| | ... | Email			| dtimeFTP@gameofholdstesting.corp
| | ... | Department	| TimeFTP
| | ... | UniqueID		| ab381d6d-d29d-4058-a989-a0ea7f1070zz

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${ExistingObjectNulled}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${ExistingObjectNulled}
| | Create a Custodian using API	| 
| | ... | Full Name		| ${ExistingObjectNulled}
| | ... | Email			| ltimeFTP@gameofholdstesting.corp
| | ... | Department	| TimeFTP
| | ... | UniqueID		| ab381d6d-d29d-4058-a989-a0ea7f10707t

#------------------------------------------------------------------------------------------------------------------------

| | ${IntegrationPointExists}=	| Determine if the Following Integration Point Exists in the current Workspace	| ${IntegrationName}
| | Run Keyword If	| ${${IntegrationPointExists}}	| Delete the following Integration Point 	| ${IntegrationName}

| | @{Step1Fields}=	| Create List	| 
| | ... | ${IntegrationName}
| | ... | FTP (CSV File)
| | ... | Custodian
| | ... | No

| | @{Step2Fields}=	| Create List	| 
| | ... | files.kcura.com
| | ... | FTP
| | ... | 21
| | ... | rip
| | ... | GameOfHolds
| | ... | /ftp/LegalHold/OverlayFTP.csv

| | @{Step3Mapping}=	| Create List	|
| | ... | Email			| Email
| | ... | Department	| Department
| | ... | First Name	| FirstName
| | ... | Last Name		| LastName
| | ... | UniqueID		| unique_id

| | Create FTP Integration Point	| ${Step1Fields}	| ${Step2Fields}	| ${Step3Mapping}	| Overlay Only	| Email

| | Run Import Now on the Following Integration Point	| ${IntegrationName}
| | [Teardown]		| Run Keywords	| Drop connection to the relativity services api	| Capture Page Screenshot

| Delete the Objects created in the Suite then Close All Browsers
| | [Documentation]	| Delete the Suite test data of Integration Point and Custodians.
| | Setup connection to the Relativity Services API for Legal Hold	| ${WorkspaceName}

| | ${CustodianExists}=	| Determine if the Following Custodian Exists in the current Workspace	| ${NewObject}
| | Run Keyword If	| ${${CustodianExists}}	| Delete the following Custodian using API	| ${NewObject}

| | Delete the following Custodian using API	| ${ExistingAndUpdatedObject}
| | Delete the following Custodian using API	| ${ExistingObjectNulled}
| | Delete the following Custodian using API	| ${ExistingObjectNulled}

| | Delete the following Integration Point	| ${IntegrationName}

| | Drop connection to the relativity services api
| | Close All Browsers


*** Settings ***
| Library	| Collections
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${KEYWORD_DIR}/LegalHold/Legal_Hold_Keywords_API.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/MiscLegalHoldKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/GenericKeywords.txt