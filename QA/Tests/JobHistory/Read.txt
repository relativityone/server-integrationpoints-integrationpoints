
*** Settings ***
| Documentation 	| Verify that the Read pages for Job History objects have the fields and sections for given situations.
| Metadata			| Authors	| Daniel Oliver	| William Sehy
| Force Tags		| in-progress	| team.method
| Suite Setup		| Suite Setup Keyword
| Test Setup		| View Job History	| ${JobHistory1}
| Test Teardown 	| Capture Screenshot if Test Fails
| Suite Teardown	| Delete the Objects created in the Suite then Close All Browsers


*** Variables ***

| ${WorkspaceName}		| LDAP Sync
| ${JobHistory1}		| Integration Point 1
| ${JobHistory2}		| Integration Point 2
| ${ConnectionPath}		| testing.corp/CN=Users,DC=testing,DC=corp
| ${ObjectFilterString}	| (objectClass=*)
| ${Authentication}		| FastBind
| ${ConnectionUsername}	| testing\\administrator
| ${ConnectionPassword}	| P@ssw0rd@1
| ${ImportNestedItems}	| Yes


*** Test Cases ***

| The Job History Read page defaults to the correct Layout
| | [Documentation]	| Verify that the default Layout on a Job History Read page is "Job Details". 
| | ...				| 
| | [Tags]			| untested
| | Verify that the layout dropdown is set to "Job Details"


| The Job History Read page has the correct Layouts
| | [Documentation]	| Verify that the Job History Read page has only the "Job Details" Layout. 
| | ...				| 
| | [Tags]			| untested
| | Verify that the Layout Dropdown only has the following Layouts	| Job Details


| The correct Buttons appear in the Header
| | [Documentation]	| Verify that the correct buttons appear at the top of the page.
| | [Tags]			| untested
| | Verify the buttons are available on the layout	| 
| | ... | Edit				| 
| | ... | Delete			| 
| | ... | Back				| 
| | ... | Edit Permissions	| 
| | ... | View Audit


| The Category Headings are in the correct order
| | [Documentation]	| Verify that the Category headings are sorted correctly on the Integration Point Details" Layout.
| | [Tags]			| untested
| | Verify that the current Layout has the following Categories in Order	| 
| | ... | Job Details		| 
| | ... | Import Statistics	| 
| | ... | Records with Errors


| The correct Fields are present on the Job Details Layout
| | [Documentation]	| Verify that the correct Fields are present on the "Job Details" Layout.
| | [Tags]			| manual	| field_placement
| | Run the following manual steps:
| | ... | Expected Results:
| | ... | 1. Verify that the Job Details Category has the following fields in order:	| 
| | ... | - Integration Point	| 
| | ... | - Status				| 
| | ... | - Job Error			| 
| | ... | - Start Time (UTC)	| 
| | ... | - End Time (UTC)		|
| | ... | 2. Verify that the Import Statistics Category has the following fields in order:	| 
| | ... | - Records Imported	| 
| | ... | - Records with Errors


| The Columns on the Records With Errors table are correct
| | [Documentation] | Verifies that the Records With Errors table on the Job History View page has the correct columns.
| | [Tags]			| untested
| | Verify that the item list contains the following fields	| 
| | ... | Source Unique ID	| 
| | ... | Error				| 
| | ... | Timestamp (UTC)


| The Sorting on the Records With Errors table is correct
| | [Documentation] | Verifies that the Records With Errors table on the Job History View page has the correct sorting.
| | [Tags]			| untested
| | Verify that the Item List is sorted by the following Date Column in Ascending Order	| Timestamp (UTC)


*** Keywords ***

| Verify that the current Layout has the following Categories in Order
| | [Documentation]	| This keyword verifies that the given Categories are visible on the current Layout in the given order.
| | [ARGUMENTS]		| @{ExpectedCategories}
| | Element Should Be Visible	| id=_dynamicTemplate__kCuraScrollingDiv__dynamicViewFieldRenderer_span	| No Categories were found on the current Layout.
| | @{ActualCategories}=	| Get Element Text	| id=_dynamicTemplate__kCuraScrollingDiv__dynamicViewFieldRenderer_span	| False
| | Log	| Expected Categories: ${ExpectedCategories}
| | Log	| Actual Categories: ${ActualCategories}
| | Lists Should Be Equal	| ${ExpectedCategories}	| ${ActualCategories}

| Verify that the Layout Dropdown only has the following Layouts
| | [Documentation]	| This keyword verifies that the Layout dropdown has only the given Layouts in any order.
| | ...				| This keyword assumes that the Layout dropdown's name is "_dynamicTemplate$ctl03".
| | [ARGUMENTS]		| @{ExpectedLayouts}
| | Element Should Be Visible	| css=select[name='_dynamicTemplate$ctl03']	| The Layout dropdown was not found.
| | @{ActualLayouts}=	| Get Element Text	| css=select[name='_dynamicTemplate$ctl03'] > option	| False
| | Sort List	| ${ExpectedLayouts}
| | Sort List	| ${ActualLayouts}
| | Lists Should Be Equal	| ${ExpectedLayouts}	| ${ActualLayouts}

| Suite Setup Keyword
| | [Documentation]	| Create the Suite Preconditions
| | Setup connection to the relativity database
| | Open and Log in to Method
| | Setup connection to the Relativity Services API for Method	| ${WorkspaceName}
| | Enable Strict Mode for Layouts	| # Makes 'Verify the current layout has the following fields' ensure that only the given fields are present

# --------------------------------------------------------------------------------------------------------

| | @{Step1Fields}=		| Create List	| 
| | ... | ${JobHistory1}	| 
| | ... | Append/Overlay		| 
| | ... | LDAP					| 
| | ... | Custodian				| 
| | ... | No

| | @{Step2Fields}=		| Create List	| 
| | ... | ${ConnectionPath}		| 
| | ... | ${ObjectFilterString}	| 
| | ... | ${Authentication}		| 
| | ... | ${ConnectionUsername}	| 
| | ... | ${ConnectionPassword}	| 
| | ... | ${ImportNestedItems}

| | @{Step3Mapping}=	| Create List	| 
| | ... | Email			| mail			| 
| | ... | Department	| countrycode	| 
| | ... | First Name	| givenname		| 
| | ... | Last Name		| sn			| 
| | ... | UniqueID		| objectguid

| | ${JobHistoryExists}=	| Determine if the Following Integration Point Exists in the current Workspace	| Job History Testing
| | Run Keyword If	| ${${JobHistoryExists}}	| Delete the following Integration Point using API	| Job History Testing
| | Create Integration Point | ${Step1Fields} | ${Step2Fields} | ${Step3Mapping} | UniqueID

| | Run Import Now on the Following Integration Point	| Job History Testing
| | [Teardown]		| Drop connection to the relativity services api

| Delete the Objects created in the Suite then Close All Browsers
| | [Documentation]	| Delete the Integration Point created in this suite then closes all open browsers.
| | ...				| This is run as the Suite Teardown.
| | Setup connection to the Relativity Services API for Method	| ${WorkspaceName}

| | Delete the following Integration Point using API	| Job History Testing

| | Drop connection to the relativity services api
| | Close All Browsers


*** Settings ***
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${KEYWORD_DIR}/Relativity/Processing/Processing/Relativity_Keywords_General.txt
| Resource	| ${KEYWORD_DIR}/Relativity/Shared/Layouts/LayoutKeywords.txt
| Resource	| ${KEYWORD_DIR}/Relativity/Shared/RelativityKeywords.txt
| Resource	| ${KEYWORD_DIR}/Relativity/Shared/Views/ItemListKeywords.txt
| Resource	| ${METHOD_KEYWORD_DIR}/MiscMethodKeywords.txt
| Resource	| ${METHOD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/DatabaseKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/IntegrationPointKeywords.txt
