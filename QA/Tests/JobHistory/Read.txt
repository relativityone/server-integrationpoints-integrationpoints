
*** Settings ***
| Documentation 	| Verify that the Read pages for Job History objects have the fields and sections for given situations.
| Metadata			| Authors	| Daniel Oliver	| William Sehy
| Force Tags		| in-progress	| team.game_of_holds	| feature.integration_points
| Suite Setup		| Suite Setup Keyword
| Test Setup		| View Job History	| ${IntegrationPoint1}
| Test Teardown 	| Capture Screenshot if Test Fails
| Suite Teardown	| Delete the Objects created in the Suite then Close All Browsers


*** Variables ***

| ${WorkspaceName}		| LDAP Sync
| ${IntegrationPoint1}	| Integration Point 1
| ${ConnectionPath}		| testing.corp/CN=Users,DC=testing,DC=corp
| ${ObjectFilterString}	| (objectClass=*)
| ${Authentication}		| FastBind
| ${ConnectionUsername}	| testing\\administrator
| ${ConnectionPassword}	| P@ssw0rd@1
| ${ImportNestedItems}	| Yes


*** Test Cases ***

| The Job History Read page defaults to the correct Layout
| | [Documentation]	| Verify that the default Layout on a Job History Read page is "Job Details". 
| | [Tags]			| automated
| | Verify that the layout dropdown is set to "Job Details"


| The Job History Read page has the correct Layouts
| | [Documentation]	| Verify that the Job History Read page has only the "Job Details" Layout. 
| | [Tags]			| automated
| | Verify that the Layout Dropdown only has the following Layouts	| Job Details


| The correct Buttons appear in the Header
| | [Documentation]	| Verify that the correct buttons appear at the top of the page.
| | [Tags]			| automated
| | Verify the buttons are available on the layout	| 
| | ... | Edit				| 
| | ... | Delete			| 
| | ... | Back				| 
| | ... | Edit Permissions	| 
| | ... | View Audit


| The Category Headings are in the correct order
| | [Documentation]	| Verify that the Category headings are sorted correctly on the "Job Details" Layout.
| | [Tags]			| automated
| | Verify that the current Layout has the following Categories in Order	| 
| | ... | Job Details		| 
| | ... | Import Statistics 


| The correct Fields are present on the Job Details Layout
| | [Documentation]	| Verify that the correct Fields are present on the "Job Details" Layout.
| | [Tags]			| manual	| field_placement
| | Run the following manual steps:
| | ... | Expected Results:
| | ... | 1. Verify that the Job Details Category has the following fields in order:	| 
| | ... | - Name
| | ... | - Integration Point	| 
| | ... | - Status				| 
| | ... | - Start Time (UTC)	| 
| | ... | - End Time (UTC)		|
| | ... | 2. Verify that the Import Statistics Category has the following fields in order:	| 
| | ... | - Records Imported	| 
| | ... | - Records with Errors


| The Table has the Title Job History Error
| | [Documentation]	| Verify that the Table on the Job Details layout has the title "Job History Error".
| | [Tags]			| manual	| automateable?
| | Run the following manual steps:
| | ... | Expected Results:
| | ... | Verify that the table has the title "Job History Error"


| The Columns on the Job History Error table are correct
| | [Documentation] | Verifies that the Job History Error table on the Job History View page has the correct columns.
| | [Tags]			| manual	| automateable?
| | Run the following manual steps:
| | ... | Expected Results:
| | ... | Verify that the item list contains the following fields	| 
| | ... | Source Unique ID	| 
| | ... | Error				| 
| | ... | Timestamp (UTC)	| 
| | ... | Error Type


| The Sorting on the Job History Error table is correct
| | [Documentation] | Verifies that the Job History Error table on the Job History View page has the correct sorting.
| | [Tags]			| manual	| 
| | Run the following manual steps:
| | ... | Preconditions:
| | ... | 1. At least three records appear in the ItemList
| | ... |
| | ... | Expected Results:
| | ... | Verify that the Item List is sorted by the following Date Column in Ascending Order	| Timestamp (UTC)


| The Start and End Time Fields display in UTC
| | [Documentation] | Verifies that the Start and End Time Fields are calculated using UTC.
| | [Tags]			| manual	| time
| | Run the following manual steps:
| | ... | Expected Results:
| | ... | Verify that the Start and End Time fields are displayed in UTC time.


*** Keywords ***

| Verify that the current Layout has the following Categories in Order
| | [Documentation]	| This keyword verifies that the given Categories are visible on the current Layout in the given order.
| | [ARGUMENTS]		| @{ExpectedCategories}
| | Element Should Be Visible	| id=_dynamicTemplate__kCuraScrollingDiv__dynamicViewFieldRenderer_span	| No Categories were found on the current Layout.
| | @{ActualCategories}=	| Get Element Text	| id=_dynamicTemplate__kCuraScrollingDiv__dynamicViewFieldRenderer_span	| False
| | Log	| Expected Categories: ${ExpectedCategories}
| | Log	| Actual Categories: ${ActualCategories}
| | Lists Should Be Equal	| ${ExpectedCategories}	| ${ActualCategories}

| Verify that the Layout Dropdown only has the following Layouts
| | [Documentation]	| This keyword verifies that the Layout dropdown has only the given Layouts in any order.
| | ...				| This keyword assumes that the Layout dropdown's name is "_dynamicTemplate$ctl03".
| | [ARGUMENTS]		| @{ExpectedLayouts}
| | Element Should Be Visible	| css=select[name='_dynamicTemplate$ctl03']	| The Layout dropdown was not found.
| | @{ActualLayouts}=	| Get Element Text	| css=select[name='_dynamicTemplate$ctl03'] > option	| False
| | Sort List	| ${ExpectedLayouts}
| | Sort List	| ${ActualLayouts}
| | Lists Should Be Equal	| ${ExpectedLayouts}	| ${ActualLayouts}

| Suite Setup Keyword
| | [Documentation]	| Create the Suite Preconditions
| | Setup connection to the relativity database
| | Open and Log in to Method
| | Setup connection to the Relativity Services API for Legal Hold	| ${WorkspaceName}
| | Enable Strict Mode for Layouts	| # Makes 'Verify the current layout has the following fields' ensure that only the given fields are present

# --------------------------------------------------------------------------------------------------------

| | @{Step1Fields}=		| Create List	| 
| | ... | ${IntegrationPoint1}	| 
| | ... | Append/Overlay		| 
| | ... | LDAP					| 
| | ... | Custodian				| 
| | ... | No

| | @{Step2Fields}=		| Create List	| 
| | ... | ${ConnectionPath}		| 
| | ... | ${ObjectFilterString}	| 
| | ... | ${Authentication}		| 
| | ... | ${ConnectionUsername}	| 
| | ... | ${ConnectionPassword}	| 
| | ... | ${ImportNestedItems}

| | @{Step3Mapping}=	| Create List	| 
| | ... | Email			| mail			| 
| | ... | Department	| countrycode	| 
| | ... | First Name	| givenname		| 
| | ... | Last Name		| sn			| 
| | ... | UniqueID		| objectguid

| | ${IntegrationPointExists}=	| Determine if the Following Integration Point Exists in the current Workspace	| ${IntegrationPoint1}
| | Run Keyword If	| ${${IntegrationPointExists}}	| Delete the following Integration Point using API	| ${IntegrationPoint1}
| | Create Integration Point | ${Step1Fields} | ${Step2Fields} | ${Step3Mapping} | UniqueID

| | Run Import Now on the Following Integration Point	| ${IntegrationPoint1}
| | [Teardown]		| Drop connection to the relativity services api

| Delete the Objects created in the Suite then Close All Browsers
| | [Documentation]	| Delete the Integration Point created in this suite then closes all open browsers.
| | ...				| This is run as the Suite Teardown.
| | Setup connection to the Relativity Services API for Legal Hold	| ${WorkspaceName}

| | Delete the following Integration Point using API	| ${IntegrationPoint1}

| | Drop connection to the relativity services api
| | Close All Browsers


*** Settings ***
| Library	| ${KEYWORD_DIR}/ManualKeywords.py
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${KEYWORD_DIR}/Relativity/Processing/Processing/Relativity_Keywords_General.txt
| Resource	| ${KEYWORD_DIR}/Relativity/Shared/Layouts/LayoutKeywords.txt
| Resource	| ${KEYWORD_DIR}/Relativity/Shared/RelativityKeywords.txt
| Resource	| ${KEYWORD_DIR}/Relativity/Shared/Views/ItemListKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/MiscMethodKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/DatabaseKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/IntegrationPointKeywords.txt