
*** Settings ***
| Documentation 	| Verify that the Job History tab's List View is correct.
| Metadata		    | Authors	| William Sehy
| Force Tags		| in-progress	| team.game_of_holds	| feature.integration_points
| Suite Setup		| Suite Setup Keyword
#| Test Setup		| NONE
| Test Teardown 	| Capture Screenshot If Test Fails
| Suite Teardown	| Delete the Objects created in the Suite then Close All Browsers


*** Variables ***

| ${WorkspaceName}	| LDAP Sync
| ${ConnectionPath}			| testing.corp/CN=Users,DC=testing,DC=corp
| ${ObjectFilterString}		| (objectClass=*)
| ${Authentication}			| FastBind
| ${ConnectionUsername}		| testing\\administrator
| ${ConnectionPassword}		| P@ssw0rd@1
| ${ImportNestedItems}		| Yes
| ${New Job History Button}	| xpath=//a[@class='actionButtonSmall' and text()='New Job History']


*** Test Cases ***

| The New Job History Button is not on the page
| | [Documentation] | Verifies that the "New Job History" Button is not on the page.
| | [Tags]			| automated
| | Go to the Tab	| Job History
| | Page Should Not Contain Element	| ${New Job History Button}	| The "New Job History" Button was present on the page.


| The Default View on the Job History List View is All jobs
| | [Documentation] | Verifies that the default View on the Job History list view page is All jobs
| | [Tags]			| automated
| | Go to the Tab	| Job History
| | Verify that the view dropdown is set to the following value	| All Jobs


| The Views on the Job History List View are correct
| | [Documentation] | Verifies that the Views on the Integration Points list view page are correct.
| | [Tags]			| automated
| | Go to the Tab	| Job History
| | Verify that the View Dropdown only has the following Views	| All Jobs	| All Jobs with Errors


| The Columns on the "All Jobs" View are correct
| | [Documentation] | Verifies that the "All Jobs" View on the Job History list view page has the correct columns.
| | [Tags]			| outdated
| | Go to the Tab	| Job History
| | Set View	| All Jobs
| | Verify that the item list contains the following fields	| 
| | ... | Name
| | ... | Integration Point		| 
| | ... | Job Status			| 
| | ... | Records Imported		| 
| | ... | Records with Errors	| 
| | ... | Start Time (UTC)


| The "All Jobs" View shows Jobs of all Statuses
| | [Documentation] | Verifies that the "All Jobs" View on the Job History list view page shows Jobs with all types of Job Status.
| | [Tags]			| manual	| timing
| | Run the following manual steps:
| | ... | Preconditions:
| | ... | 1. At least one job of each Job Status exists in the workspace
| | ... | 
| | ... | Steps:
| | ... | 1. Go to the Tab	| Job History
| | ... | 2. Ensure that the View is	| All Jobs
| | ... | 
| | ... | Expected Results:
| | ... | Verify that the Item List shows all Jobs in the Workspace


| The Sorting on the "All Jobs" View is correct
| | [Documentation] | Verifies that the "All Jobs" View on the Job History list view page has the correct sorting.
| | [Tags]			| manual	| partially_automated	| sorting
| | Go to the Tab	| Job History
| | Set View	| All Jobs

| | Run the following manual steps:
| | ... | Expected Results:
| | ... | Verify that the Item List is sorted by the following Date Column in Ascending Order	| Start Time (UTC)


| The Columns on the "All Jobs with Errors" View are correct
| | [Documentation] | Verifies that the "All Jobs with Errors" View on the Job History list view page has the correct columns.
| | [Tags]			| outdated
| | Go to the Tab	| Job History
| | Set View	| All Jobs with Errors
| | Verify that the item list contains the following fields	| 
| | ... | Name					| 
| | ... | Integration Point		| 
| | ... | Job Status			| 
| | ... | Records Imported		| 
| | ... | Records with Errors	| 
| | ... | Start Time (UTC)


| The "All Jobs with Errors" View shows only Jobs with error Statuses
| | [Documentation] | Verifies that the "All Jobs with Errors" View on the Job History list view page 
| | ...				| shows only Jobs with the Job Statuses: Completed with errors, Error - job failed.
| | [Tags]			| manual	| timing
| | Run the following manual steps:
| | ... | Preconditions:
| | ... | 1. At least one job of each Job Status exists in the workspace
| | ... | 
| | ... | Steps:
| | ... | 1. Go to the Tab	| Job History
| | ... | 2. Ensure that the View is	| All Jobs with Errors
| | ... | 
| | ... | Expected Results:
| | ... | Verify that the Item List shows only Jobs with the following Job Statuses	| Completed with errors	| Error - job failed.


| The Sorting on the "All Jobs with Errors" View is correct
| | [Documentation] | Verifies that the "All Jobs with Errors" View on the Job History list view page has the correct sorting.
| | [Tags]			| manual	| sorting
| | Run the following manual steps:
| | ... | Preconditions:
| | ... | 1. At least three jobs with errors exist in the workspace
| | ... | 2. Jobs with both Error Job Statuses exist in the workspace
| | ... | 
| | ... | Steps:
| | ... | 1. Go to the Tab	| Job History
| | ... | 2. Ensure that the View is	| All Jobs with Errors
| | ... | 
| | ... | Expected Results:
| | ... | Verify that the Item List is sorted by the following Date Column in Ascending Order	| Start Time (UTC)


| The Name link goes to the Job Details Layout for that Job
| | [Documentation] | Verifies that clicking a Job's Integration Point link goes to the Job Details Layout for that Job.
| | [Tags]			| manual	| partially_automated	| automateable?
| | Go to the Tab	| Job History

| | Run the following manual steps:
| | ... | Steps:
| | ... | 1. Click a Name link
| | ... | 
| | ... | Expected Results:
| | ... | 1. Verify that you are sent to a Job Details Layout
| | ... | 2. Verify that you are on the correct Job


| Error - Job Failed Counts are correct
| | [Documentation] | Verifies that the Records Imported and Records with Errors fields are blank when a Job Fails.
| | [Tags]			| manual
| | Run the following manual steps:
| | ... | Preconditions:
| | ... | 1. Setup a RIP job to fail (make Email the Unique Identifier)
| | ... | 2. Attempt to run the job
| | ... |
| | ... | Steps:
| | ... | 1. Go to the Tab	| Job History
| | ... | 2. Search for the failed RIP job
| | ... | 
| | ... | Expected Results:
| | ... | 1. Verify that the Records Imported value is 0.
| | ... | 2. Verify that the Records with Errors value is 0.


| Completed with errors Counts are correct
| | [Documentation] | Verifies that the Records Imported and Records with Errors fields are correct when a Job runs with error.
| | [Tags]			| manual
| | Run the following manual steps:
| | ... | Preconditions:
| | ... | 1. Setup a RIP job to run with errors (e.g. Unique Identifier is set to Department and all Custodians have the same department)
| | ... | 2. Run the job
| | ... |
| | ... | Steps:
| | ... | 1. Go to the Tab	| Job History
| | ... | 2. Search for the errored RIP job
| | ... | 
| | ... | Expected Results:
| | ... | 1. Verify that the Records Imported value is 1.
| | ... | 2. Verify that the Records with Errors value is X-1, where X is the number of Custodians in the job.


| Completed Counts are Correct
| | [Documentation] | Verifies that 
| | [Tags]			| manual
| | Run the following manual steps:
| | ... | Preconditions:
| | ... | 1. Setup a RIP job to run without errors
| | ... | 2. Run the job
| | ... |
| | ... | Steps:
| | ... | 1. Go to the Tab	| Job History
| | ... | 2. Search for the completed RIP job
| | ... | 
| | ... | Expected Results:
| | ... | 1. Verify that the Records Imported value is X, where X is the number of Custodians in the Job.
| | ... | 2. Verify that the Records with Errors value is 0.


*** Keywords ***

| Verify that the View Dropdown only has the following Views
| | [Documentation]	| This keyword verifies that the View dropdown has only the given views in any order.
| | ...				| This keyword assumes that the View dropdown's ID is "ctl00_viewsDropDown".
| | [ARGUMENTS]		| @{ExpectedViews}
| | Page Should Contain Element	| id=ctl00_viewsDropDown	| The View dropdown has not found.
| | @{ActualViews}=	| Get Element Text	| xpath=//select[@id='ctl00_viewsDropDown']/child::option	| False
| | Sort List	| ${ExpectedViews}
| | Sort List	| ${ActualViews}
| | Lists Should Be Equal	| ${ExpectedViews}	| ${ActualViews}

| Get the values for the following Column from the item list
| | [Documentation]	| Returns all of the values in the given column of the current page's Item List, assumed to have an ID of "ctl00_ctl00_itemList_tableDiv".
| | ...				| [Tags] itemlist
| | [ARGUMENTS]		| ${ColumnHeader}
| | ${_column_headers_cache}=	| Set Variable	| ${EMPTY}	| # Clear the column header cache
| | Unselect frame
| | Select iframe	| ListTemplateFrame
| | ${Table}=	| Find Element	| id=ctl00_ctl00_itemList_tableDiv
| | ${Headers}=	| Get Table Column Headers	| ${Table}
| | ${Column Index}=	| Get From Dictionary	| ${Headers}	| ${ColumnHeader}
| | ${Column Index}=	| Evaluate	| ${Column Index} - 1	| # Dictionary starts from 1, not from 0
| | @{Values}=	| Create list
| | @{Table}=	| Get table | ctl00_ctl00_itemList_listTable | 7
| | :FOR | ${Row} | IN | @{Table}
| | | Append to list | ${Values} | ${Row[${Column Index}].lower()}
| | [Return]	| ${Values}

| Verify that the Item List is sorted by the following Date Column in Ascending Order
| | [Documentation]	| Verifies that the Item List on the current page, assumed to have an ID of "ctl00_ctl00_itemList_tableDiv",
| | ...				| is sorted on the given Date Column in ascending order.
| | ...				| [Tags] itemlist
| | [ARGUMENTS]		| ${ColumnHeader}
| | @{Values}=	| Get the values for the following Column from the item list	| ${ColumnHeader}
| | Log	| Actual Values: ${Values}
| | ${RowCount}=	| Get Length	| ${Values}
| | ${PreviousRowValue}=	| Set Variable	| ${Values[0]}

| | :FOR	| ${RowNum}	| IN RANGE	| 1	| ${RowCount}
| | | ${CurrentRowValue}=	| Set Variable	| ${Values[${RowNum}]}
| | | ${CurrentMonth}	| ${CurrentDay}		| ${CurrentYear}=	| Run Keyword Unless	| "${CurrentRowValue}" == "${SPACE}"	| Split String	| ${CurrentRowValue}	| /	| 2
| | | ${PreviousMonth}	| ${PreviousDay}	| ${PreviousYear}=	| Run Keyword Unless	| "${PreviousRowValue}" == "${SPACE}"	| Split String	| ${PreviousRowValue}	| /	| 2
| | | Run Keyword If	| ("${CurrentRowValue}" == "${SPACE}") and ("${PreviousRowValue}" != "${SPACE}")	| Fail	| The entry "${PreviousRowValue}" was above the entry "${CurrentRowValue}".
| | | ... | ELSE IF 	| ("${CurrentRowValue}" == "${SPACE}") and ("${PreviousRowValue}" == "${SPACE}")	| Log	| Currently in the empty section.
| | | ... | ELSE IF 	| ("${CurrentRowValue}" != "${SPACE}") and ("${PreviousRowValue}" == "${SPACE}")	| Log	| Passed the empty section.
| | | ... | ELSE IF 	| ${CurrentYear} < ${PreviousYear}												| Fail	| The entry "${PreviousRowValue}" was above the entry "${CurrentRowValue}".
| | | ... | ELSE IF 	| (${CurrentYear} == ${PreviousYear}) and (${CurrentMonth} < ${PreviousMonth})	| Fail	| The entry "${PreviousRowValue}" was above the entry "${CurrentRowValue}".
| | | ... | ELSE IF 	| (${CurrentYear} == ${PreviousYear}) and (${CurrentMonth} == ${PreviousMonth}) and (${CurrentDay} < ${PreviousDay})	| Fail	| The entry "${PreviousRowValue}" was above the entry "${CurrentRowValue}".
| | | ${PreviousRowValue}=	| Set Variable	| ${CurrentRowValue}

| Suite Setup Keyword
| | [Documentation]	| Create the Suite Preconditions
| | Setup connection to the relativity database
| | Open and Log in to Legal Hold
| | Setup connection to the Relativity Services API for Legal Hold	| ${WorkspaceName}

# --------------------------------------------------------------------------------------------------------

| | @{Step1Fields}=		| Create List	| 
| | ... | List View Sort Testing
| | ... | LDAP
| | ... | Custodian
| | ... | No

| | @{Step2Fields}=		| Create List | 
| | ... | ${ConnectionPath}		| 
| | ... | ${ObjectFilterString}	| 
| | ... | ${Authentication}		| 
| | ... | ${ConnectionUsername}	| 
| | ... | ${ConnectionPassword}	| 
| | ... | ${ImportNestedItems}

| | @{Step3Mapping}=	| Create List	|
| | ... | Email			| mail			| 
| | ... | Department	| countrycode	| 
| | ... | First Name	| givenname		| 
| | ... | Last Name		| sn			| 
| | ... | UniqueID		| objectguid

| | ${IntegrationPointExists}=	| Determine if the Following Integration Point Exists in the current Workspace	| List View Sort Testing
| | Run Keyword Unless	| ${${IntegrationPointExists}}	| Create LDAP Integration Point | ${Step1Fields} | ${Step2Fields} | ${Step3Mapping} | Append/Overlay | UniqueID

# --------------------------------------------------------------------------------------------------------

| | @{Step1Fields}=	| Create List	| 
| | ... | Integration Point List View Testing
| | ... | LDAP
| | ... | Custodian
| | ... | No

| | ${IntegrationPointExists}=	| Determine if the Following Integration Point Exists in the current Workspace	| Integration Point List View Testing
| | Run Keyword Unless	| ${${IntegrationPointExists}}	| Create LDAP Integration Point | ${Step1Fields} | ${Step2Fields} | ${Step3Mapping} | Append/Overlay | UniqueID

# --------------------------------------------------------------------------------------------------------

| | @{Step1Fields}=	| Create List	| 
| | ... | Integration Point Sort Testing
| | ... | LDAP
| | ... | Custodian
| | ... | No

| | ${IntegrationPointExists}=	| Determine if the Following Integration Point Exists in the current Workspace	| Integration Point Sort Testing
| | Run Keyword Unless	| ${${IntegrationPointExists}}	| Create LDAP Integration Point | ${Step1Fields} | ${Step2Fields} | ${Step3Mapping} | Append/Overlay | UniqueID

# --------------------------------------------------------------------------------------------------------

| | Run Import Now on the Following Integration Point	| Integration Point Sort Testing
| | Run Import Now on the Following Integration Point	| List View Sort Testing
| | Run Import Now on the Following Integration Point	| Integration Point List View Testing
| | [Teardown]		| Drop connection to the relativity services api

| Delete the Objects created in the Suite then Close All Browsers
| | [Documentation]	| Delete the Integration Point created in this suite then closes all open browsers.
| | ...				| This is run as the Suite Teardown.
| | Setup connection to the Relativity Services API for Legal Hold	| ${WorkspaceName}

| | ${IntegrationPointExists}=	| Determine if the Following Integration Point Exists in the current Workspace	| Integration Point Sort Testing
| | Run Keyword If	| ${${IntegrationPointExists}}	| Delete the following Integration Point | Integration Point Sort Testing

| | ${IntegrationPointExists}=	| Determine if the Following Integration Point Exists in the current Workspace	| Integration Point List View Testing
| | Run Keyword If	| ${${IntegrationPointExists}}	| Delete the following Integration Point | Integration Point List View Testing

| | ${IntegrationPointExists}=	| Determine if the Following Integration Point Exists in the current Workspace	| List View Sort Testing
| | Run Keyword If	| ${${IntegrationPointExists}}	| Delete the following Integration Point | List View Sort Testing

| | Drop connection to the relativity services api
| | Close All Browsers


*** Settings ***
| Library	| Collections
| Library	| ${KEYWORD_DIR}/ManualKeywords.py
| Library	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${KEYWORD_DIR}/Relativity/Shared/Views/ItemListKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/NavigationKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/DatabaseKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/GenericKeywords.txt
