
*** Settings ***
| Documentation	| This suite of keywords deals with the Integration Point object and its associated pages and actions.


*** Variables ***


*** Keywords ***

| Get the Unassigned Workspace Fields
| | [Documentation]	| Returns a list of the Workspace Fields from the Unassigned Workspace Fields list box on Step 3 of the Integration Point creation process.
| | Wait Until Page Contains Element	| id=workspace-fields	| 10s	| The unassigned Workspace Fields List Box never loaded.
| | Wait Until Page Contains Element	| xpath=//select[@id='workspace-fields']/child::option	| 10s	| The unassigned Workspace Fields never loaded.
| | @{WorkspaceFields}=	| Get Element Text	| xpath=//select[@id='workspace-fields']/child::option	| False
| | [Return]		| @{WorkspaceFields}

| Verify that the Unassigned Workspace Listbox has the following Fields in order
| | [Documentation]	| Verifies that the given fields are on the Unassigned Workspace listbox in the given order.
| | [ARGUMENTS]		| @{ExpectedFields}
| | @{WorkspaceFields}=	| Get the Unassigned Workspace Fields
| | Log	| Workspace Fields: ${WorkspaceFields}
| | Log	| Expected Fields: ${ExpectedFields}
| | Lists Should Be Equal	| ${WorkspaceFields}	| ${ExpectedFields}

| Get the Assigned Workspace Fields
| | [Documentation]	| Returns a list of the Workspace Fields from the Assigned Workspace Fields list box on Step 3 of the Integration Point creation process.
| | Wait Until Page Contains Element	| id=selected-workspace-fields	| 10s	| The assigned Workspace Fields List Box never loaded.
| | Wait Until Page Contains Element	| xpath=//select[@id='selected-workspace-fields']/child::option	| 10s	| The assigned Workspace Fields never loaded.
| | @{WorkspaceFields}=	| Get Element Text	| xpath=//select[@id='selected-workspace-fields']/child::option	| False
| | [Return]		| @{WorkspaceFields}

| Verify that the Assigned Workspace Listbox has the following Fields in order
| | [Documentation]	| Verifies that the given fields are on the Assigned Workspace listbox in the given order.
| | [ARGUMENTS]		| @{ExpectedFields}
| | @{WorkspaceFields}=	| Get the Assigned Workspace Fields
| | Log	| Workspace Fields: ${WorkspaceFields}
| | Log	| Expected Fields: ${ExpectedFields}
| | Lists Should Be Equal	| ${WorkspaceFields}	| ${ExpectedFields}

| Get the Assigned Source Attributes
| | [Documentation]	| Returns a list of the Workspace Fields from the Assigned Source Attributes list box on Step 3 of the Integration Point creation process.
| | Wait Until Page Contains Element	| id=selected-source-fields	| 10s	| The assigned Source Attributes List Box never loaded.
| | Wait Until Page Contains Element	| xpath=//select[@id='selected-source-fields']/child::option	| 10s	| The assigned Source Attributes never loaded.
| | @{SourceAttributes}=	| Get Element Text	| xpath=//select[@id='selected-source-fields']/child::option	| False
| | [Return]		| @{SourceAttributes}

| Get the Unassigned Source Attributes
| | [Documentation]	| Returns a list of the Workspace Fields from the Unassigned Source Attributes list box on Step 3 of the Integration Point creation process.
| | Wait Until Page Contains Element	| id=source-fields	| 10s	| The unassigned Source Attributes List Box never loaded.
| | Wait Until Page Contains Element	| xpath=//select[@id='source-fields']/child::option	| 10s	| The unassigned Source Attributes never loaded.
| | @{SourceAttributes}=	| Get Element Text	| xpath=//select[@id='source-fields']/child::option	| False
| | [Return]		| @{SourceAttributes}

| Click Workspace Field
| | [Documentation] | A keyword that clicks an item from the (unselected) Workspace Field list. Multiple calls of select can be used with a single click of Move.
| | [ARGUMENTS]		| ${Field}
| | Wait Until Keyword Succeeds	| 20s	| 2s	| Element should be visible	| xpath=//select[@id='workspace-fields']	| The Workspace Fields list box was not visible.
| | Click	| xpath=//select[@id='workspace-fields']/child::option[text()='${Field}']

| Click Selected Workspace Field
| | [Documentation] | A keyword that clicks an item from the Selected Workspace Field list. Multiple calls of select can be used with a single click of Move.
| | [ARGUMENTS]		| ${Field}
| | Wait Until Keyword Succeeds	| 20s	| 2s	| Element should be visible	| xpath=//select[@id='selected-workspace-fields']	| The Selected Workspace Fields list box was not visible.
| | Click	| xpath=//select[@id='selected-workspace-fields']/child::option[text()='${Field}']

| Click Source Attribute Field
| | [Documentation] | A keyword that clicks an item from the (unselected) Source Attribute Field list. Multiple calls of select can be used with a single Click of Move.
| | [ARGUMENTS]		| ${Field}
| | Wait Until Keyword Succeeds	| 20s	| 2s	| Element should be visible	| xpath=//select[@id='source-fields']	| The Source Attribute Fields list box was not visible.
| | Click	| xpath=//select[@id='source-fields']/child::option[text()='${Field}']

| Click Selected Source Attribute Field
| | [Documentation] | A keyword that clicks an item from the (unselected) Source Attribute Field list. Multiple calls of select can be used with a single Click of Move.
| | [ARGUMENTS]		| ${Field}
| | Wait Until Keyword Succeeds	| 20s	| 2s	| Element should be visible	| xpath=//select[@id='selected-source-fields']	| The Selected Source Attribute Fields list box was not visible.
| | Click	| xpath=//select[@id='selected-source-fields']/child::option[text()='${Field}']

| Move the Following Workspace Fields to the Selected List
| | [Documentation] | 
| | [ARGUMENTS]		| @{WorkspaceFields}
| | :FOR	| ${WorkspaceField}	| IN	| @{WorkspaceFields}
| | | Click Workspace Field	| ${WorkspaceField}
| | | Click	| id=add-workspace-field

| Move the Following Workspace Fields from the Selected List
| | [Documentation] | 
| | [ARGUMENTS]		| @{WorkspaceFields}
| | :FOR	| ${WorkspaceField}	| IN	| @{WorkspaceFields}
| | | Click Selected Workspace Field	| ${WorkspaceField}
| | | Click	| id=remove-workspace-field

| Move the Following Source Attribute Fields to the Selected List
| | [Documentation] | 
| | [ARGUMENTS]		| @{AttributeFields}
| | :FOR	| ${AttributeField}	| IN	| @{AttributeFields}
| | | Click Source Attribute Field	| ${AttributeField}
| | | Click	| id=add-source-field

| Move the Following Source Attribute Fields from the Selected List
| | [Documentation] | 
| | [ARGUMENTS]		| @{AttributeFields}
| | :FOR	| ${AttributeField}	| IN	| @{AttributeFields}
| | | Click Selected Source Attribute Field	| ${AttributeField}
| | | Click	| id=remove-source-field

| Map the Following Fields on the Integration Point Step 3 form
| | [Documentation] | A keyword that maps all of the specified fields for a RIP on step 3.
| | [ARGUMENTS]		| @{WorkspaceFieldsAndSourceAttributeFields}
| | :FOR	| ${WorkspaceField}	| ${SourceAttribute}	| IN	| @{WorkspaceFieldsAndSourceAttributeFields}
| | | Move the Following Workspace Fields to the Selected List			| ${WorkspaceField}
| | | Move the Following Source Attribute Fields to the Selected List	| ${SourceAttribute}

| Verify that the following field is not in the following list
| | [Documentation] | A keyword that verifies that the specified field is not present in the specified list.
| | ... 			| The value of ${ListName} should be either: workspace-fields, selected-workspace-fields, selected-source-fields, or source-fields
| | [ARGUMENTS]		| ${FieldName}	| ${ListName}
| | Element should not be visible	| xpath=//select[@id='${ListName}']/child::option[text()='${FieldName}']

| Verify that the Selected Workspace fields are in the following order
| | [Documentation] | A keyword that verifies the fields in the Selected Workspace list are in the desired order.
| | [ARGUMENTS]		| @{ExpectedFields}
| | Wait Until Keyword Succeeds	| 20s	| 2s	| Element should be visible	| xpath=//select[@id='selected-workspace-fields']	| The Selected Fields list box was not visible.
| | @{ActualFields}=	| Get Element Text	| xpath=//select[@id='selected-workspace-fields']/child::option | False 
| | Lists should be Equal	| ${ActualFields}	| ${ExpectedFields}

| Verify that the Selected Source Attribute fields are in the following order
| | [Documentation] | A keyword that verifies the fields in the Selected Source Attribute list are in the desired order.
| | [ARGUMENTS]		| @{ExpectedFields}
| | Wait Until Keyword Succeeds	| 20s	| 2s	| Element should be visible	| xpath=//select[@id='selected-source-fields']	| The Selected Fields list box was not visible.
| | @{ActualFields}=	| Get Element Text	| xpath=//select[@id='selected-source-fields']/child::option | False 
| | Lists should be Equal	| ${ActualFields}	| ${ExpectedFields}

*** Settings ***
| Library	| String
| Library	| Collections
| Library 	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${LDAP_KEYWORD_DIR}/DatabaseKeywords.txt
| Resource	| ${LEGAL_HOLD_KEYWORD_DIR}/MiscMethodKeywords.txt
| Resource	| ${LDAP_KEYWORD_DIR}/DatabaseKeywords.txt
