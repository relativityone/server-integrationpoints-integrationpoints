
*** Settings ***
| Documentation	| This suite of keywords deals with the LDAP Sync objects in the Database.


*** Variables ***


*** Keywords ***

| Determine if the Following Integration Point Exists in the current Workspace
| | [Documentation]	| A keyword designed to determine whether an Integration Point with the given name exists in the workspace. Can be run from anywhere without leaving the page.
| | ... 			| Returns "true" or "false".
| | [ARGUMENTS]		| ${IntegrationPointName}
| | ${IntegrationPointName}=	| Replace String	| ${IntegrationPointName}	| '	| ''
| | ${Exists}=	| Query Workspace Database	| 
| | ... | SELECT 
| | ... | CASE WHEN (COUNT([ArtifactID])>0) THEN 'true' ELSE 'false' END 
| | ... | FROM EDDSDBO.IntegrationPoint
| | ... | Where Name = '${IntegrationPointName}'
| | [Return]		| ${Exists[0][0]}

| Get the ArtifactID of the following Integration Point
| | [Documentation]	| A keyword designed to return the ArtifactID of the Integration Point object with the given name. \n
| | ... 			| A database connection must be made for this to run. \n
| | ... 			| The given Project Name cannot contain any apostrophes or the SQL statement will fail.
| | [ARGUMENTS]		| ${IntegrationPointName}
| | ${IntegrationPointName}=	| Replace String	| ${IntegrationPointName}	| '	| ''
| | ${IntegrationPointID}=	| Query workspace database	| 
| | ... | Select ArtifactID
| | ... | From EDDSDBO.IntegrationPoint
| | ... | Where Name = '${IntegrationPointName}'
| | Should Not Be Empty	| ${IntegrationPointID}	| The Integration Point "${IntegrationPointName}" was not found.
| | [Return]		| ${IntegrationPointID[0][0]}

| Delete the following Integration Point using API
| | [Documentation]	| Deletes an Integration Point object using the API. \n
| | ... 			| A database connection must be made for this to run.
| | [ARGUMENTS]		| ${IntegrationPointName}
| | ${IntegrationPointID}=	| Get the ArtifactID of the following Integration Point	| ${IntegrationPointName}
#| | Delete an artifact with the following identifier	| Integration Point	| ${IntegrationPointID}
| | Given we have a REST Session
| | ${URI}=	| Get URI Variable	| Integration Point
| | Perform a Delete	| ${URI}/${IntegrationPointID}
| | Fail if the Perform a Delete Statement Failed	| The Integration Point "${IntegrationPointName}" was not deleted.

| Delete All Integration Points with the following Name using API
| | [Documentation]	| Deletes all the Integration Points objects with the given Name using the API. \n
| | ... 			| A database connection must be made for this to run.
| | [ARGUMENTS]		| ${IntegrationPointName}
| | Delete all instances of a Following Object Type with the Following Properties	| IntegrationPoint	| Name	| ${IntegrationPointName}

| Determine the RDO Identifier Field on the following Object Type
| | [Documentation]	| Returns the Unique field for the given Object Type.
| | ...				| This field is used in a number of ways on Step 3.
| | [ARGUMENTS]		| ${ObjectType}
| | ${2DGrid}=	| Query Workspace Database	| 
| | ... | SELECT f.DisplayName
| | ... | FROM [EDDSDBO].[Field] f
| | ... | JOIN [ObjectType] ot on ot.DescriptorArtifactTypeID = f.FieldArtifactTypeID
| | ... | WHERE ot.Name = '${ObjectType}'
| | ... | AND f.FieldCategoryID = 2	| # Unique Value
| | Should Not Be Empty	| ${2DGrid}	| No Unique fields were found for the Object Type "${ObjectType}".
| | [Return]		| ${2DGrid[0][0]}

| Get a List of All Non-System Object Types in the Workspace
| | [Documentation]	| Gets a list of all of the Object types in the Workspace, removes the System Object types, and then return the remainder. 
| | ${2DGrid}=	| Query Workspace Database	| 
| | ... | SELECT Name
| | ... | FROM [EDDSDBO].[ObjectType]
| | ... | ORDER BY Name
| | @{ObjectFields}=	| Flatten	| ${2DGrid}		| # Converts from 2D grid to 1D List
| | @{SystemFields}=	| Create List	| 
| | ... | Agent Type			| 
| | ... | Batch					| 
| | ... | Batch Set				| 
| | ... | Code					| 
| | ... | Event Handler			| 
| | ... | Field					| 
| | ... | Folder				| 
| | ... | Group					| 
| | ... | History				| 
| | ... | Install Event Handler	| 
| | ... | Integration Point		| 
| | ... | Layout				| 
| | ... | Markup Set			| 
| | ... | Mass Operation		| 
| | ... | Object Rule			| 
| | ... | Object Type			| 
| | ... | Production			| 
| | ... | Relativity Script		| 
| | ... | Report				| 
| | ... | Search				| 
| | ... | Search Container		| 
| | ... | Search Index			| 
| | ... | Source Provider		| 
| | ... | Tab					| 
| | ... | View					| 
| | ... | Workspace
| | Remove Values From List	| ${ObjectFields}	| @{SystemFields}
| | [Return]		| @{ObjectFields}

| Get the Non-System Fields for the following Object Type
| | [Documentation]	| Gets a list of all of the fields on the given object, removes the five system fields, and then returns the remaining fields.
| | [ARGUMENTS]		| ${ObjectType}
| | ${DocumentQCStatusFieldArtifactID}=	| Query Workspace Database	| 
| | ... | SELECT TOP 1 [ArtifactID]
| | ... | FROM [EDDSDBO].[SystemArtifact]
| | ... | WHERE [SystemArtifactIdentifier] = 'DocumentQCStatusField'

| | ${artifactTypeID}=	| Query Workspace Database	| 
| | ... | SELECT TOP 1 DescriptorArtifactTypeID
| | ... | FROM [EDDSDBO].[ObjectType]
| | ... | WHERE [Name] = '${ObjectType}'

| | ${2DGrid}=	| Query Workspace Database	| 
| | ... | SELECT t.DisplayName
| | ... | FROM 
| | ... | (
| | ... |		SELECT Field.*, 
| | ... |			CASE WHEN Field.FieldTypeID IN (10,13) OR Field.FieldCategoryID IN (3,14) THEN 1 ELSE 0 END AS isSpecial,
| | ... |			CASE WHEN (NOT Field.AssociativeArtifactTypeID IS NULL) AND (Field.AssociativeArtifactTypeID IN (10,40,41) OR Field.AssociativeArtifactTypeID > 1000003 ) THEN 1 ELSE 0 END AS IsDynamic,
| | ... |			FieldLength = ISNULL(MaxLength, 
| | ... |			(SELECT TOP 1 MaxLength FROM Field F WHERE F.FieldCategoryID = 2 AND F.FieldArtifactTypeID = Field.AssociativeArtifactTypeID)),
| | ... |			ArtifactGuids = STUFF((SELECT DISTINCT ';' + CAST(ArtifactGuid AS NVARCHAR(MAX)) COLLATE database_default FROM ArtifactGuid WHERE Field.ArtifactID = ArtifactGuid.ArtifactID FOR XML PATH('')),1,1,'')
| | ... |       FROM Field
| | ... |       WHERE IsArtifactBaseField = 0
| | ... |		AND Field.FieldCategoryID != 6
| | ... |		AND Field.FieldCategoryID != 3
| | ... |		AND Field.FieldArtifactTypeID = ${artifactTypeID[0][0]}
| | ... |		AND ( Field.IsVisible = 1 
| | ... |				OR Field.FieldCategoryID = 1 
| | ... |				OR Field.FieldCategoryID = 8 )
| | ... |		AND Field.ArtifactID != ${DocumentQCStatusFieldArtifactID[0][0]} 
| | ... |		AND NOT (Field.FieldCategoryID = 1 
| | ... |		AND Field.AssociativeArtifactTypeID = 10)
| | ... |       ) t
| | ... | WHERE ((t.isSpecial=0) OR (t.isSpecial=1 AND t.IsDynamic = 1)) AND (NOT t.FieldCategoryID IN(15,14,10,7,8,6,12))
| | ... | ORDER BY DisplayName

| | Should Not Be Empty	| ${2DGrid}	| No Workspace Fields were found for the Object Type "${ObjectType}".
| | @{NonSystemFields}=	| Flatten	| ${2DGrid}		| # Converts from 2D grid to 1D List
| | [Return]		| @{NonSystemFields}

| Wait Until Integration Point Job Runs
| | [Documentation]	| This keyword waits until the given Integration Point's Job finishes running.
| | [ARGUMENTS]		| ${IntegrationPointName}	| ${RunTime}=60s
| | Wait Until Keyword Succeeds	| 20s			| 2s	| Verify that the following Integration Point has a Running Job				| ${IntegrationPointName}
| | Wait Until Keyword Succeeds	| ${RunTime}	| 2s	| Verify that the following Integration Point does not have a Running Job	| ${IntegrationPointName}

| Determine if the following Integration Point has a Currently Running Job
| | [Documentation]	| This keyword searches the database to determine if the given Integration Point currently has a Job that is currently running and then returns true/false.
| | ...				| [Tags] private
| | [ARGUMENTS]		| ${IntegrationPointName}
| | ${Exists}= 	| Query workspace database	| 
| | ... | IF EXISTS(SELECT TOP 1 1
| | ... | FROM [EDDSDBO].[IntegrationPoint] as IP
| | ... | JOIN [EDDS].[eddsdbo].[ScheduleAgentQueue_08C0CE2D-8191-4E8F-B037-899CEAEE493D] as SAQ
| | ... | ON IP.ArtifactID = SAQ.RelatedObjectArtifactID
| | ... | WHERE IP.Name = '${IntegrationPointName}'
| | ... | AND SAQ.LockedByAgentID is not NULL)
| | ... | SELECT 'true'
| | ... | ELSE
| | ... | SELECT 'false'
| | [Return]		| ${Exists[0][0]}

| Verify that the Following Integration Point Exists in the current Workspace
| | [Documentation]	| A keyword that verifies that an Integration Point with the given name exists in the workspace. Can be run from anywhere without leaving the page.
| | [ARGUMENTS]		| ${IntegrationPointName}
| | ${IntegrationPointExists}=	| Determine if the Following Integration Point Exists in the current Workspace	| ${IntegrationPointName}
| | Run Keyword Unless	| ${${IntegrationPointExists}}	| FAIL	| The Integration Point "${IntegrationPointName}" was not found in the current Workspace.

| Verify that the Following Integration Point does not Exist in the current Workspace
| | [Documentation]	| A keyword that verifies that no Integration Point with the given name exists in the workspace. Can be run from anywhere without leaving the page.
| | [ARGUMENTS]		| ${IntegrationPointName}
| | ${IntegrationPointExists}=	| Determine if the Following Integration Point Exists in the current Workspace	| ${IntegrationPointName}
| | Run Keyword If	| ${${IntegrationPointExists}}	| FAIL	| The Integration Point "${IntegrationPointName}" was found in the current Workspace.

| Verify that the following Integration Point has a Running Job
| | [Documentation]	| This keyword searches the database to verify that the given Integration Point currently has a Job in progress.
| | [ARGUMENTS]		| ${IntegrationPointName}
| | ${Exists}=	| Determine if the following Integration Point has a Currently Running Job	| ${IntegrationPointName}
| | Run Keyword Unless	| ${${Exists}}	| Fail	| The Integration Point "${IntegrationPointName}" did not have a Scheduled Job.

| Verify that the following Integration Point does not have a Running Job
| | [Documentation]	| This keyword searches the database to verify that the given Integration Point currently does not have a Job in progress.
| | [ARGUMENTS]		| ${IntegrationPointName}
| | ${Exists}=	| Determine if the following Integration Point has a Currently Running Job	| ${IntegrationPointName}
| | Run Keyword If	| ${${Exists}}	| Fail	| The Integration Point "${IntegrationPointName}" had a Scheduled Job.


*** Settings ***
| Library	| String
| Library	| Collections
| Library 	| ${KEYWORD_DIR}/kWebDriver.py
| Resource	| ${KEYWORD_DIR}/Method/Method_Keywords_API.txt
| Resource	| ${METHOD_KEYWORD_DIR}/MiscMethodKeywords.txt